;------------------
;MODE7_STUFF

MODE7_STUFF	MODULE

	PHP
	SET_AXY16
	LDA	MODE7_FLAG
	BNE	@DO_MODE7
	PLP
	RTL
;-----
@DO_MODE7	LDA	ANGLE
	JSL	GET_SIN	;GET SIN OF ANGLE
	STA	SIN_ANGLE
;
	LDA	ANGLE
	JSL	GET_COS	;GET COS OF ANGLE
	STA	COS_ANGLE
;-----
;CALCULATE A
;
;A = COS (ANGLE) * XSCALE
;
	STA	TEMP_ANGLE
	TAX		;SAVE FOR MULTIPLY
;
	LDA	XSCALE	;IS X SCALE <= 256 ?
	STA	TEMP_SCALE
	JSL	M7_CALC
;
@SET_A	SET_A8
	STA	M7A	;A
	XBA
	STA	M7A
;-----
;CALCULATE B
;
;B = SIN (ANGLE) * XSCALE
;
	SET_A16
	LDA	SIN_ANGLE
	STA	TEMP_ANGLE
	TAX		;SAVE FOR MULTIPLY
;
	LDA	XSCALE	;IS X SCALE <= 256 ?
	STA	TEMP_SCALE
	JSL	M7_CALC
;
@SET_B	SET_A8
	STA	M7B	;B
	XBA
	STA	M7B
;-----
;CALCULATE C
;
;C = -SIN (ANGLE) * YSCALE
;
	SET_A16
	LDA	SIN_ANGLE
	STA	TEMP_X
	TAX		;SAVE FOR MULTIPLY
;
	LDA	YSCALE	;IS Y SCALE <= 256 ?
	STA	TEMP_SCALE
	JSL	M7_CALC
;
@SET_C	STA	TEMP_ACC
	LDA	#0
	SEC
	SBC	TEMP_ACC	; -SIN ( ANGLE )
	SET_A8
	STA	M7C	;C
	XBA	
	STA	M7C
;-----
;CALCULATE D
;
;D = COS (ANGLE) * YSCALE
;
	SET_A16
	LDA	COS_ANGLE
	STA	TEMP_ANGLE
	TAX		;SAVE FOR MULTIPLY
;
	LDA	YSCALE	;IS Y SCALE <= 256 ?
	STA	TEMP_SCALE
	JSL	M7_CALC
;
@SET_D	SET_A8
	STA	M7D	;D
	XBA
	STA	M7D
;-----
	LDA	#$80
	STA	M7X	;CENTER X
	LDA	#$00
	STA	M7X
;
	LDA	#$80
	STA	M7Y	;CENTER Y
	LDA	#$00
	STA	M7Y
;-----
	SET_A16
	STZ	MODE7_FLAG
;-----
@NO_MODE7	PLP
	RTL

	MODEND

;------------------
;M7_CALC
;
;	X = FUNCTION (ANGLE)
;	A = SCALE

M7_CALC	MODULE

	BIT	#$FF00
	BNE	@BIG_SCALE
	TAY
	CPX	#$100	;AT MAX ?
	BEQ	@IS_100A
	CPX	#$8000	;NEGATIVE ANGLE ?
	BGE	@NEG_ANGLE
	JSL	MULTIPLY	;COS (ANGLE) * XSCALE
	XBA
@IS_100A	AND	#$00FF	;JUST LOWER BYTE
	RTL
;-----
@NEG_ANGLE	CPX	#$FF00	;-100 ?
	BEQ	@IS_100D
	LDA	#0
	SEC
	SBC	TEMP_ANGLE	;MAKE POSITIVE
	TAX
	JSL	MULTIPLY
	XBA
@IS_100D	AND	#$00FF
	STA	TEMP_ACC
	LDA	#0
	SEC
	SBC	TEMP_ACC
	RTL
;-----
@BIG_SCALE   	TAY
	CPX	#$100
	BEQ	@IS_100B
	CPY	#$100
	BEQ	@IS_100C
	CPX	#$8000	;NEGATIVE ANGLE ?
	BGE	@NEG_ANGLE2
	JSL	MULTIPLY	;COS (ANGLE) * XSCALE
	XBA
	AND	#$00FF
	STA	TEMP_ACC
;-----
	LDA	TEMP_SCALE
	XBA
	TAY		;USE HIGH BYTE NOW
	LDX	TEMP_ANGLE
	JSL	MULTIPLY
	CLC
	ADC	TEMP_ACC
@IS_100B	RTL
;-----
@IS_100C	TXA
	RTL
;-----
@NEG_ANGLE2	CPX	#$FF00	;-100 ?
	BEQ	@IS_100E
	LDA	#0
	SEC
	SBC	TEMP_ANGLE	;MAKE POSITIVE
	PHA		;SAVE IT
	TAX
	JSL	MULTIPLY
	XBA
	AND	#$00FF
	STA	TEMP_ACC
;-----
	PLX		;GET POSITIVE ANGLE BACK
	LDA	TEMP_SCALE
	XBA
	TAY
	JSL	MULTIPLY
	CLC
	ADC	TEMP_ACC
@IS_100E	STA	TEMP_ACC
	LDA	#0
	SEC
	SBC	TEMP_ACC	;MAKE NEG AGAIN
	RTL
	
	MODEND

;------------------
;MULTIPLY
;
; VALUES IN X AND Y

MULTIPLY	MODULE

	SET_AXY8
	STX	WRMPYA	;SET MULTIPLICAND
	STY	WRMPYB	;SET MULTIPLIER
	NOP
	NOP
	NOP
	NOP
	NOP		;WAIT 8 CYCLES
	LDA	RDMPYH	;GET HIGH
	XBA
	LDA	RDMPYL	;GET LOW
	SET_AXY16
	RTL

	MODEND

;------------------
;M7_LEFT

M7_RATE	EQU	2

M7_LEFT	MODULE

	PHP
	SET_AXY16
;-----
	SEC
	LDA	MAP1_X
	SBC	#M7_RATE
	STA	MAP1_X
;-----
	PLP
	RTL

	MODEND

;------------------
;M7_RIGHT

M7_RIGHT	MODULE

	PHP
	SET_AXY16
;-----
	CLC
	LDA	MAP1_X
	ADC	#M7_RATE
	STA	MAP1_X
;-----
	PLP
	RTL

	MODEND

;------------------
;M7_UP

M7_UP	MODULE

	PHP
	SET_AXY16
;-----
	SEC
	LDA	MAP1_Y
	SBC	#M7_RATE
	STA	MAP1_Y
;-----
	PLP
	RTL

	MODEND

;------------------
;M7_DOWN

M7_DOWN	MODULE

	PHP
	SET_AXY16
;-----
	CLC
	LDA	MAP1_Y
	ADC	#M7_RATE
	STA	MAP1_Y
;-----
	PLP
	RTL

	MODEND

;------------------
;M7_CW -- ROTATE CLOCKWISE

SPIN_RATE	EQU	1

M7_CW	MODULE

	PHP
	SET_AXY16
;-----
	CLC
	LDA	ANGLE
	ADC	#SPIN_RATE
	CMP	#360
	BLT	@ANGLE_OK
	SEC
	SBC	#360
@ANGLE_OK	STA	ANGLE
	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL

	MODEND

;------------------
;M7_CCW -- ROTATE COUNTER CLOCKWISE

M7_CCW	MODULE

	PHP
	SET_AXY16
;-----
	SEC
	LDA	ANGLE
	SBC	#SPIN_RATE
	BCS	@ANGLE_OK
	CLC
	ADC	#360
@ANGLE_OK	STA	ANGLE
	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL

	MODEND

;------------------
;INIT_MODE7 -- SET THE MODE 7 HANDLER

INIT_MODE7	MODULE

	PHP
	SET_AXY16
;-----	
	LDA	#0
	STA	ANGLE
	LDA	#1
	STA	MODE7_FLAG
	LDA	#$100
	STA	XSCALE
	STA	YSCALE
	LDA	#0
	STA	SPIN_CTR
	LDA	#1
	STA	SPIN_SPEED
	LDA	#$8000
	STA	DISTANCE
;-----
	LDA	#<MODE7_CONTROL
	STA	LOOP_HANDLER
	SET_A8
	LDA	#^MODE7_CONTROL
	STA	LOOP_HANDLER+2
;-----
	LDA	#$C0
	STA	M7SEL
;-----
	PLP
	RTL

	MODEND

;------------------
;INIT_DISNEY -- SET THE MODE 7 HANDLER

INIT_DISNEY	MODULE

	PHP
	SET_AXY16
;-----
	STZ	NMI_CTR
	LDA	#0
	STA	ANGLE
	LDA	#1
	STA	MODE7_FLAG
	LDA	#13
	STA	SPIN_SPEED
	LDA	#$8000
	STA	XSCALE
	STA	YSCALE
	LDA	#0
	STA	SPIN_CTR
	LDA	#$8000
	STA	DISTANCE
	LDA	#90
	STA	DIST_CTR
;-----
	LDA	#<SPIN_MICKEY
	STA	LOOP_HANDLER
	SET_A8
	LDA	#^SPIN_MICKEY
	STA	LOOP_HANDLER+2
;-----
	LDA	#$C0
	STA	M7SEL
;-----
	PLP
	RTL

	MODEND


;------------------
;SPIN_MICKEY -- SEE IF MICKEY SHOULD SPIN

SPIN_MICKEY	MODULE

	PHP
	SET_AXY16
;-----
	JSL	M7_SPINX
;-----
	PLP
	RTL
	LDA	NMI_CTR
	CMP	#90	;WAIT TIME
	BLT	@NO_SPIN
	CMP	#90*3
	BEQ	@ACCEL_SPIN
	CMP	#90*4
	BEQ	@ACCEL_SPIN
	CMP	#90*5
	BEQ	@ACCEL_SPIN
	CMP	#90*6
	BEQ	@ACCEL_SPIN
	CMP	#90*7
	BEQ	@ACCEL_SPIN
	CMP	#90*8
	BEQ	@ACCEL_SPIN
	CMP	#90*9
	BEQ	@ACCEL_SPIN
	CMP	#90*10
	BNE	@DO_SPIN
@ACCEL_SPIN	INC	SPIN_SPEED
@DO_SPIN	JSL	M7_SPINX
;-----
@NO_SPIN	PLP
	RTL

	MODEND

;------------------
;ALL_SCALE_DN -- GO FURTHER AWAY

SCALE_RATE	EQU	2

ALL_SCALE_DN	MODULE

	PHP
	SET_AXY16
;-----
	CLC
	LDA	XSCALE
	ADC	#SPIN_RATE
	STA	XSCALE
	STA	YSCALE
 	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL
	
	MODEND

;------------------
;ALL_SCALE_UP -- GET CLOSER

ALL_SCALE_UP	MODULE

	PHP
	SET_AXY16
;-----
	SEC
	LDA	XSCALE
	SBC	#SCALE_RATE
	BCS	@SCALE_OK
	LDA	#0
@SCALE_OK	STA	XSCALE
	STA	YSCALE
 	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL

	MODEND

;------------------
;X_SCALE_DN -- GO FURTHER AWAY

X_SCALE_DN	MODULE

	PHP
	SET_AXY16
;-----
	CLC
	LDA	XSCALE
	ADC	#SPIN_RATE
	STA	XSCALE
 	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL
	
	MODEND

;------------------
;X_SCALE_UP -- GET CLOSER

X_SCALE_UP	MODULE

	PHP
	SET_AXY16
;-----
	SEC
	LDA	XSCALE
	SBC	#SCALE_RATE
	BCS	@SCALE_OK
	LDA	#0
@SCALE_OK	STA	XSCALE
 	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL

	MODEND

;------------------
;Y_SCALE_DN -- GO FURTHER AWAY

Y_SCALE_DN	MODULE

	PHP
	SET_AXY16
;-----
	CLC
	LDA	YSCALE
	ADC	#SPIN_RATE
	STA	YSCALE
 	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL
	
	MODEND

;------------------
;Y_SCALE_UP -- GET CLOSER

Y_SCALE_UP	MODULE

	PHP
	SET_AXY16
;-----
	SEC
	LDA	YSCALE
	SBC	#SCALE_RATE
	BCS	@SCALE_OK
	LDA	#0
@SCALE_OK	STA	YSCALE
 	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL

	MODEND

;------------------
;M7_SPINX -- SPIN AROUND X AXIS

	INCLUDE	GG_SPIN.658

;SPIN_TABLE	DW	$100           		;0
;	DW	$104,$110,$128,$152
;	DW	$18E,$200,$2EC,$5C2
;	DW	$4000		;90
;
;	DW	$FA3E,$FD14,$FE00,$FE72
;	DW	$FEAE,$FED8,$FEF0,$FEFC
;	DW	$FF00	      	;180
;
;	DW	$FEFC,$FEF0,$FED8,$FEAE
;	DW	$FE72,$FE00,$FD14,$FA3E
;		             
;	DW	$4000		;270
;	DW	$5C2,$2EC,$200,$18E
;	DW	$152,$128,$110,$104

M7_SPINX	MODULE

	PHP
	SET_AXY16
;-----
	LDA	NMI_CTR
	CMP	#5*60	;10 SECONDS ?
	BNE	@KEEP_SPINING
	PLP
	JMP	DISNEY_EXIT
;-----
@KEEP_SPINING	LDA	DIST_CTR
	CMP	#0
	BEQ	@STOPPED
	ASL
	TAX
	LDA	SPIN_TABLE,X
	STA	DISTANCE
	DEC	DIST_CTR
;	JMP	@NOT_STOPPED
;-----
@STOPPED	LDA	SPIN_SPEED
	BEQ	@NOT_STOPPED
	CMP	#2
	BGE	@CAN_SLOW
         	LDA	SPIN_CTR
	BNE	@NOT_STOPPED
	STZ	SPIN_SPEED
	JMP	@NOT_STOPPED
@CAN_SLOW    	LDA	NMI_CTR
	BIT	#$07
	BNE	@NOT_STOPPED
	DEC	SPIN_SPEED	
@NOT_STOPPED	LDA	SPIN_CTR
	CLC
	ADC	SPIN_SPEED
	CMP	#360
	BLT	@ANGLE_OK
	SEC
	SBC	#360
@ANGLE_OK	STA	SPIN_CTR
	ASL	A
	TAX
	LDA	SPIN_TABLE,X
	STZ	NEG_FLAG	;ASSUME POSITIVE
	BIT	#$8000
	BEQ	@NOT_NEG
	STA	SPIN_VALUE
	LDA	#1
	STA	NEG_FLAG
	LDA	#0
	SEC
	SBC	SPIN_VALUE	;MAKE POSITIVE
@NOT_NEG	STA	SPIN_VALUE
;-----
	XBA		;A = SPIN MSB
	TAX
	LDA	DISTANCE
	XBA		;B = DIST MSB
	TAY
	JSL	MULTIPLY	;A.MSB * B.MSB
	CMP	#$80	;OVER $8000 ?
	BGE	@OVERFLOW
	XBA		;MOVE TO MSB
	AND	#$FF00	;MASK JUST HIGH BYTE
	STA	XSCALE	;SAVE FOR NOW
;
	LDA	SPIN_VALUE
	XBA		;A = SPIN MSB
	TAX
	LDY	DISTANCE	;B = DIST LSB
	JSL	MULTIPLY	;A.MSB * B.LSB
	CLC
	ADC	XSCALE	;ADD TO XSCALE
	STA	XSCALE
;
	LDX	SPIN_VALUE	;A = SPIN LSB
	LDA	DISTANCE
	XBA		;B = DIST MSB
	TAY
	JSL	MULTIPLY	;A.LSB * B.MSB
	CLC
	ADC	XSCALE
	STA	XSCALE
	LDA	NEG_FLAG
	BEQ	@SET_YSCALE
	LDA	#0
	SEC
	SBC	XSCALE
	STA	XSCALE
	JMP	@SET_YSCALE
;
@OVERFLOW	LDA	#$8000
	STA	XSCALE
;-----
@SET_YSCALE	LDA	DISTANCE
	STA	YSCALE
;-----
;	BIT	#$8000
;	BNE	@OVER
;	ASL	A
;	ASL	A
;	ASL	A
;@OVER	STA	XSCALE
;	LDA	#512*4
;	STA	YSCALE
;-----
 	LDA	#1
	STA	MODE7_FLAG
;-----
	PLP
	RTL

	MODEND

;------------------
;MODE7_CONTROL -- GET USER INPUT TO CONTROL MODE 7 

MODE7_CONTROL	MODULE

	PHP
	SET_AXY16
	STZ	MODE7_FLAG
;-----
	LDA	JOY1_DATA
	BIT	#JOY_ABUTT
	BEQ	@5
	JSL	INIT_MODE7
	LDA	JOY1_DATA
;-----
@5   	BIT	#JOY_XBUTT	;SPIN ON X AXIS
	BEQ	@7
	JSL	M7_SPINX
	LDA	JOY1_DATA
;-----
@7	BIT	#JOY_LBUTT	;SPIN CCW ?
	BEQ	@10
	JSL	M7_CCW
	LDA	JOY1_DATA
;-----
@10	BIT	#JOY_RBUTT	;SPIN CW ?
	BEQ	@20
	JSL	M7_CW
	LDA	JOY1_DATA
;-----
@20	BIT	#JOY_BBUTT	;SCALE BUTTONS ?
	BEQ	@100
;-----
 	BIT	#JOY_LEFT	;BIGGER IN X ?
	BEQ	@30
	JSL	X_SCALE_UP
	LDA	JOY1_DATA
;-----
@30	BIT	#JOY_RIGHT	;SMALLER IN X ?
	BEQ	@40
	JSL	X_SCALE_DN
	LDA	JOY1_DATA
;-----
@40	BIT	#JOY_UP	;BIGGER IN Y ?
	BEQ	@50
	JSL	Y_SCALE_UP
	LDA	JOY1_DATA
;-----
@50	BIT	#JOY_DOWN	;SMALLER IN Y ?
	BEQ	@60
	JSL	Y_SCALE_DN
;-----
@60	PLP
	RTL
;-----
@100 	BIT	#JOY_LEFT	;MOVE LEFT ?
	BEQ	@130
	JSL	M7_RIGHT
	LDA	JOY1_DATA
;-----
@130	BIT	#JOY_RIGHT	;MOVE RIGHT ?
	BEQ	@140
	JSL	M7_LEFT
	LDA	JOY1_DATA
;-----
@140	BIT	#JOY_UP	;MOVE UP ?
	BEQ	@150
	JSL	M7_DOWN
	LDA	JOY1_DATA
;-----
@150	BIT	#JOY_DOWN	;MOVE DOWN ?
	BEQ	@160
	JSL	M7_UP
	LDA	JOY1_DATA
;-----
@160
;-----
	PLP
	RTL

	MODEND

;------------------
;------------------
;------------------

