;------------------
;
;  MACROS.658 -- MACRO FILE
;
;INCLUDE IN SN_MAIN.658
;
;------------------
;SET THE PROCESSOR TO 65816 MODE

SET_16BIT	MACRO

	CLC		;CARRY = 0
	XCE		;SET EMULATION BIT TO 0
	MX	0	;ACC,X,Y ALL 16 BIT

	ENDM

;------------------
;SET THE PROCESSOR TO 6502 MODE

SET_8BIT	MACRO

	SEC		;CARRY = 1
	XCE		;SET EMULATION BIT TO 1
	MX	3	;ACC,X,Y ALL 8 BIT

	ENDM

;------------------
;SET THE ACC,X,Y TO 16 BIT MODE

SET_AXY16	MACRO

	REP	#$30	;M,X SET TO 16 BIT
	MX	0	

	ENDM

;------------------
;SET THE ACC TO 16 BIT MODE

SET_A16	MACRO

	REP	#$20	;M SET TO 16 BIT
	LONGA	ON

	ENDM

;------------------
;SET THE X,Y TO 16 BIT MODE

SET_XY16	MACRO

	REP	#$10	;X SET TO 16 BIT MODE
	LONGI	ON

	ENDM

;------------------
;SET THE ACC,X,Y TO 8 BIT MODE

SET_AXY8	MACRO

	SEP	#$30	;ACC,X,Y SET TO 8 BIT MODE	
	MX	3   	;TELL ASSEMBLER

	ENDM

;------------------
;SET ACC TO 8 BIT MODE

SET_A8	MACRO

	SEP	#$20	;ACC SET TO 8 BIT
	LONGA	OFF

	ENDM

;------------------
;SET X,Y TO 8 BIT MODE

SET_XY8	MACRO

	SEP	#$10	;X,Y SET TO 8 BIT
	LONGI	OFF

	ENDM

	
;------------------
;EXECUTE THE COMMAND SCRIPT

RUN	MACRO

	SET_A8
	SET_XY16
	LDA	#^\1
	PHA
	PLB		;SET DATA BANK
	LDY	#<\1
	JSL	DO_SCRIPT

	ENDM

;------------------
;SCRIPT MACROS

SC_END	MACRO

	DB	0	;END OF SCRIPT

	ENDM

;=====

SC_PAL	MACRO

	DB	1	;SCRIPT CODE
	DT	\1	;SOURCE PALETTE ADDRESS
	DB	\2	;DESTINATION ADDRESS IN PALETTE
	DW	\3	;# OF COLORS TO MOVE

	ENDM

;=====

SC_VRAM	MACRO

	DB	2	;SCRIPT CODE
	DT	\1	;SOURCE ADDRESS
	DW	\2	;DESTINATION ADDRESS IN VRAM
	DW	\3	;# OF BYTES TO MOVE

	ENDM

;=====

SC_JPH 	MACRO

	DB	3	;SCRIPT CODE
	DT	\1	;ADDRESS OF JOYPAD HANDLER LIST

	ENDM

;=====

SC_LVL 	MACRO

	DB	4	;SCRIPT CODE
	DT	\1	;ADDRESS OF LEVEL DATA

	ENDM

;=====

SC_ON 	MACRO

	DB	5	;SCRIPT CODE

	ENDM

;=====

SC_JSL 	MACRO

	DB	6	;SCRIPT CODE
	DT	\1	;ADDRESS OF LONG ROUTINE

	ENDM

;=====

SC_SCRN	MACRO

	DB	7	;SCRIPT CODE
	DW	\1	;SCREEN # TO DRAW TO
	DT	\2	;MAP ADDRESS
	DW	(\3*\4)*2	;SIZE OF MAP IN BYTES
	DW	\3	;MAP WIDTH IN BLOCKS
	DW	\4	;MAP HEIGTH IN BLOCKS
	DT	\5	;BLOCK ADDRESS
	DW	\6	;BLOCK LENGTH
	DW	\7	;SCREEN X
	DW	\8	;SCREEN Y

	ENDM

;=====

SC_MAP	MACRO

	DB	8	;SCRIPT CODE
	DW	\1	;SCREEN # TO DRAW TO
	DW	\2	;MAP START X
	DW	\3	;MAP START Y
	DW	\4	;MAP WIDTH IN BLOCKS
	DW	\5	;MAP HEIGTH IN BLOCKS
	DB	\6
	DT	\7	;MAP ADDRESS
	DW	(\4*\5)*2	;SIZE OF MAP IN BYTES
	DT	\8	;BLOCK ADDRESS
	DW	\9	;BLOCK LENGTH
	DT	\10	;FLOOR ADDRESS
	DT	\11	;CONTOUR ADDRESS

	ENDM

;=====

SC_PXY	MACRO

	DB	9	;SCRIPT CODE
	DW	\1	;STARTING X POSITION ON SCREEN
	DW	\2	;STARTING Y POSITION ON SCREEN

	ENDM

;=====

SC_CLR	MACRO

	DB	10	;SCRIPT CODE
	DB	\1

	ENDM

;=====

SC_CLRV	MACRO

	DB	11	;SCRIPT CODE

	ENDM

;=====

SC_CLR_SB	MACRO

	DB	12	;SCRIPT CODE

	ENDM

;=====

SC_SCH 	MACRO

	DB	13	;SCRIPT CODE
	DT	\1	;ADDRESS OF SCROLL HANDLER ROUTINE

	ENDM

;=====

SC_DCVR	MACRO

	DB	14	;SCRIPT CODE
	DT	\1	;SOURCE ADDRESS
	DW	\2	;DESTINATION ADDRESS IN VRAM
	DW	\3	;# OF BYTES TO MOVE

	ENDM

;=====

SC_AAM	MACRO

	DB	15	;SCRIPT CODE
	DW	\1	;SCREEN # TO DRAW TO
	DW	\2	;MAP START X
	DW	\3	;MAP START Y
	DW	\4	;MAP WIDTH IN BLOCKS
	DW	\5	;MAP HEIGTH IN BLOCKS
	DB	\6
	DT	\7	;MAP ADDRESS
	DW	(\4*\5)*2	;SIZE OF MAP IN BYTES

	ENDM

;=====

SC_CMP	MACRO

	DB	16	;SCRIPT CODE
	DW	\1	;SCREEN # TO DRAW TO
	DW	\2	;MAP START X
	DW	\3	;MAP START Y
	DW	\4	;MAP WIDTH IN BLOCKS
	DW	\5	;MAP HEIGTH IN BLOCKS
	DB	\6
	DT	\7	;MAP ADDRESS
	DW	(\4*\5)*2	;SIZE OF MAP IN BYTES
	DT	\8	;BLOCK ADDRESS
	DW	\9	;BLOCK LENGTH
	DT	\10	;FLOOR ADDRESS
	DT	\11	;CONTOUR ADDRESS

	ENDM

;------------------
;JOYPAD HANDLER MACROS

JP_END	MACRO

	DW	0	;END OF SCRIPT

	ENDM

;=====

JP_TEST	MACRO

	DW	\1	;JOYPAD COMPARE CODE
	DT	\2	;ADDRESS OF ROUTINE TO EXECUTE

	ENDM

;=====

DMA_2_OAM	MACRO

	PHP
	SET_A8
	LDA	#1
	STA	OAM_DMA_FLAG	;DMA ENABLED
	PLP

	ENDM

;=====

DMA_2_CGRAM	MACRO

	PHP
	SET_A8
	LDA	#1
	STA	CG_DMA_FLAG	;DMA ENABLED
	PLP

	ENDM

;=====
;
;PARAMETERS
;1 = DMA HANDLER COMMAND
;	0 = END OF DMA LIST
;	1 = SPRITE DMA LIST
;		PARAMETERS
;		2 = 
;1 = VALUE FOR VMA_INC	1
;2 = SOURCE ADDRESS		3
;3 = DESTINATION ADDRESS	2
;4 = LENGTH OF DATA		2 = 8 BYTES PER ENTRY

DMA_2_VRAM	MACRO

	PHP
	SET_AXY16
	PHX
	LDX	DMA_HEAP_PTR
	LDA	#<\2	;OFFSET OF SOURCE
	STA	DMA_HEAP+2,X	;SAVE SOURCE OFFSET
	LDA	#<\3	;GET OFFSET OF DESTINATION
	STA	DMA_HEAP+5,X	;SAVE DESTINATION OFFSET
	LDA	#<\4	;GET LENGTH
	STA	DMA_HEAP+7,X	;SAVE LENGTH
	SET_A8
	LDA	#2	;SINGLE DMA LIST COMMAND
	STA	DMA_HEAP,X	;SAVE CMD
	LDA	#^\2	;GET BANK OF SOURCE
	STA	DMA_HEAP+4,X	;SAVE SOURCE BANK
	LDA	#<\1	;GET VMA_INC VALUE
	STA	DMA_HEAP+1,X
	STZ	DMA_HEAP+9,X	;SET END OF DMA_HEAP
	SET_A16
	TXA
	CLC
	ADC	#9	;POINT TO END
	STA	DMA_HEAP_PTR	;SAVE NEW POINTER
	PLX
	PLP

	ENDM

;-----
;CHAR ANIMATION MACRO

ani_dma	MACRO

	DL	\1	;SOURCE ADDRESS IN ROM
	DW	\2	;VRAM DEST ADDRESS
	DW	\3	;LENGTH

	ENDM

;-----
;AUDIO CALL MACRO


AUDCALL	MACRO

	JSL	SOUND_SNESDRIVER+rl\1

	ENDM

;-----
;ANIMATION HEADER MACRO


ANIM	MACRO

	DL	\1	;CONTROL ROUTINE
	DL	\2	;HIT ROUTINE
	DL	\3	;ANIM ADDRESS
	DW	\4	;X SCROLL VALUE
	DW	\5	;Y SCROLL VALUE
	
	ENDM


;-----
;COLOR CYCLE LIST MACROS
;
;GLOBAL = 5 BYTES
;REGION = 13 BYTES

CC_GLOBAL	MACRO

	DB	$00	;TYPE: 0 = GLOBAL, 1 = REGIONAL, -1 = END OF LIST
	DB	\1	;STARTING COLOR # 0 - 255
	DB	\2	;LENGTH OF CYCLE
	DB	\3	;CLOCK TICKS BETWEEN COLOR CYCLES
	DB	\4	;CYCLE DIRECTION 0 = UP, 1 = DOWN

	ENDM

;-----

CC_REGION	MACRO

	DB	$01	;TYPE: 0 = GLOBAL, 1 = REGIONAL, -1 = END OF LIST
	DB	\1	;STARTING COLOR # 0 - 255
	DB	\2	;LENGTH OF CYCLE
	DB	\3	;CLOCK TICKS BETWEEN COLOR CYCLES
	DB	\4	;CYCLE DIRECTION 0 = UP, 1 = DOWN
	DW	\5	;REGION X1
	DW	\6	;REGION X2
	DW	\7	;REGION Y1
	DW	\8	;REGION Y2

	ENDM


