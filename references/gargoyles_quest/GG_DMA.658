;------------------
;DO_OAM_DMA -- DMA THE OAM DATA TO OAM RAM

DO_OAM_DMA	MACRO	DMA_NOW

	SET_A8
	LDA	OAM_DMA_FLAG	;NEED TO DO OAM DMA ?
	CMP	#0
	BNE	DMA_NOW\@
	JMP	DMA_DONE\@
;
DMA_NOW\@	STZ	OAMADDL	;WRITE TO $0 IN OAM
	STZ	OAMADDH
	STZ	MDMAEN	;CLEAR DMA CHANNEL
	LDA	#$00
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0
	LDA	#<OAMDATAW	;WRITE TO HERE
	STA	BADMA0
;
	LDA	#<OAM_DATA	;ADDRESS TO READ FROM
	STA	A1ADMAL0	;LOW
	LDA	#>OAM_DATA
	STA	A1ADMAH0	;HIGH
	LDA	#^OAM_DATA
	STA	A1ADMAB0	;BANK
;
	LDA	#<542	;# OF BYTES TO TRANSFER
	STA	DMADATL0	;LOW
	LDA	#>542
	STA	DMADATH0	;HIGH
;
	LDA	#1
	STA	MDMAEN	;START DMA
;-----
DMA_DONE\@	STZ	OAM_DMA_FLAG	;CLEAR IT NOW

	ENDM

;------------------
;DO_VRAM_DMA -- DMA DATA TO VRAM

DO_VRAM_DMA	MACRO

;	SET_XY16
;	LDX	#3 
;VRAM_AGAIN\@	PHX
	SET_A8
	LDA	VRAM_DMA_FLAG	;NEED TO DO VRAM DMA ?
	CMP	#0
	BNE	DMA_NOW\@
DMA_DONE\@	JMP	DMA_EXIT\@
;-----
;LOOK AT FIRST BYTE TO TELL IF IT'S A SINGLE DMA LIST
;OR POINTER TO A SPRITE DMA LIST
DMA_NOW\@	SET_XY16
	LDX	#0	;START AT BEGINNING
	SET_A8
	LDA	#^DMA_HEAP
	PHA
	PLB		;SET BANK OF DMA_HEAP
;
VRAM_DMA_LP\@	SET_A8
	LDA	DMA_HEAP,X	;GET COMMAND
	INX
	CMP	#0
	BEQ	DMA_DONE\@
	CMP	#1	;A SPRITE DMA LIST ?
	BEQ	SPRITE_DMA\@
;-----
SINGLE_DMA\@	LDA	DMA_HEAP,X	;GET VMA_INC VALUE
	STA	VMAINC
             	LDA	#$01
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0
	LDA	#<WVMDATAL	;WRITE TO HERE
	STA	BADMA0
	SET_A16
	LDA	DMA_HEAP+1,X	;GET OFFSET OF DATA
	SET_A8
	STA	A1ADMAL0	;LOW
	XBA
	STA	A1ADMAH0	;HIGH
	LDA	DMA_HEAP+3,X	;GET SOURCE BANK
	STA	A1ADMAB0	;BANK
	SET_A16
	LDA	DMA_HEAP+4,X	;GET DEST
	STA	VMADDL
	LDA	DMA_HEAP+6,X	;GET # OF BYTES OF DATA
	SET_A8
	STA	DMADATL0	;LOW
	XBA
	STA	DMADATH0	;HIGH
	LDA	#1
	STA	MDMAEN	;START DMA
	SET_A16
	TXA
	CLC
	ADC	#8
	TAX
	JMP	VRAM_DMA_LP\@	;DO NEXT IN HEAP
;-----
SPRITE_DMA\@	SET_A8
	LDA	#$80
	STA	VMAINC
	SET_AXY16
	LDA	DMA_HEAP,X	;GET OFFSET OF DMA LIST
	STA	FRAME_ADDR
	INX
	INX
	LDA	DMA_HEAP,X  	;GET BANK OF DMA LIST
	STA	FRAME_ADDR+2
	INX
	INX
;
	LDY	#0
	SET_A8
	LDA	[FRAME_ADDR],Y	;GET # OF DMA TRANSFERS TO DO
	STA	DMA_TOTAL
	INY
;
VRAM_DMA_LP2\@	SET_A8

	STZ	MDMAEN	;CLEAR DMA CHANNEL

	LDA	#$01
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0

	LDA	#<WVMDATAL	;WRITE TO HERE
	STA	BADMA0

	SET_A16
	LDA	[FRAME_ADDR],Y	;GET OFFSET OF DATA IN CHR FILE
	INY
	INY
	SET_A8
	STA	A1ADMAL0	;LOW
	XBA
	STA	A1ADMAH0	;HIGH
	LDA	[FRAME_ADDR],Y	;GET BANK OF DATA
	INY
	INY
	STA	A1ADMAB0	;BANK
;
	SET_A16
	LDA	[FRAME_ADDR],Y	;GET DEST
	CLC
	ADC	OBJ_BASE	;ADD BASE ADDRESS OF SPRITES IN VRAM
	STA	VMADDL
	INY
	INY

     	SET_A16
	LDA	[FRAME_ADDR],Y	;GET # OF BYTES OF DATA
	INY
	INY

	SET_A8
	STA	DMADATL0	;LOW
	XBA
	STA	DMADATH0	;HIGH

	SET_A8
	LDA	#1
	STA	MDMAEN	;START DMA

	LDA	DMA_TOTAL
	SEC
	SBC	#1
	STA	DMA_TOTAL
	CMP	#$00
	BNE	VRAM_DMA_LP2\@
	JMP	VRAM_DMA_LP\@
;-----
DMA_EXIT\@
;	SET_XY16
;	PLX
;	DEX
;	BEQ	TEST_DONE\@
;	JMP	VRAM_AGAIN\@
TEST_DONE\@	SET_A8
	STZ	VRAM_DMA_FLAG	;TURN FLAG OFF NOW
	SET_A16
	STZ	DMA_HEAP_PTR	;RESET THE DMA HEAP	

        	ENDM

;------------------
;DO_CGRAM_DMA -- DMA COLOR DATA TO PALETTE

DO_CGRAM_DMA	MACRO

	SET_A8
	LDA	CG_DMA_FLAG	;NEED TO DO CG-RAM DMA ?
	CMP	#0
	BNE	DMA_NOW\@
	JMP	DMA_DONE\@
;
DMA_NOW\@	STZ	MDMAEN	;CLEAR DMA CHANNEL
	LDA	#$00
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0
	LDA	#<WCGDATA	;WRITE TO HERE
	STA	BADMA0
;
	LDA	#<CG_DATA	;GET ADDRESS OF PALETTE
	STA	A1ADMAL0	;LOW
	LDA	#>CG_DATA
	STA	A1ADMAH0	;HIGH
;
	LDA	#^CG_DATA	;GET DATA BANK OF PALETTE
	STA	A1ADMAB0	;BANK
;
	LDA	#0	;GET DEST ADDRESS IN PALETTE
	STA	CGADD	;SET WRITE ADDRESS
;
	LDA	#<512	;# OF BYTES TO SEND
	STA	DMADATL0	;LOW
	LDA	#>512
	STA	DMADATH0	;HIGH
;
	LDA	#1
	STA	MDMAEN	;START DMA
;-----
DMA_DONE\@

	ENDM

;------------------
;DO_HSC1_DMA -- DMA NEW HORIZONTAL EDGE DATA TO VRAM

DO_HSC1_DMA	MACRO

	SET_A8
	LDA	HSC_DMA_FLAG1	;DO I NEED TO DO THIS ?
	BNE	DO_HSC1\@
	JMP	EXIT\@
;-----
DO_HSC1\@	LDA	#0
	PHA
	PLB		;RESET BANK FOR TRANSFER
;
	LDA	#$81	;DO EDGE DATA
	STA	VMAINC
;
	STZ	MDMAEN	;CLEAR DMA CHANNEL
	LDA	#$01
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0
	STA	WDMAP1	;DMA PARAMETER DATA FOR CHANNEL 1
	STA	WDMAP2	;DMA PARAMETER DATA FOR CHANNEL 2
	STA	WDMAP3	;DMA PARAMETER DATA FOR CHANNEL 3
	LDA	#<WVMDATAL	;WRITE TO HERE
	STA	BADMA0
	STA	BADMA1
	STA	BADMA2
	STA	BADMA3
;
	SET_A16
	LDA	#<SCRN_BUF	;GET ADDRESS OF TOP LEFT EDGE
	STA	A1ADMAL0	;LOW
	CLC
	ADC	#64	;LOWER LEFT EDGE
	STA	A1ADMAL2	;LOW
	CLC
	ADC	#64	;TOP RIGHT EDGE
	STA	A1ADMAL1	;LOW
	CLC
	ADC	#64	;BOTTOM RIGHT EDGE
	STA	A1ADMAL3	;LOW
	SET_A8
	LDA	#^SCRN_BUF	;GET DATA BANK OF SOURCE
	STA	A1ADMAB0	;BANK
	STA	A1ADMAB1	;BANK
	STA	A1ADMAB2	;BANK
	STA	A1ADMAB3	;BANK
;
	SET_A16
	LDA	HSC_DMA_TOP1	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	LDA	#64	;# OF BYTES TO SEND
	STA	DMADATL0	;LOW
	STA	DMADATL1	;LOW
	STA	DMADATL2	;LOW
	STA	DMADATL3	;LOW
;
	SET_A8
	LDA	#1	;TOP LEFT SCREEN
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	HSC_DMA_TOP1	;GET DEST ADDRESS IN VRAM
	CLC
	ADC	#$01
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#2	;TOP RIGHT SCREEN
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	HSC_DMA_BTM1	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#4	;BOTTOM LEFT
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	HSC_DMA_BTM1	;GET DEST ADDRESS IN VRAM
	CLC
	ADC	#$01
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#8	;BOTTOM RIGHT
	STA	MDMAEN	;START DMA
	STZ	HSC_DMA_FLAG1	;CLEAR THE DMA FLAG
;-----
EXIT\@
	
	ENDM

;------------------
;DO_VSC1_DMA -- DMA NEW VERTICAL EDGE DATA TO VRAM

DO_VSC1_DMA	MACRO

	SET_A8
	LDA	VSC_DMA_FLAG1	;DO I NEED TO DO THIS ?
	BNE	DO_VSC1\@
	JMP	EXIT\@
;-----
DO_VSC1\@	LDA	#0
	PHA
	PLB		;RESET BANK FOR TRANSFER
;
	LDA	#$80	;DO EDGE DATA
	STA	VMAINC
;
	STZ	MDMAEN	;CLEAR DMA CHANNEL
	LDA	#$01
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0
	STA	WDMAP1	;DMA PARAMETER DATA FOR CHANNEL 1
	LDA	#<WVMDATAL	;WRITE TO HERE
	STA	BADMA0
	STA	BADMA1
;
	SET_A16
	LDA	#<SCRN_BUF+$100	;GET ADDRESS OF LEFT SCREEN DATA
	STA	A1ADMAL0	;LOW
	LDA	#<SCRN_BUF+$180	;GET ADDRESS OF RIGHT SCREEN DATA
	STA	A1ADMAL1	;LOW
	SET_A8
	LDA	#^SCRN_BUF	;GET DATA BANK OF SOURCE
	STA	A1ADMAB0	;BANK
	STA	A1ADMAB1	;BANK
;
	SET_A16
	LDA	VSC_DMA_LEFT1	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	LDA	#128	;# OF BYTES TO SEND
	STA	DMADATL0	;LOW
	STA	DMADATL1	;LOW
;
	SET_A8
	LDA	#1	;LEFT SCREEN
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	VSC_DMA_RHT1	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#2	;TOP RIGHT SCREEN
	STA	MDMAEN	;START DMA
;
	STZ	VSC_DMA_FLAG1	;CLEAR THE DMA FLAG
;-----
EXIT\@
	ENDM

;------------------
;DO_HSC2_DMA -- DMA NEW HORIZONTAL EDGE DATA TO VRAM

DO_HSC2_DMA	MACRO

	SET_A8
	LDA	HSC_DMA_FLAG2	;DO I NEED TO DO THIS ?
	BNE	DO_HSC2\@
	JMP     EXIT\@
;-----
DO_HSC2\@	LDA	#0
	PHA
	PLB		;RESET BANK FOR TRANSFER
;
	LDA	#$81	;DO EDGE DATA
	STA	VMAINC
;
	STZ	MDMAEN	;CLEAR DMA CHANNEL
	LDA	#$01
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0
	STA	WDMAP1	;DMA PARAMETER DATA FOR CHANNEL 1
	STA	WDMAP2	;DMA PARAMETER DATA FOR CHANNEL 2
	STA	WDMAP3	;DMA PARAMETER DATA FOR CHANNEL 3
	LDA	#<WVMDATAL	;WRITE TO HERE
	STA	BADMA0
	STA	BADMA1
	STA	BADMA2
	STA	BADMA3
;
	SET_A16
	LDA	#<SCRN_BUF+$200	;GET ADDRESS OF TOP LEFT EDGE
	STA	A1ADMAL0	;LOW
	CLC
	ADC	#64	;LOWER LEFT EDGE
	STA	A1ADMAL2	;LOW
	CLC
	ADC	#64	;TOP RIGHT EDGE
	STA	A1ADMAL1	;LOW
	CLC
	ADC	#64	;BOTTOM RIGHT EDGE
	STA	A1ADMAL3	;LOW
	SET_A8
	LDA	#^SCRN_BUF	;GET DATA BANK OF SOURCE
	STA	A1ADMAB0	;BANK
	STA	A1ADMAB1	;BANK
	STA	A1ADMAB2	;BANK
	STA	A1ADMAB3	;BANK
;
	SET_A16
	LDA	HSC_DMA_TOP2	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	LDA	#64	;# OF BYTES TO SEND
	STA	DMADATL0	;LOW
	STA	DMADATL1	;LOW
	STA	DMADATL2	;LOW
	STA	DMADATL3	;LOW
;
	SET_A8
	LDA	#1	;TOP LEFT SCREEN
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	HSC_DMA_TOP2	;GET DEST ADDRESS IN VRAM
	CLC
	ADC	#$01
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#2	;TOP RIGHT SCREEN
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	HSC_DMA_BTM2	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#4	;BOTTOM LEFT
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	HSC_DMA_BTM2	;GET DEST ADDRESS IN VRAM
	CLC
	ADC	#$01
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#8	;BOTTOM RIGHT
	STA	MDMAEN	;START DMA
	STZ	HSC_DMA_FLAG2	;CLEAR THE DMA FLAG
;-----
EXIT\@
	ENDM

;------------------
;DO_VSC2_DMA -- DMA NEW VERTICAL EDGE DATA TO VRAM

DO_VSC2_DMA	MACRO

	SET_A8
	LDA	VSC_DMA_FLAG2	;DO I NEED TO DO THIS ?
	BNE	DO_VSC2\@
	JMP	EXIT\@
;-----
DO_VSC2\@	LDA	#0
	PHA
	PLB		;RESET BANK FOR TRANSFER
;
	LDA	#$80	;DO EDGE DATA
	STA	VMAINC
;
	STZ	MDMAEN	;CLEAR DMA CHANNEL
	LDA	#$01
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0
	STA	WDMAP1	;DMA PARAMETER DATA FOR CHANNEL 1
	LDA	#<WVMDATAL	;WRITE TO HERE
	STA	BADMA0
	STA	BADMA1
;
	SET_A16
	LDA	#<SCRN_BUF+$300	;GET ADDRESS OF LEFT SCREEN DATA
	STA	A1ADMAL0	;LOW
	LDA	#<SCRN_BUF+$380	;GET ADDRESS OF RIGHT SCREEN DATA
	STA	A1ADMAL1	;LOW
	SET_A8
	LDA	#^SCRN_BUF	;GET DATA BANK OF SOURCE
	STA	A1ADMAB0	;BANK
	STA	A1ADMAB1	;BANK
;
	SET_A16
	LDA	VSC_DMA_LEFT2	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	LDA	#128	;# OF BYTES TO SEND
	STA	DMADATL0	;LOW
	STA	DMADATL1	;LOW
;
	SET_A8
	LDA	#1	;LEFT SCREEN
	STA	MDMAEN	;START DMA
;
	SET_A16
	LDA	VSC_DMA_RHT2	;GET DEST ADDRESS IN VRAM
	STA	VMADDL	;SET VRAM WRITE ADDRESS
;
	SET_A8
	LDA	#2	;TOP RIGHT SCREEN
	STA	MDMAEN	;START DMA
;
	STZ	VSC_DMA_FLAG2	;CLEAR THE DMA FLAG
;-----
EXIT\@

	ENDM

;------------------
;------------------
;------------------
;------------------
