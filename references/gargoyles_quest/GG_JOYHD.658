
;------------------
;
;GG_JOYHD.658
;
;  THE JOYPAD HANDLER LOOKS FOR CERTIAN JOYPAD INPUT AND EXECUTES ROUTINES
;IN RESPONSE TO THE INPUTS. THE VARIABLE JP_LIST CONTAINS THE ADDRESS OF
;THE JOYPAD LIST. THE JOYPAD LIST CONSISTS OF A BUTTON PRESS CODE AND
;THE ADDRESS OF THE ROUTINE TO EXECUTE IF THE INPUT OCCURES.
;
;
;------------------
;JOYPAD EQUATES

JOY_ABUTT	EQU	$0080
JOY_BBUTT	EQU	$8000
JOY_XBUTT	EQU	$0040
JOY_YBUTT	EQU	$4000
JOY_LBUTT	EQU	$0020
JOY_RBUTT	EQU	$0010
JOY_START	EQU	$1000
JOY_SELECT	EQU	$2000
JOY_UP	EQU	$0800
JOY_DOWN	EQU	$0400
JOY_LEFT	EQU	$0200
JOY_RIGHT	EQU	$0100

;------------------

;	SECTION	GG_CODE,?	;PAGE00_GROUP

;------------------
;JOYPAD LISTS

DISNEY_JPL	JP_TEST	JOY_START,DISNEY_EXIT	;PRESS START TO EXIT DISNEY SCREEN
	JP_END

TITLE_JPL	JP_TEST	JOY_START,TITLE_EXIT	;PRESS START TO EXIT TITLE
	JP_TEST	JOY_UP,PREV_LVL
	JP_TEST	JOY_DOWN,NEXT_LVL
	JP_END


LVL1_JPL	JP_TEST	JOY_START,LVL1_EXIT	;PRESS START TO EXIT LEVEL 1
	JP_END

LVL2_JPL	JP_TEST	JOY_START,LVL2_EXIT	;PRESS START TO EXIT LEVEL 2
	JP_END

MODE7_JPL	JP_TEST	JOY_START,LVL1_EXIT	;PRESS START TO EXIT LEVEL 1
;	JP_TEST	JOY_UP,M7_UP
;	JP_TEST	JOY_DOWN,M7_DOWN
;	JP_TEST	JOY_LEFT,M7_LEFT
;	JP_TEST	JOY_RIGHT,M7_RIGHT
;	JP_TEST JOY_LBUTT,M7_CCW
;	JP_TEST	JOY_RBUTT,M7_CW
	JP_END


;------------------
;JP_HANDLER -- LOOK FOR INPUT AND EXECUTE ROUTINES

JP_HANDLER	MODULE

	PHP
	PHB
;-----
	SET_A8
	SET_XY16
	LDX	JP_LIST	;GET OFFSET
	LDA	JP_LIST+2	;GET DATA BANK
	PHA
	PLB
	SET_A16
@LOOP	LDA	$0,X	;GET TEST VALUE
	CMP	#0
	BEQ	JP_DONE
	BIT	DB1_DATA	;TEST INPUT DATA
	BNE	JP_DOIT
	TXA
	CLC
	ADC	#5	;SKIP TO NEXT JP CMD
	TAX
	JMP	@LOOP
;-----
JP_DOIT	INX
	INX
	PHX		;SAVE X ADDRESS FOR LATER
	PHP
	PHB
	PHK		;PUSH PROGRAM BANK
	PER	@RETURN-1	;SAVE RETURN ADDRESS
;
	SET_A8
	LDA	$2,X	;GET BANK
	PHA		;PUSH PROGRAM BANK ADDRESS
	SET_A16
	LDA	$0,X	;GET ADDRESS OF ROUTINE
	SEC
	SBC	#1
	PHA		;PUSH ADDRESS
	RTL		;GO THERE NOW
;-----
@RETURN
	PLB
	PLP
	PLA
	CLC
	ADC	#3	;
	TAX
	JMP	@LOOP
;-----
JP_DONE	PLB
	PLP
	RTL

	MODEND

;------------------
;READ_JOYPAD -- READ THE JOYPAD

READ_JOYPAD	MODULE

	PHP
	SET_AXY16
	LDA	JOY1_DATA
	STA	OLD1_DATA		;LAST INPUT
	SET_A8

RJ_AGAIN	LDA	HVBJOY
	BIT	#$01
	BNE	RJ_AGAIN
;
	LDA	JOY1H
	XBA
	LDA	JOY1L
	SET_A16
	STA	JOY1_DATA		;RAW DATA
;-----
;NOW CREATE THE DEBOUNCED DATA
	EOR	OLD1_DATA		;MASK WITH LAST INPUT
	AND	JOY1_DATA		;KEEP ONLY NEW INPUT
	STA	DB1_DATA		;SAVE DEBOUNCED DATA
;-----
	LDA	JOY1_DATA
	LDX	IN_BUF_PTR
	STA	IN_BUF,X		;STORE NEW INPUT DATA
	INX
	INX
	CPX	#60*2		;AT END OF BUFFER ?
	BNE	@PTR_OK
	LDX	#0
@PTR_OK	STX	IN_BUF_PTR		;SAVE NEW POINTER
;-----
	PLP
	RTL

	MODEND
;------------------
;PATTERN_CK -- LOOK FOR AN INPUT PATTERN
;
; LOAD PATTERN TABLE ADDRESS INTO PATTERN

PATTERN_CK	MODULE

	PHP
	SET_AXY16
;-----
	LDA	IN_BUF_PTR		;GET CURRENT POINTER
	LDY	#0
	SEC
	SBC	[PATTERN],Y		;GO BACK 1/60 SEC TICKS * 2
	INY
	INY			;SKIP TO 1ST DATA
	BCS	@GT_ZERO
	ADC	#60*2		;WRAP AROUND
@GT_ZERO	STA	PAT_PTR		;SAVE IT
;-----
	LDX	PAT_PTR		;INPUT POINTER
@PATTERN_LP	LDA	IN_BUF,X		;GET AN INPUT
	CMP	[PATTERN],Y		;PATTERN MATCH ?
	BEQ	@MATCH1
	CPX	IN_BUF_PTR		;END OF BUFFER ?
	BEQ	@NO_PATTERN
	INX
	INX
	CPX	#60*2
	BNE	@PATTERN_LP
	LDX	#0
	JMP	@PATTERN_LP
;-----
;NOW SEE IF IT REPEATS LESS THEN THE MAXIMUM NUMBER
@MATCH1	STY	TEMP_Y		;SAVE Y FOR NOW
	LDY	#0
@COUNT_LP	CPX	IN_BUF_PTR		;END OF BUFFER
	BEQ	@NO_PATTERN
	INX
	INX
	CPX	#60*2		;END OF BUFFER
	BNE	@NO_WRAP1
        	LDX	#0
@NO_WRAP1	CMP	IN_BUF,X		;SAME INPUT ?
	BNE	@NEW_INPUT
	INY
	JMP	@COUNT_LP
;-----
@NEW_INPUT	TYA			;# OF MATCHES
	LDY	TEMP_Y
	INY
	INY
	CMP	[PATTERN],Y		;COMPARE TO MAX # OF MATCHES
	BLT	@COUNT_OK		;PATTERN MATCH SHORT ENOUGH
	LDY	TEMP_Y		;RELOAD POINTER TO PATTERN
	JMP	@PATTERN_LP		;KEEP LOOKING
;-----
@COUNT_OK	INY
	INY			;POINT TO NEXT PATTERN
	LDA	[PATTERN],Y		;GET NEXT PATTERN
	CMP	#-1		;END OF PATTERN ?
	BEQ	@HAVE_MATCH
;
	LDA	IN_BUF,X		;MATCHES THE NEXT PATTERN ?
	CMP	[PATTERN],Y
	BEQ	@MATCH1		;YES
	LDY	#2
	JMP	@PATTERN_LP		;START ALL OVER
;-----
@HAVE_MATCH	PLP
	SEC			;CARRY SET = MATCHED
	RTL
;-----
@NO_PATTERN	PLP
	CLC			;CARRY CLEAR = NO MATCH
	RTL

	MODEND

;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------

	
