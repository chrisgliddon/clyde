
DEBUG_ON	EQU	1

LEVEL01_ART	EQU	0	;ANCIENT CASTLE 1 = FINAL ART, 0 = MARIO ART
LEVEL02_ART	EQU	1	;ROOKERY 1 = FINAL ART, 0 = MARIO ART
LEVEL03_ART	EQU	1	;ATRIUM 1 = FINAL ART, 0 = MARIO ART
LEVEL04_ART	EQU	0	;ROOFTOP 1 = FINAL ART, 0 = MARIO ART
LEVEL05_ART	EQU	0	;MUSEUM 1 = FINAL ART, 0 = MARIO ART
LEVEL06_ART	EQU	0	;SUBWAY 1 = FINAL ART, 0 = MARIO ART
LEVEL07_ART	EQU	0	;HYDROPLANT 1 = FINAL ART, 0 = MARIO ART
BONUS_ART	EQU	0	;CLOCK TOWER 1 = FINAL ART, 0 = MARIO ART

;------------------
;
;  SN_MAIN.658 -- SNES MAIN PROGRAM FILE.
;
;COMPILE THIS FILE TO PRODUCE EXECUTABLE CODE.
;
;11/9/93 BY PETER FOKOS
;
;------------------
;
;  SNES_ANI.658 -- ANIMATION DISPLAY PROGRAM
;
;12/27/93 BY PETER FOKOS
;
;------------------

	INCLUDE	MACROS.658	;MACRO FILE
	INCLUDE	GG_DMA.658	;DMA ROUTINES IN MACROS
	INCLUDE	GROUPS.658	;GROUP DEFINITIONS
	INCLUDE PPU_REG.658	;PPU AND CPU REGISTER EQUATES
	INCLUDE	SNESAUD.H	;SNES AUDIO INCLUDE FILE

;------------------
;VARIABLES AREA

	SECTION	ZERO_PAGE,ZERO_GROUP

;-----
;LONG INDIRECT VARIABLES HERE
;DIRECT PAGE NEEDS TO BE SET AND ASSUMED FOR THESE TO WORK

FRAME_ADDR	DS	4	;ADDRESS OF FRAME DATA

JP_LIST	DS	3	;ADDRESS OF JOYPAD HANDLER

THIS_OBJ_ANIM	DS	4	;ADDRESS OF CURRENT OBJ'S ANIM LIST

TEXT_ADDR	DS	3	;ADDRESS OF TEXT STRING

MAP_IN_ROM	DS	3	;ADDRESS OF MAP IN ROM
MAP_IN_RAM	DS	3	;ADDRESS OF MAP IN RAM

BLK_IN_ROM	DS	3	;ADDRESS OF BLOCK IN ROM
BLK_IN_RAM	DS	3	;ADDRESS OF BLOCK IN RAM

FLR_IN_ROM	DS	3	;ADDRESS OF FLOOR IN ROM
FLR_IN_RAM	DS	3	;ADDRESS OF FLOOR IN RAM

CON_IN_ROM	DS	3	;ADDRESS OF CONTOUR IN ROM

MAP1_IN_RAM	DS	3	;ADDRESS OF MAP IN RAM
MAP2_IN_RAM	DS	3	;ADDRESS OF MAP IN RAM
MAP3_IN_RAM	DS	3	;ADDRESS OF MAP IN RAM
MAP4_IN_RAM	DS	3	;ADDRESS OF MAP IN RAM

BLK1_IN_RAM	DS	3	;ADDRESS OF BLK IN RAM
BLK2_IN_RAM	DS	3	;ADDRESS OF BLK IN RAM
BLK3_IN_RAM	DS	3	;ADDRESS OF BLK IN RAM
BLK4_IN_RAM	DS	3	;ADDRESS OF BLK IN RAM

FLR1_IN_RAM	DS	3	;ADDRESS OF FLOOR IN RAM
FLR2_IN_RAM	DS	3	;ADDRESS OF FLOOR IN RAM
FLR3_IN_RAM	DS	3	;ADDRESS OF FLOOR IN RAM
FLR4_IN_RAM	DS	3	;ADDRESS OF FLOOR IN RAM

PAL_IN_RAM	DS	3	;ADDRESS OF PALETTE IN RAM

SC_MAP_ADD	DS	3	;ADDRESS OF MAP BEING USED
SC_BLK_ADD	DS	3	;ADDRESS OF BLOCK BEING USED

SCRL_HANDLER	DS	3	;SCROLL HANDLER ROUTINE

LOOP_HANDLER	DS	3	;MAIN LOOP ROUTINE HANDLER

CA1_ADDRESS	DS	4	;LOCATION OF CHAR ANIM DATA #1

GOL_TRAJ	DS	4	;GOLIATH TRAJECTORY TABLE

DOOR_ADDR	DS	4	;ADDRESS OF DOOR TABLES THIS LEVEL

HIT_ADDR	DS	4	;ADDRESS OF HIT TABLE BEING USED

DCMP_SRC	DS	4	;DECOMPRESS SOURCE BUFFER
DCMP_DEST	DS	4	;DECOMPRESS DESTINATION BUFFER

PATTERN	DS	4	;ADDRESS OF PATTERN FOR PATTERN_CK

;-----
; *** OBJECT DATA AREA ***

	SECTION	OBJ_PAGE,OBJ_GROUP

OBJS	EQU	32	;MAXIMUM # OF OBJECTS

OBJ_CONTROL	DS	OBJS*4	;LONG ADDRESS OF OBJ CONTROL ROUTINE
OBJ_HIT	DS	OBJS*4	;LONG ADDRESS OF OBJ HIT ROUTINE
OBJ_HIT_TBL	DS	OBJS*4	;LONG ADDRESS OF OBJ ANIM HIT TABLE
OBJ_ANIM	DS	OBJS*4	;LONG ADDRESS OF OBJ ANIM BEING PLAYED
OBJ_FRAME	DS	OBJS*4	;LONG ADDRESS OF OBJ ANIM FRAME DATA

THIS_OBJ	DS	2	;OBJ# x 2 BEING WORKED ON

TOTAL_OBJS	DS	2	;TOTAL # OF OBJECTS RUNNING

OBJ_WORD	DS	2	;CURRENT OBJECT WORD POINTER
OBJ_DWORD	DS	2	;CURRENT OBJECT DOUBLE WORD POINTER

OBJ_DX	DS	2	;DELTA X OF THIS FRAME FOR GOLIATH
OBJ_DY	DS	2	;DELTA Y OF THIS FRAME FOR GOLIATH
ABS_DY	DS	2	;ABSOLUTE VALUE  OF DELTA Y
OBJ_XSPEED	DS	2	;SPEED OF GOLAITH
OBJ_YSPEED	DS	2	;SPEED OF GOLAITH
FLAP_FLAG	DS	2	;0 = CAN FLAP, 1 = ALREADY FLAPPED


OBJ_X	DS	OBJS*2	;MAP X OF OBJECTS
OBJ_Y	DS	OBJS*2	;MAP Y OF OBJECTS
OBJ_FLAG	DS	OBJS*2	;OBJECT ATTRIBUTE DATA
OBJ_TIME	DS	OBJS*2	;TIME TO RUN CONTROL ROUTINE AGAIN
OBJ_CON	DS	OBJS*2	;OBJECT CONTOUR VALUE FOR THIS POSITION

OBJ_SPRITE	DS	OBJS	;STARTING SPRITE # OF THIS OBJECT
OBJ_SPR_SIZE	DS	OBJS	;SPRITE SIZE USED BY OBJECT ANIM
OBJ_MAX_SPR	DS	OBJS	;MAX # OF SPRITES USED BY OBJECT ANIM
OBJ_WIDTH	DS	OBJS	;WIDTH OF OBJECT
OBJ_HEIGTH	DS	OBJS	;HEIGTH OF OBJECT
OBJ_FLIP	DS	OBJS	;FLIP DATA FOR EACH OBJECT

NEW_OBJ_X	DS	2	;MAP X OF NEW OBJECT
NEW_OBJ_Y	DS	2	;MAP Y OF NEW OBJECT
NEW_OBJ_CTRL	DS	4	;LONG ADDRESS OF NEW OBJ CONTROL ROUTINE
NEW_OBJ_HIT	DS	4	;LONG ADDRESS OF NEW OBJ HIT ROUTINE
NEW_OBJ_ANIM	DS	4	;LONG ADDRESS OF NEW OBJ ANIM BEING PLAYED
			

;-----
;FADE VARIABLES

FADE_FLAG	DS	1	;BIT 7: 0 = OFF, 1 = ON
			;BIT 6: 0 = INC, 2 = DEC
			;BIT 0-3 = BRIGHTNESS

;-----


ANIM_PTR	DS	2	;POINTER INTO ANIMATION COMMANDS

ANIM_FRAME	DS	2

MAX_SPRITE	DS	1	;MAX # SPRITES IN THIS OBJECT
SPRITE_SIZE	DS	1	;SIZE OF SPRITE TO USE

FLIP_DATA	DS	1	;SPRITE FLIP DATA
FLIP_WIDTH	DS	2	;WIDTH OF OBJ
FLIP_HEIGTH	DS	2	;HEIGTH OF OBJ
SPRITE_COLOR	DS	1	;COLOR DATA FOR SUB-SPRITE
FRAME_X	DS	1	;WHERE TO DRAW SPRITE
FRAME_Y	DS	1	;WHERE TO DRAW SPRITE

EXTRA_SPRITE	DS	1	;EXTRA SPRITE TO CLEAR THIS FRAME

X_OFF	DS	2	;X OFFSET OF SPRITE
Y_OFF	DS	2	;Y OFFSET OF SPRITE

;-----
; *** JOYPAD INPUT DATA ***

JOY1_DATA	DS	2	;JOYPAD 1 DATA HERE
OLD1_DATA	DS	2	;LAST INPUT
DB1_DATA	DS	2	;DEBOUNCED DATA

IN_BUF_PTR	DS	2	;POINTER INTO INPUT BUFFER
IN_BUF	DS	60*2	;INPUT BUFFER

PAT_PTR	DS	2	;POINTER INTO PATTERN

;-----

SS_FLAG	DS	1	;SINGLE STEP FLAG 0 = OFF, 1 = ON

;-----

THIS_SPRITE	DS	1	;SPRITE # BEING WORKED ON
THIS_SPRITE_MSB	DS	1	;POINT TO BYTE WHERE MSB IS
THIS_SPRITE_BYTE	DS	1	;PICK MASK USING THIS

SPRITE_X	DS	1	;X POSITION OF SPRITE
SPRITE_Y	DS	1	;Y POSITION OF SPRITE
SPRITE_DIR	DS	1	;DIR DATA

SPRITE_BLOCK	DS	2
SPRITE_MAP	DS	2
SPRITE_CHAR	DS	2

SPRITE_ADDR	DS	4

NMI_CTR	DS	2

;-----

SCROLL_X	DS	2	;SCROLL X POSITION
SCROLL_Y	DS	2	;SCROLL Y POSITION
SCROLL_VRAM	DS	2	;ADDRESS OF CURRENT ROW IN VRAM

START_BUTT	DS	2

;-----

MAP_PC	DS	2	;PRIORITY AND COLOR BITS
MAP_FB	DS	2	;FLIP BITS
SCRN_HEIGTH	DS	2
SCRN_WIDTH	DS	2

;-----

TITLE_FLAG	DS	1	;0 = OFF 1 = ON

;-----
;TEXT VARIABLES

TEXT_X	DS	1	;TEXT START X
TEXT_Y	DS	1	;TEXT START Y

TEXT_PTR	DS	2	;POINTER INTO SCRN_BUF

TEXT_SIZE	DS	1	;CHAR SIZE OF TEXT
TEXT_PAL	DS	2	;PALETTE # OF TEXT
CHAR_BASE	DS	2	;BASE CHAR # OF ASCII TEXT

;-----
; *** SCRIPT DATA AREA ***

SCRIPT_PTR	DS	2	;POINTER INTO CURRENT SCRIPT

SCRN_PTR	DS	2	;SCREEN # * 2
SCRN_PTR3	DS	2	;SCREEN # * 3


MAP_BYTES	DS	2	;# OF BYTES TO MOVE

BLK_BYTES	DS	2	;# OF BYTES TO MOVE

SCRN_OFF	DS	2	;OFFSET INTO SCRN_BUF

SCRN_X	DS	2	;DRAW_SCRN START X
SCRN_Y	DS	2	;DRAW_SCRN START Y

MAP_OFF	DS	2	;OFFSET INTO MAP

MAP_STARTX	DS	2	;START X OF MAP BEING DRAWN
MAP_STARTY	DS	2	;START Y OF MAP BEING DRAWN
	
MAP_WIDTH	DS	2	;WIDTH OF MAP BEING DRAWN
MAP_HEIGTH	DS	2	;HEIGTH OF MAP BEING DRAWN

T0	DS	2	;TOP LEFT TILE
T1	DS	2	;TOP RIGHT TILE
T2	DS	2	;BOTTOM LEFT TILE
T3	DS	2	;BOTTOM RIGHT TILE

SCROLL_DIR	DS	2	;0 = RIGHT
			;1 = LEFT
			;2 = UP
			;3 = DOWN

;-----
; *** MODE 7 DATA AREA ***

MODE7_FLAG	DS	2	;0 = NOT USED, 1 = USED
ANGLE	DS	2	;ANGLE OF SCREEN 0 - 359
XSCALE	DS	2	;X REDUCTION RATE 256 = 1
			;> 256 MAKES IT SMALLER
			;< 256 MAKES IT LARGER
YSCALE	DS	2	;SAME AS X SCALE
SIN_ANGLE	DS	2	;SIN OF ANGLE
COS_ANGLE	DS	2	;COS OF ANGLE

SPIN_CTR	DS	2	;SPIN COUNTER
SPIN_SPEED	DS	2	;SPEED OF SPIN

DISTANCE	DS	2	;DISTANCE MULTIPLIER
DIST_CTR	DS	2	;
SPIN_VALUE	DS	2	;SPIN VALUE TO MULTIPLY
NEG_FLAG	DS	2	;


TEMP_ANGLE	DS	2
TEMP_SCALE	DS 	2


DST_LSB	DS	1
DST_MSB	DS	1

;-----
; *** LEVEL DATA ***

TEMP_WORD	DS	2	;STORE TEMP VALUES HERE

OBJ_BASE	DS	2	;OBJ BASE ADDRESS
OBJ_AREA	DS	1	;OBJECT DATA AREA SELECT
OBJ_SIZE	DS	1	;OBJ SIZES

SC_MODE	DS	1	;SCREEN MODE
BG3_PRIO	DS	1	;BG 3 PRIORITY
BG_CSIZE	DS	1	;BG CHAR SIZE

BG1_SIZE	DS	1	;BG1 SCREEN SIZE
BG2_SIZE	DS	1	;BG2 SCREEN SIZE
BG3_SIZE	DS	1	;BG3 SCREEN SIZE
BG4_SIZE	DS	1	;BG4 SCREEN SIZE

BG1_SC	DS	2	;BG1 SCREEN BASE ADDRESS
BG2_SC	DS	2	;BG2 SCREEN BASE ADDRESS
BG3_SC	DS	2	;BG3 SCREEN BASE ADDRESS
BG4_SC	DS	2	;BG4 SCREEN BASE ADDRESS

BG1_CHAR	DS	2	;BG1 CHAR DATA BASE ADDRESS
BG2_CHAR	DS	2	;BG2 CHAR DATA BASE ADDRESS
BG3_CHAR	DS	2	;BG3 CHAR DATA BASE ADDRESS
BG4_CHAR	DS	2	;BG4 CHAR DATA BASE ADDRESS

TM_VALUE	DS	1	;MAIN SCREEN DESIGNATON

MAP1_WIDTH	DS	2	;WIDTH OF MAP 1
MAP2_WIDTH	DS	2	;WIDTH OF MAP 2
MAP3_WIDTH	DS	2	;WIDTH OF MAP 3
MAP4_WIDTH	DS	2	;WIDTH OF MAP 4

MAP1_HEIGTH	DS	2	;HEIGTH OF MAP 1
MAP2_HEIGTH	DS	2	;HEIGTH OF MAP 2
MAP3_HEIGTH	DS	2	;HEIGTH OF MAP 3
MAP4_HEIGTH	DS	2	;HEIGTH OF MAP 4

RIGHT_EDGE1	DS	2	;RIGHT EDGE MAP 1
RIGHT_EDGE2	DS	2	;RIGHT EDGE MAP 2
RIGHT_EDGE3	DS	2	;RIGHT EDGE MAP 3
RIGHT_EDGE4	DS	2	;RIGHT EDGE MAP 4

BOTTOM_EDGE1	DS	2	;BOTTOM EDGE MAP 1
BOTTOM_EDGE2	DS	2	;BOTTOM EDGE MAP 2
BOTTOM_EDGE3	DS	2	;BOTTOM EDGE MAP 3
BOTTOM_EDGE4	DS	2	;BOTTOM EDGE MAP 4

MAP1_SC_FLAG	DS	1	;SCROLL FLAG FOR MAP 1 
MAP2_SC_FLAG	DS	1	;SCROLL FLAG FOR MAP 2 
MAP3_SC_FLAG	DS	1	;SCROLL FLAG FOR MAP 3 
MAP4_SC_FLAG	DS	1	;SCROLL FLAG FOR MAP 4

MAP1_STARTX	DS	2	;STARTING X MAP POSITION OF SCREEN
MAP2_STARTX	DS	2	;STARTING X MAP POSITION OF SCREEN
MAP3_STARTX	DS	2	;STARTING X MAP POSITION OF SCREEN
MAP4_STARTX	DS	2	;STARTING X MAP POSITION OF SCREEN

MAP1_STARTY	DS	2	;STARTING Y MAP POSITION OF SCREEN
MAP2_STARTY	DS	2	;STARTING Y MAP POSITION OF SCREEN
MAP3_STARTY	DS	2	;STARTING Y MAP POSITION OF SCREEN
MAP4_STARTY	DS	2	;STARTING Y MAP POSITION OF SCREEN

MAP1_X	DS	2	;CURRENT X OF SCREEN IN MAP
MAP2_X	DS	2	;CURRENT X OF SCREEN IN MAP
MAP3_X	DS	2	;CURRENT X OF SCREEN IN MAP
MAP4_X	DS	2	;CURRENT X OF SCREEN IN MAP

MAP1_Y	DS	2	;CURRENT Y OF SCREEN IN MAP
MAP2_Y	DS	2	;CURRENT Y OF SCREEN IN MAP
MAP3_Y	DS	2	;CURRENT Y OF SCREEN IN MAP
MAP4_Y	DS	2	;CURRENT Y OF SCREEN IN MAP

MAP1_LASTX	DS	2	;LAST X OF SCREEN IN MAP
MAP2_LASTX	DS	2	;LAST X OF SCREEN IN MAP
MAP3_LASTX	DS	2	;LAST X OF SCREEN IN MAP
MAP4_LASTX	DS	2	;LAST X OF SCREEN IN MAP

MAP1_LASTY	DS	2	;LAST Y OF SCREEN IN MAP
MAP2_LASTY	DS	2	;LAST Y OF SCREEN IN MAP
MAP3_LASTY	DS	2	;LAST Y OF SCREEN IN MAP
MAP4_LASTY	DS	2	;LAST Y OF SCREEN IN MAP

PLAYER_X	DS	2	;CURRENT PLAYER PIXEL X IN MAP
PLAYER_Y	DS	2	;CURRENT PLAYER PIXEL Y IN MAP

;-----
; *** RAM ALLOCATION DATA ***

RAM_SIZE	DS	2	;SIZE OF AREA TO ALLOCATE

NEXT_RAM	DS	4	;NEXT EMPTY LOCATION IN RAM

RAM_BLOCK0	DS	4	;STARTING ADDRESS OF BLOCK 0
RAM_BLOCK1	DS	4	;STARTING ADDRESS OF BLOCK 1
RAM_BLOCK2	DS	4	;STARTING ADDRESS OF BLOCK 2
RAM_BLOCK3	DS	4	;STARTING ADDRESS OF BLOCK 3
RAM_BLOCK4	DS	4	;STARTING ADDRESS OF BLOCK 4
RAM_BLOCK5	DS	4	;STARTING ADDRESS OF BLOCK 5
RAM_BLOCK6	DS	4	;STARTING ADDRESS OF BLOCK 6
RAM_BLOCK7	DS	4	;STARTING ADDRESS OF BLOCK 7

RAM_SIZE0	DS	2	;SIZE OF RAM BLOCK 0
RAM_SIZE1	DS	2	;SIZE OF RAM BLOCK 1
RAM_SIZE2	DS	2	;SIZE OF RAM BLOCK 2
RAM_SIZE3	DS	2	;SIZE OF RAM BLOCK 3
RAM_SIZE4	DS	2	;SIZE OF RAM BLOCK 4
RAM_SIZE5	DS	2	;SIZE OF RAM BLOCK 5
RAM_SIZE6	DS	2	;SIZE OF RAM BLOCK 6
RAM_SIZE7	DS	2	;SIZE OF RAM BLOCK 7

MAP_HANDLE	DS	2	;MAP RAM BLOCK POINTER 

MAP1_HANDLE	DS	2	;BG1 MAP MEMORY HANDLE
MAP2_HANDLE	DS	2	;BG2 MAP MEMORY HANDLE 
MAP3_HANDLE	DS	2	;BG3 MAP MEMORY HANDLE 
MAP4_HANDLE	DS	2	;BG4 MAP MEMORY HANDLE 

BLK_HANDLE	DS	2	;BLOCK RAM BLOCK POINTER

BLK1_HANDLE	DS	2	;BG1 BLOCK MEMORY HANDLE
BLK2_HANDLE	DS	2	;BG2 BLOCK MEMORY HANDLE 
BLK3_HANDLE	DS	2	;BG3 BLOCK MEMORY HANDLE 
BLK4_HANDLE	DS	2	;BG4 BLOCK MEMORY HANDLE

FLR_HANDLE	DS	2	;FLOOR RAM BLOCK POINTER

FLR1_HANDLE	DS	2	;BG1 FLOOR MEMORY HANDLE
FLR2_HANDLE	DS	2	;BG2 FLOOR MEMORY HANDLE
FLR3_HANDLE	DS	2	;BG3 FLOOR MEMORY HANDLE
FLR4_HANDLE	DS	2	;BG4 FLOOR MEMORY HANDLE

;-----
;PALETTE IN RAM DATA

PAL_IN_CGRAM	DS	1	;WHERE TO DMA INTO CG-RAM
PAL_SIZE	DS	2	;# OF BYTES TO DMA 

;-----
; *** DMA DATA AREA ***

TOTAL_DMAS	DS	2	;# OF OBJECT DMA'S THIS VBLANK

OAM_DMA_FLAG	DS	1	;IS <>0 IF OAM DMA NESSACARY
VRAM_DMA_FLAG	DS	1	;IS <>0 IF VRAM DMA NESSACARY
CG_DMA_FLAG	DS	1	;IS <>0 IF CG-RAM DMA NESSACARY

HSC_DMA_FLAG1	DS	1	;IS <>0 IF HORIZONTAL SCROLL DMA NESSACARY
HSC_DMA_FLAG2	DS	1	;IS <>0 IF HORIZONTAL SCROLL DMA NESSACARY
HSC_DMA_FLAG3	DS	1	;IS <>0 IF HORIZONTAL SCROLL DMA NESSACARY

VSC_DMA_FLAG1	DS	1	;IS <>0 IF VERTICAL SCROLL DMA NESSACARY
VSC_DMA_FLAG2	DS	1	;IS <>0 IF VERTICAL SCROLL DMA NESSACARY
VSC_DMA_FLAG3	DS	1	;IS <>0 IF VERTICAL SCROLL DMA NESSACARY

HSC_DMA_TOP1	DS	2	;VRAM ADDRESS OF TOP HORIZONTAL SCROLL
HSC_DMA_TOP2	DS	2	;VRAM ADDRESS OF TOP HORIZONTAL SCROLL
HSC_DMA_TOP3	DS	2	;VRAM ADDRESS OF TOP HORIZONTAL SCROLL

HSC_DMA_BTM1	DS	2	;VRAM ADDRESS OF BOTTOM HORIZONTAL SCROLL
HSC_DMA_BTM2	DS	2	;VRAM ADDRESS OF BOTTOM HORIZONTAL SCROLL
HSC_DMA_BTM3	DS	2	;VRAM ADDRESS OF BOTTOM HORIZONTAL SCROLL

VSC_DMA_LEFT1	DS	2	;VRAM ADDRESS OF LEFT VERTICAL SCROLL
VSC_DMA_LEFT2	DS	2	;VRAM ADDRESS OF LEFT VERTICAL SCROLL
VSC_DMA_LEFT3	DS	2	;VRAM ADDRESS OF LEFT VERTICAL SCROLL

VSC_DMA_RHT1	DS	2	;VRAM ADDRESS OF RIGHT VERTICAL SCROLL
VSC_DMA_RHT2	DS	2	;VRAM ADDRESS OF RIGHT VERTICAL SCROLL
VSC_DMA_RHT3	DS	2	;VRAM ADDRESS OF RIGHT VERTICAL SCROLL

DMA_HEAP_PTR	DS	2	;POINTER INTO DMA LIST

DMA_HEAP	DS	128	;BYTE 0 IS CONTROL BYTE
			;0 = END OF HEAP
			;
			;1 = DATA IS ADDRESS OF DMA LIST
			;THEN BYTE 1,2,3 IS ADDRESS OF DMA LIST
			;
			;2 = DATA IS A SINGLE DMA LIST
			;THEN BYTE 1 IS THE VMA_INC VALUE
			;BYTE 2,3,4 IS ADDRESS OF SOURCE
			;BYTE 5,6 IS DESTINATION IN VRAM
			;BYTE 7,8 IS LENGTH OF DMA

DMA_TOTAL	DS	1	;# OF DMA TRANSFERS TO DO

NEXT_SPRITE	DS	1

SPRITE_COUNT	DS	1

DMA_LIST	DS	2	;OFFSET TO DMA LIST

DMA_FLAG	DS	1

;-----
; *** TEMPORARY VARIABLES ***

TEMP_ACC	DS	2	;TEMP
TEMP_X	DS	2	;TEMP 
TEMP_Y	DS	2	;TEMP

;-----
; *** CONTOUR AND SPECIAL VARIABLES

CON_X	DS	2	;CONTOUR X
CON_Y	DS	2	;CONTOUR Y

CON_OFF	DS	2	;CONTOUR OFFSET INTO MAP

SPECIAL_00	DS	2	;ROW 0
SPECIAL_01	DS	2
SPECIAL_02	DS	2
SPECIAL_03	DS	2
;
SPECIAL_10	DS	2	;ROW 1
SPECIAL_11	DS	2
SPECIAL_12	DS	2
SPECIAL_13	DS	2
;
SPECIAL_20	DS	2	;ROW 2
SPECIAL_21	DS	2
SPECIAL_22	DS	2
SPECIAL_23	DS	2
;
SPECIAL_30	DS	2	;ROW 3
SPECIAL_31	DS	2
SPECIAL_32	DS	2
SPECIAL_33	DS	2

;-----
; *** SCROLL VARIABLES ***

SC_X	DS	2	;X OF THIS MAP
SC_Y	DS	2	;Y OF THIS MAP
SC_WIDTH	DS	2	;WIDTH OF MAP BEING USED
BG_SC_ADD	DS	2	;ADDRESS OF BACKGROUND SCREEN BEING USED
SC_DEST	DS	2	;DEST ADDRESS IN SCRN_BUF
XSCROLL_RATE	DS	2	;# PIXELS TO SCROLL IN X
YSCROLL_RATE	DS	2	;# PIXELS TO SCROLL IN Y
XSCROLL_DIR	DS	1	;DIR OF X SCROLL
YSCROLL_DIR	DS	1	;DIR OF Y SCROLL

HORZ_BUFFER	DS	2	;DISTANCE FROM GOLIATH TO EDGE
SCROLL_CTR1	DS	2	;COUNT TO NEXT SCROLL
SCROLL_CTR2	DS	2	;COUNT TO NEXT SCROLL
SCROLL_CTR3	DS	2	;COUNT TO NEXT SCROLL
SCROLL_CTR4	DS	2	;COUNT TO NEXT SCROLL

OPP_MAPX	DS	2	;OPTIMUM MAP X
OPP_MAPY	DS	2	;OPTIMUM MAP Y

ERRORX	DS	2	;SCROLL ERROR IN X
ERRORY	DS	2	;SCROLL ERROR IN Y

ABS_ERRX	DS	2	;ABSOLUTE VALUE OF ERRORX
ABS_ERRY	DS	2	;ABSOLUTE VALUE OF ERRORY

ERRORX_CTR	DS	2	;# OF VB'S ERROR HAS OCCURED
ERRORY_CTR	DS	2	;# OF VB'S ERROR HAS OCCURED

MAX_XRATE	DS	2	;MAX X SCROLL RATE
MAX_YRATE	DS	2	;MAX Y SCROLL RATE

CUSTOM_FLAG	DS	2	;0 = NORMAL MAP, 1 = CUSTOM MAP

;-----
; *** CHARACTER ANIMATION DATA ***

CA1_FRAME	DS	2	;FRAME # TO DO
CA1_FRAMES	DS	2	;TOTAL # OF FRAMES
CA1_RATE	DS	2	;FRAME RATE
CA1_TIMER	DS	2	;FRAME RATE COUNTDOWN TIMER VALUE
CA1_CHAR_BASE	DS	2	;BASE ADDRESS OF CHAR DATA IN VRAM

;-----
; *** GAME DATA ***

GAME_LEVEL	DS	2	;GAME LEVEL
GOL_CON_FLAG	DS	1	;0 = N/A, 1 = DONT LOOK FOR CONTOURS
SONG_PTR	DS	2	;# OF CURRENT SONG
JMP_SKIP	DS	2	;# OF FRAMES TO SKIP IN JUMP HOLD ZONE
;-----
; *** COLISSION DATA ***

HIT_BOXES	DS	2	;# OF HIT BOXES FOR THIS OBJECT
HIT_TBL_OFF	DS	2	;OFFSET INTO HIT TABLE
HIT_PTR	DS	2	;POINTER TO OBJECT BEING TESTED

HIT_X1	DS	2	;LEFT X
HIT_X2	DS	2	;RIGHT X
HIT_Y1	DS	2	;TOP Y
HIT_Y2	DS	2	;BOTTOM Y

;-----
; *** BREAKABLE DOORWAYS ***

DOOR1_FLAG	DS	2	;0-3 = IMPASSABLE, 4 = OPEN
DOOR2_FLAG	DS	2
DOOR3_FLAG	DS	2
DOOR4_FLAG	DS	2

;-----
; *** DECOMPRESSION VARIABLES ***

SRC_PTR	DS	2	;SOURCE OFFSET
SRC_BIT_PTR	DS	2	;SOURCE BIT PTR
DEST_PTR	DS	2	;DESTINATION OFFSET

;-----
; *** HDMA DATA ***

HDMA_FLAG	DS	1	; 0 = OFF, 1 = DO IT

HDMA_SLOT0	DS	3
HDMA_SLOT1	DS	3
HDMA_SLOT2	DS	3
HDMA_SLOT3	DS	3
HDMA_SLOT4	DS	3
HDMA_SLOT5	DS	3
HDMA_SLOT6	DS	3
HDMA_SLOT7	DS	3

HDMA_DATA	DS	224*2		;DATA HERE

;-----
; *** VERTICAL SHEAR DATA ***

OFFSET_TABLE	DS	32*2

VERT_SHEAR	DS	32*2

SHEAR1_Y	DS	2
SHEAR2_Y	DS	2

;-----
; *** COLOR CYCLE DATA ***

CCS	EQU	4

CC_TYPE	DS	CCS*2	;0 = EMPTY, 1 = GLOBAL, 2 = REGIONAL
CC_START	DS	CCS*2	;PALETTE CYCLE START POINTER
CC_LENGTH	DS	CCS*2	;LENGTH IN BYTES
CC_COUNT	DS	CCS*2	;COUNT TO NEXT CYCLE
CC_TIME	DS	CCS*2	;COUNTER RESET IN 1/60 SEC
CC_DIR	DS	CCS*2	;CYCLE DIRECTION 0 = UP, 1 = DOWN
CC_X1	DS	CCS*2	;REGION TEST X1
CC_X2	DS	CCS*2	;REGION TEST X2
CC_Y1	DS	CCS*2	;REGION TEST Y1
CC_Y2	DS	CCS*2	;REGION TEST Y2

CG_DATA	DS	512	;CG_DATA AREA

;-----
; *** ENEMY DETECT DATA AREA

ENEMY_ID	DS	1	;ID # OF ENEMY DETECTED
ENEMY_X	DS	2	;MAP TILE X WHERE ENEMY WAS DETECTED
ENEMY_Y	DS	2	;MAP TILE Y WHERE ENEMY WAS DETECTED
ENEMY_XOFF	DS	2	;X OFFSET FOR THIS ENEMY
ENEMY_YOFF	DS	2	;Y OFFSET FOR THIS ENEMY

;-----
; *** OAM DATA AREA ***
;
;THIS WHOLE AREA IS DMA'ED TO VRAM EVERY VBLANK

	CNOP	0,16

OAM_DATA	DS	542

;------------------
;***** PROGRAM STARTS HERE *****

	SECTION	RESET,PAGE00_GROUP

;------------------

	MX	3	;ACC AND INDEX IN 8 BIT MODE

	REGS	a=0,x=0,y=0,db=0
;-----
;ROM INFO AREA

	DB	13,10
	DB	'GARGOYLES',13,10
	DB	'BY PETER FOKOS',13,10
	DB	'COPYRIGHT DISNEY',13,10
	DB	'VERSION 0.00',13,10
	DB	'LAST ASSEMBLEY:',13,10
	DB	'\#_month/\#_day/\#_year at \#_hours:\#_minutes:\#_seconds',13,10
	DB	13,10,13,10


;-----
		                               
BEGIN	SET_16BIT
	SET_A16
	LDA	#$1FFF	;SET TOP OF STACK
	TCS
	SET_A8
	SEI		;DISABLE INTERRUPTS
	JSL	CLEAR_ZERO	;CLEAR ZERO PAGE 
	JSL	CLEAR_RAM2	;CLEAR $7E0000 RAM
	JSL	CLEAR_REGS	;CLEAR ALL THE PPU REGISTERS
;-----
	JSL	TEST_SND
;-----
	JSL	DISNEY_SCRN	;DRAW THE DISNEY SCREEN FIRST

;==================
;HERE IS WHERE ALL THE CONTROL LOGIC IS EXECUTED
MAIN_LP

;-----
;WAIT FOR THE START OF VERTICAL BLANK

	SET_A8
	LDA	#$81	;JOY-C ENABLE
	STA	NMITIMEN
VB_LP1	LDA	RDNMI	;WAIT FOR V-BLANK TO FINISH
	BIT	#$80
	BNE	VB_LP1
VB_LP2	LDA	RDNMI	;WAIT FOR START OF V-BLANK
	BIT	#$80
	BEQ	VB_LP2
;-----
	LDA	HDMA_FLAG	;DO I NEED TO DO A H-DMA ?
	BEQ	NO_HDMA	;NO
;
	LDA	#$80
	STA	HDMAEN	;ENABLE THE 2ND H-DMA CHANNEL
NO_HDMA	STZ	HDMA_FLAG	;CLEAR THE H-DMA FLAG
;-----
;SET THE NEW HORIZONTAL AND VERTICAL SCROLL VALUES IN HARDWARE
	SET_A16
	LDA	MAP1_X	;GET NEW SCROLL VALUE
	SET_A8
	STA	BG1HOFS	;LOW BYTE
	XBA
	STA	BG1HOFS	;HIGH BYTE
	SET_A16
	LDA	MAP2_X	;GET NEW SCROLL VALUE
	SET_A8
	STA	BG2HOFS	;LOW BYTE
	XBA
	STA	BG2HOFS	;HIGH BYTE
            	SET_A16
	LDA	MAP3_X	;GET NEW SCROLL VALUE
	SET_A8
	STA	BG3HOFS	;LOW BYTE
	XBA
	STA	BG3HOFS	;HIGH BYTE
;
	SET_A16
	LDA	MAP1_Y	;GET NEW SCROLL VALUE
    	SET_A8
	STA	BG1VOFS	;LOW BYTE
	XBA
	STA	BG1VOFS	;HIGH BYTE
	SET_A16
	LDA	MAP2_Y	;GET NEW SCROLL VALUE
    	SET_A8
	STA	BG2VOFS	;LOW BYTE
	XBA
	STA	BG2VOFS	;HIGH BYTE
           	SET_A16
	LDA	MAP3_Y	;GET NEW SCROLL VALUE
    	SET_A8
	STA	BG3VOFS	;LOW BYTE
	XBA
	STA	BG3VOFS	;HIGH BYTE
;-----
;ADJUST THE SCREEN SCROLL POSITION AND DRAW NEW SCREEN EDGES IF NEEDED.
	DO_HSC1_DMA	;DO HORIZONTAL SCROLL DMA
	DO_VSC1_DMA	;DO VERTICAL SCROLL DMA
	DO_HSC2_DMA	;DO HORIZONTAL SCROLL DMA
	DO_VSC2_DMA	;DO VERTICAL SCROLL DMA
;-----
;DMA THE OAM DATA
	DO_OAM_DMA	;SEND OBJECT ATTRIBUTE MEMORY
;-----
;DMA DATA TO VRAM
	DO_VRAM_DMA	;SEND DATA TO VRAM
;-----
;DMA PALETTE DATA TO CG-RAM
	DO_CGRAM_DMA	;SEND PALETTE DATA TO CG-RAM
;-----
;DMA SOUND DATA

;-----
	JSL	MODE7_STUFF	;SET MODE 7 VALUES IF NEEDED
;-----
	JSL	READ_JOYPAD	;GET JOYPAD INPUT
	JSL	JP_HANDLER	;EXECUTE JOYPAD COMMANDS
;-----
;MOVE THE OBJECT THAT THE SCREEN SCROLL IS BASED ON.
;MOST OF THE TIME THIS WILL BE GOLIATH
	JSL	OBJ_HANDLER	;DO OBJECTS CONTROL
	JSL	CK_HITS	;SEE IF ANY HITS OCCURED
	JSL	ENEMY_DETECT	;SEE IF ANY ENEMIES NEED TO BE TURNED ON
;-----
;MOVE ALL THE OTHER OBJECTS THAT EXIST NOW USING THE CURRENT SCROLL_X
;TO SET THEIR NEW POSITIONS.


;-----
;DO ANY CHARACTER ANIMATIONS

	JSL	CHAR_ANIM_HANDLER

;-----
;DO ANY COLOR CYCLES NOW

	JSL	COLOR_CYCLES

;-----
;EXECUTE ANY GAME CONTROL LOGIC
	SET_A16
	LDA	LOOP_HANDLER
	BEQ	LOOP_RETURN
	PHK		;PUSH PROGRAM BANK
	PER	LOOP_RETURN-1	;SAVE RETURN ADDRESS
	SET_A8
	LDA	LOOP_HANDLER+2	;GET BANK
	PHA		;PUSH PROGRAM BANK ADDRESS
	SET_A16
	LDA	LOOP_HANDLER	;GET ADDRESS OF ROUTINE
	SEC
	SBC	#1
	PHA		;PUSH ADDRESS
	RTL		;GO THERE NOW
;-----
LOOP_RETURN	SET_AXY16
;-----
	JMP	MAIN_LP

;------------------
;NMI_INT -- NON-MASKABLE INTERRUPT

NMI_INT	PROC	NEAR,MX:3

	PHP		;SAVE STATUS
	SET_AXY16
	PHA
	PHB
	PHD
	PHX
	PHY
;-----

	IF 	DEBUG_ON

	COP	$00	;POLL HOST

	ENDIF

;-----
;VBLANK ROUTINES
	CLC
	LDA	NMI_CTR
	ADC	#1
	STA	NMI_CTR
;
      	JSR	DO_FADE	;DO ANY FADEING
;-----
	SET_AXY16
	PLY
	PLX
	PLD
	PLB
	PLA
	PLP		;RESTORE STATUS
	RTI

	ENDP

;------------------
;IRQ_INT -- 

IRQ_INT	PROC	NEAR,MX:3

NO_INT	RTI

	ENDP

;------------------
;CLEAR_REGS -- CLEAR THE PPU REGISTERS FOR A COLDSTART

CLEAR_REGS	MODULE

	PHP
;-----
	SET_AXY8	;8 BIT REGISTERS
;
	LDA	#$8F	;FORCE BLANKING
	STA	INIDISP
;
	STZ	OBJSEL
	STZ	OAMADDL
	STZ	OAMADDH
	STZ	OAMDATAW
	STZ	BGMODE
	STZ	MOSAIC
	STZ	BG1SC
	STZ	BG2SC
	STZ	BG3SC
	STZ	BG4SC
	STZ	BG12NBA
	STZ	BG34NBA
;
	STZ	BG1HOFS	;LOW BYTES
	STZ	BG1VOFS
	STZ	BG2HOFS
	STZ	BG2VOFS
	STZ	BG3HOFS
	STZ	BG3VOFS
	STZ	BG4HOFS
	STZ	BG4VOFS
;
	STZ	BG1HOFS	;HIGH BYTES
	STZ	BG1VOFS
	STZ	BG2HOFS
	STZ	BG2VOFS
	STZ	BG3HOFS
	STZ	BG3VOFS
	STZ	BG4HOFS
	STZ	BG4VOFS
;
	LDA	#$80	;VRAM ADDRESS INC AFTER 2ND BYTE
	STA	VMAINC
;
	STZ	VMADDL
	STZ	VMADDH
	STZ	WVMDATAL
	STZ	WVMDATAH
	STZ	M7SEL
;
	STZ	M7A	;LOW BYTES
	STZ	M7B
	STZ	M7C
	STZ	M7D
	STZ	M7X
	STZ	M7Y
;
	LDA	#$01	;HIGH BYTES
	STA	M7A
	STZ	M7B
	STZ	M7C
	STA	M7D
	STZ	M7X
	STZ	M7Y
;
	STZ	CGADD
	STZ	WCGDATA
	STZ	W12SEL
	STZ	W34SEL
	STZ	WOBJSEL
	STZ	WH0
	STZ	WH1
	STZ	WH2
	STZ	WH3
	STZ	WBGLOG
	STZ	TM
	STZ	TS
	STZ	TMW
	STZ	TSW
	LDA	#$30
	STA	CGSWSEL
	STZ	CGADSUB
	LDA	#$E0
	STA	COLDATA
	STZ	SETINI
;
	STZ	NMITIMEN
	LDA	#$FF
	STA	WRIO
	STZ	WRMPYA
	STZ	WRMPYB
	STZ	WRDIVL
	STZ	WRDIVH
	STZ	WRDIVB
	STZ	HTIMEL
	STZ	HTIMEH
	STZ	MDMAEN
	STZ	HDMAEN
	STZ	MEMSEL
;-----
;CLEAR SINGLE BYTE VARIABLES
	STZ	FLIP_DATA
	STZ	SS_FLAG
	STZ	DMA_FLAG
	STZ	OAM_DMA_FLAG
	STZ	VRAM_DMA_FLAG
	STZ	CG_DMA_FLAG
	STZ	HSC_DMA_FLAG1
	STZ	HSC_DMA_FLAG2
	STZ	HSC_DMA_FLAG3
	STZ	VSC_DMA_FLAG1
	STZ	VSC_DMA_FLAG2
	STZ	VSC_DMA_FLAG3
	STZ	DMA_HEAP
;-----
;CLEAR WORD VARIABLES
	SET_A16
	STZ	DMA_HEAP_PTR
	STZ	MAP1_X
	STZ	MAP1_Y
	STZ	MAP1_LASTX
	STZ	MAP1_LASTY
	STZ	MAP2_X
	STZ	MAP2_Y
	STZ	MAP2_LASTX
	STZ	MAP2_LASTY
	STZ	MAP3_X
	STZ	MAP3_Y
	STZ	MAP3_LASTX
	STZ	MAP3_LASTY
;-----
	PLP
	RTL

	MODEND

;------------------
;CLEAR_ZERO -- CLEAR ALL $2000 BYTES OF ZERO PAGE

CLEAR_ZERO	MODULE

	PHP
	PHB
;-----
	SET_A8
	LDA	#0	;SET ZERO PAGE DATA BANK
	PHA
	PLB
;
	SET_AXY16
	LDX	#$00
@CZ_LP	STZ	$0,X	;CLEAR ALL $2000 BYTES TO $0000
	INX
	INX
	CPX	#$2000-4	;DON'T CLEAR TOP OF STACK
	BNE	@CZ_LP
;-----
	PLB
	PLP
	RTL	

	MODEND

;------------------
;CLEAR_RAM2 -- CLEAR ALL $1E0000 BYTES OF RAM2

CLEAR_RAM2	MODULE

	PHP
	PHB
;-----
	SET_A8
	LDA	#$7E	;SET RAM2 DATA BANK
	PHA
	PLB
;
	SET_AXY16
	LDX	#$2000
@CR2_LP1	STZ	$0,X	;CLEAR ALL $2000 BYTES TO $0000
	INX
	INX
	CPX	#0
	BNE	@CR2_LP1
;
;-----
	SET_A8
	LDA	#$7F	;SET RAM2 SECOND DATA BANK
	PHA
	PLB
;
	SET_AXY16
	LDX	#0
@CR2_LP2	STZ	$0,X	;CLEAR ALL $2000 BYTES TO $0000
	INX
	INX
	CPX	#0
	BNE	@CR2_LP2
;-----
	PLB
	PLP
	RTL	

	MODEND

;------------------
;FADE_IN -- FADE THE PALETTE IN FROM BLACK

FADE_IN	MODULE

	PHP
	SET_A8
	LDA	#$80	;SET FLAG, BRIGHTNESS = 0
	STA	FADE_FLAG
	PLP
	RTL

	MODEND

;------------------
;FADE_OUT -- FADE THE PALETTE OUT TO BLACK

FADE_OUT	MODULE

	PHP
	SET_A8
	LDA	#$CF	;SET FLAG, BRIGHTNESS = 15
	STA	FADE_FLAG
	PLP
	RTL

	MODEND

;------------------
;DO_FADE -- FADE THE PALETTE IN FROM BLACK

DO_FADE	MODULE

	PHP
	SET_A8
	LDA	FADE_FLAG	;GET FLAG
	BIT	#$80	;ON ?
	BEQ	@NO_FADE
;-----
	BIT	#$40	;DEC ?
	BEQ	@FADE_UP
	AND	#$0F
	DEC	FADE_FLAG
	DEC	A
	BNE	@10
@END_FADE	STA	INIDISP	;SET BRIGHTNESS
	STZ	FADE_FLAG	;END IT
	JMP	@NO_FADE
;
@FADE_UP	AND	#$0F
	INC	FADE_FLAG
	INC	A
	CMP	#$0F
	BEQ	@END_FADE
@10	STA	INIDISP	;SET BRIGHTNESS
@NO_FADE
	PLP
	RTS

	MODEND


;------------------
;CLEAR_SPRITES -- CLEAR ALL SPRITE OFF THE SCREEN

CLEAR_SPRITES	MODULE

	PHP
	SET_A8
;-----
	LDA	#^OAM_DATA
	PHA
	PLB
	LDX	#0
CS_LP3	LDA	#$20
	STA	OAM_DATA,X		;CLEAR OAM DATA AREA
	STZ	OAM_DATA+1,X
	STZ	OAM_DATA+2,X
	STZ	OAM_DATA+3,X
	INX
	INX
	INX
	INX
	CPX	#512
	BNE	CS_LP3
;
	LDX	#0
	LDA	#$55	;FF
CS_LP3A	STA	OAM_DATA+512,X		;SET X MSB AND SIZE
	INX
	CPX	#128/4
	BNE	CS_LP3A
;
	LDA	#1
	STA	OAM_DMA_FLAG		;FORCE DMA NOW
;-----
	PLP
	RTL

	MODEND

;------------------
;DISNEY_SCRN -- DRAW THE DISNEY LOGO SCREEN AND WAIT FOR START BUTTON

;-----

DISNEY_SCRN	MODULE

	PHP
	PHB
;-----
	SET_A16
	STZ	OBJ_CONTROL
	STZ	OBJ_CONTROL+4
	STZ	OBJ_CONTROL+8
	STZ	OBJ_CONTROL+12
;-----
	RUN	DISNEY_SCRIPT	;EXECUTE DISNEY SCRIPT
;-----
	PLB
	PLP
	RTL
;=====
DISNEY_EXIT	PHP
	JSL	FADE_OUT
	SET_A8
@FADE_WAIT	LDA	FADE_FLAG
	BNE	@FADE_WAIT
	SEI
	STZ	TITLE_FLAG
	JSL	CLEAR_REGS
;-----
	SET_AXY16
	STZ	LOOP_HANDLER
	RUN	TTL_SCRIPT	;DO TITLE SCRIPT
	PLP
	RTL

	MODEND

;------------------
;TITLE_SCRN -- DRAW THE TITLE SCREEN AND WAIT FOR START BUTTON

;-----

LVL_SCRIPTS	DL	LVL1_SCRIPT		;1
	DL	LVL2_SCRIPT		;2
	DL	LVL3_SCRIPT		;3
	DL	LVL4_SCRIPT		;4
	DL	LVL5_SCRIPT		;5
	DL	LVL6_SCRIPT		;6
	DL	MODE7_SCRIPT		;7
	DL	LVL7_SCRIPT		;8
	DL	DISNEY_SCRIPT		;9
	DL	BONUS_SCRIPT		;10

;-----

TITLE_SCRN	MODULE

	PHP
	PHB
;-----
	SET_A16
	STZ	OBJ_CONTROL
	STZ	OBJ_CONTROL+4
	STZ	OBJ_CONTROL+8
	STZ	OBJ_CONTROL+12
;-----
;EXECUTE TITLE SCRIPT

	RUN	TTL_SCRIPT	;EXECUTE TITLE SCRIPT
;-----
	PLB
	PLP
	RTL
;=====
TITLE_EXIT	PHP
	JSL	FADE_OUT
	SET_A8
@FADE_WAIT	LDA	FADE_FLAG
	BNE	@FADE_WAIT
	SEI
	STZ	TITLE_FLAG
	JSL	CLEAR_REGS
;-----
	SET_AXY16
	LDA	GAME_LEVEL
	ASL	A
	ASL	A
	TAX
	LDA	LVL_SCRIPTS,X	;GET OFFSET OF SCRIPT
	TAY
	LDA	LVL_SCRIPTS+2,X	;GET BANK
	SET_A8
	PHA
	PLB
	JSL	DO_SCRIPT	;RUN THE SCRIPT FOR THIS LEVEL
	PLP
	RTL

	MODEND

;------------------
;LVL1_EXIT -- EXIT LEVEL 1 AND START LEVEL 2

LVL1_EXIT	MODULE

	PHP
	JSL	FADE_OUT
	SET_A8
@FADE_WAIT	LDA	FADE_FLAG
	BNE	@FADE_WAIT
	SEI
	STZ	TITLE_FLAG
	JSL	CLEAR_REGS
;-----
	SET_A16
	STZ	OBJ_CONTROL
	STZ	OBJ_CONTROL+4
	STZ	OBJ_CONTROL+8
	STZ	OBJ_CONTROL+12
	STZ	MODE7_FLAG
	STZ	LOOP_HANDLER
	RUN	TTL_SCRIPT
	PLP
	RTL

	MODEND

;------------------
;LVL2_EXIT -- EXIT LEVEL 2 AND START LEVEL 2

LVL2_EXIT	MODULE

	PHP
	JSL	FADE_OUT
	SET_A8
@FADE_WAIT	LDA	FADE_FLAG
	BNE	@FADE_WAIT
	SEI
	STZ	TITLE_FLAG
	JSL	CLEAR_REGS
;-----
	SET_A16
	STZ	OBJ_CONTROL
	STZ	OBJ_CONTROL+4
	STZ	OBJ_CONTROL+8
	STZ	OBJ_CONTROL+12
	RUN	TTL_SCRIPT	;EXECUTE TITLE SCRIPT
	PLP
	RTL

	MODEND


;******************
;CODE SOURCE FILES INCLUDED HERE

	INCLUDE GG_SCRPT.658	;EXECUTE SCRIPTS

	INCLUDE	GG_MAPS.658	;DRAW MAPS

	INCLUDE	GG_JOYHD.658	;JOYPAD HANDLER

	INCLUDE	GG_LVLS.658	;LEVEL DATA

	INCLUDE GG_RAM.658	;RAM ALLOCATION

	INCLUDE	GG_SCRL.658	;SCROLL ROUTINES

	INCLUDE	GG_TEXT.658	;TEXT OUTPUT ROUTINES

	INCLUDE	GG_OBJ.658	;OBJECT CONTROL

	INCLUDE	GG_ANIM.658	;ANIMATION ROUTINES

	INCLUDE	GG_GOL.658	;GOLIATH CONTROL ROUTINES

	INCLUDE	GG_CON.658	;CONTOUR ROUTINES

	INCLUDE	GG_LVL1.658	;LEVEL 1 ENEMY CONTROL

	INCLUDE	GG_HITS.658	;LOOK FOR OBJECT COLLISIONS

	INCLUDE	GG_SIN.658	;SIN TABLE

	INCLUDE	GG_MODE7.658	;MODE7 ROUTINES

	INCLUDE	GG_PAL.658	;PALETTES ROUTINES

;-----
;END OF PROGRAM FILES

	INCLUDE	GG_SND.658	;SOUND ROUTINES

;------------------

	SECTION	REGISTER,REG_GROUP

	DB	'GARGOYLES             '
	DB	0	;ROM CART
	DB	9	;512k
	DB	0	;NO RAM
	DW	$5001	;MAKER CODE
	DB	0	;VERSION #
COMP_CHECK	DW	$7CCD	;COMPLEMENT CHECK
CHECK_SUM	DW	$8332	;CHECK SUM

;------------------

	SECTION	INT_VECTORS,VECTOR_GROUP

	DW	0,0	;EMPTY

	IF	DEBUG_ON

	DW	COP_ENTRY	;FFE4 = COP
	DW	BRK_ENTRY  	;FFE6 = BRK
	DW	BRK_ENTRY  	;FFE8 = ABORT
	DW	NMI_INT  	;FFEA = NMI
	DW	BEGIN  	;FFEC = RESET
	DW	IRQ_INT	;FFEE = IRQ

	ELSE

	DW	NO_INT	;FFE4 = COP
	DW	NO_INT 	;FFE6 = BRK
	DW	NO_INT 	;FFE8 = ABORT
	DW	NMI_INT  	;FFEA = NMI
	DW	BEGIN  	;FFEC = RESET
	DW	IRQ_INT	;FFEE = IRQ

	ENDIF

;------------------

	SECTION	DB_VECTORS,DEBUG_GROUP

	DW	0,0	;FFF0 - FFF3

	IF	DEBUG_ON
 
BRK_ENTRY  	JML	$7DE00B	;BRK ENTRY
COP_ENTRY	JML	$7DE00E	;COP ENTRY
COLD_START	JML	$7DE000	;COLD START

	ELSE

	DW	NO_INT	;FFF4 = COP
	DW	NO_INT 	;FFF6 = BRK
	DW	NO_INT 	;FFF8 = ABORT
	DW	NMI_INT  	;FFFA = NMI
	DW	BEGIN  	;FFEC = RESET
	DW	IRQ_INT	;FFFE = IRQ

	ENDIF

;------------------

	SECTION	DATA,PAGE01_GROUP


FONT1	INCBIN	FONT1.CHR
PAL2	INCLUDE	FONT1.PAL

;-----
;THIS IS WHERE I CAN LOOK UP THE ADDRESS OF ANY ANIMATION
;EACH ANIMATION TAKES UP 9 BYTES
;
;BYTE	DESCRIPTION
;--------------------------
; 0	ANIMATION COMMANDS LSB
; 1	ANIMATION COMMANDS MSB
; 2	ANIMATION COMMANDS PAGE #
; 3	CHARACTER TILES LSB
; 4	CHARACTER TILES MSB
; 5	CHARACTER TILES PAGE #
; 6
; 7
; 8

;******************
;THESE SECTIONS CONTAIN THE DATA FOR EACH ANIMATION IN THE GAME
;TO LOOK UP THE LOCATION OF AN ANIMATION MULTIPLY ITS # BY 3
;THE FIRST BYTE IS THE OFFSET LSB
;THE SECOND BYTE IS THE OFFSET MSB
;THE THIRD BYTE IS THE PAGE #
;
;ANIM #	DESCRIPTION
;---------------------------
;
; 0	GOLIATH RUNNING
; 1	
;
;
;-----
	PUSHS	;SAVE CURRENT SECTION DEFINITION
;-----
	SECTION ANIM_CMDS_SEC,PAGE01_GROUP

;ANIM_CMDS		;COMMAND LIST FOR EACH ANIMATION

;-----
	SECTION ANIM_TILES_SEC,PAGE01_GROUP

;ANIM_TILES		;TILE DATA FOR EACH ANIMATION

;-----
	SECTION ANIM_PALS_SEC,PAGE01_GROUP

;ANIM_PALS		;PALETTE DATA FOR EACH ANIMATION

;-----
	POPS	;RESTORE PREVIOUS SECTION DEFINITION

;******************
;ANIMATION DATA INCLUDED HERE

	SECTION	ANIMS,?

	INCLUDE	..\SPRITES\GOLIATH\GOL_RUN.ANY
	INCLUDE	..\SPRITES\GOLIATH\GOL_FALL.ANY
	INCLUDE	..\SPRITES\GOLIATH\GOL_JMP.ANY
	INCLUDE	..\SPRITES\GOLIATH\GOL_IDLE.ANY


	SECTION	ANIMS2,?

	INCLUDE	..\SPRITES\GOLIATH\GOL_PUN.ANY
	INCLUDE	..\SPRITES\GOLIATH\GOL_CLIM.ANY
	INCLUDE	..\SPRITES\CASTLE\VI_WALK.ANY

	SECTION	ANIMS3,?

	INCLUDE	..\SPRITES\GOLIATH\GOL_R2S.ANY

	SECTION ANIMS4,?

	INCLUDE	..\SPRITES\GOLIATH\GOL_SPRT.ANY
	SECTION ANIMS5,?
	INCLUDE	..\SPRITES\GOLIATH\GOL_JFLP.ANY
	INCLUDE	..\SPRITES\GOLIATH\GOL_CIDL.ANY
	INCLUDE	..\SPRITES\GOLIATH\GOL_CCRW.ANY

	SECTION	ANIM5,?

	INCLUDE	..\SPRITES\CASTLE\AR_SHOT.ANY



;------------------

	SECTION CONTOURS,?

CONTOUR1	INCLUDE	..\TUME_ART\OLDSTUFF\CASTLE1\CONTOUR5.CON

;------------------

	SECTION	TITLE,?

TTL_BLOCK	INCLUDE	..\TUME_ART\OLDSTUFF\TITLE\GGTTL2.BLK
TTL_BLK_SZ	EQU	*-TTL_BLOCK
TTL_CHAR	INCBIN	..\TUME_ART\OLDSTUFF\TITLE\GGTTL2.CHR
TTL_CHR_SZ	EQU	*-TTL_CHAR
TTL_PAL	INCLUDE	..\TUME_ART\OLDSTUFF\TITLE\GGTTL.PAL
TTL_MAP	INCBIN	..\TUME_ART\OLDSTUFF\TITLE\GGTTL.MAP

;------------------

	SECTION	TTL_MUSIC1,MUSIC_GROUP1

SONG1_SAMPLES	INCBIN	..\MUSIC\GHOST.CK0

	SECTION	TTL_MUSIC2,MUSIC_GROUP2

	INCBIN	..\MUSIC\GHOST.CK1

SONG1_INST	INCBIN	..\MUSIC\GHOST.IRM

SONG1_MIDI	INCBIN	..\MUSIC\GHOST.MRM

;------------------

	SECTION	TTL_MUSIC3,MUSIC_GROUP3

SONG2_SAMPLES	INCBIN	..\MUSIC\GANTAR.SRM

	SECTION	TTL_MUSIC4,MUSIC_GROUP4

SONG2_INST	INCBIN	..\MUSIC\GANTAR.IRM

SONG2_MIDI	INCBIN	..\MUSIC\GANTAR.MRM

;------------------

	SECTION	TTL_MUSIC5,MUSIC_GROUP5

SONG3_SAMPLES	INCBIN	..\MUSIC\DAVE.CK0

	SECTION	TTL_MUSIC6,MUSIC_GROUP6

	INCBIN	..\MUSIC\DAVE.CK1

SONG3_INST	INCBIN	..\MUSIC\DAVE.IRM

SONG3_MIDI	INCBIN	..\MUSIC\DAVE.MRM

;------------------

	SECTION	TTL_MUSIC7,MUSIC_GROUP7

SONG4_SAMPLES	INCBIN	..\MUSIC\ROCK.CK0

	SECTION	TTL_MUSIC8,MUSIC_GROUP8

	INCBIN	..\MUSIC\ROCK.CK1

SONG4_INST	INCBIN	..\MUSIC\ROCK.IRM

SONG4_MIDI	INCBIN	..\MUSIC\ROCK.MRM

;------------------

	SECTION	LVL1A_SEC2,?

;LVL1A_MAP	INCBIN	..\TUME_ART\CASTLE1\CAST01.MA~
;LVL1A_FLR	INCBIN	..\TUME_ART\CASTLE1\CAST01.FL~

;-----
;CHARACTER ANIMATION DATA FOR LVL1A

;LVL1A_DOORS	DL	AMAP_CAST01A0_START
;	DL	AMAP_CAST01B0_START
;	DL	AMAP_CAST01C0_START


;CAST01_DATA	INCBIN	..\TUME_ART\OLDSTUFF\CASTLE1\CAST01.ADT 	;TILE DATA
;	INCLUDE	..\TUME_ART\OLDSTUFF\CASTLE1\CAST01A.ANI	;ANIM DMA DATA
;	INCLUDE	..\TUME_ART\OLDSTUFF\CASTLE1\CAST01B.ANI	;ANIM DMA DATA
;	INCLUDE	..\TUME_ART\OLDSTUFF\CASTLE1\CAST01C.ANI	;ANIM DMA DATA
;
;CAST01A_FRAMES	DW	AMAP_CAST01A0_LEN/8	;# OF FRAMES
;CAST01B_FRAMES	DW	AMAP_CAST01B0_LEN/8	;# OF FRAMES
;CAST01C_FRAMES	DW	AMAP_CAST01C0_LEN/8	;# OF FRAMES
;
;CAST01A_RATE	DW	3	;FRAME RATE


BIGGEAR_DATA	INCBIN	..\TUME_ART\BONUS\BIGGEAR.ADT 	;TILE DATA
	INCLUDE	..\TUME_ART\BONUS\BGANIM.ANI	;ANIM DMA DATA

CAST01A_FRAMES	DW	AMAP_BGANIM0_LEN/8	;# OF FRAMES

CAST01A_RATE	DW	3	;FRAME RATE

;------------------
;CASTLE LEVEL

	SECTION	LVL1_SEC,?

	IF	LEVEL01_ART
           
LVL1_BLOCK	INCBIN	..\TUME_ART\LEVEL01\LVL01T.BL~
LVL1_BLK_SZ	EQU	*-LVL1_BLOCK
LVL1_CHAR	INCBIN	..\TUME_ART\LEVEL01\LVL01T.CH~
LVL1_CHR_SZ	EQU	*-LVL1_CHAR
LVL1_PAL	INCLUDE	..\TUME_ART\LEVEL01\LVL01T.PAL
LVL1_MAP	INCBIN	..\TUME_ART\LEVEL01\LVL01T.MA~
LVL1_FLR	INCBIN	..\TUME_ART\LEVEL01\LVL01T.FL~

	ELSE
           
LVL1_BLOCK	INCBIN	..\TUME_ART\GENERIC\LEVEL01\LVL01T.BL~
LVL1_BLK_SZ	EQU	*-LVL1_BLOCK
LVL1_CHAR	INCBIN	..\TUME_ART\GENERIC\LEVEL01\LVL01T.CH~
LVL1_CHR_SZ	EQU	*-LVL1_CHAR
LVL1_PAL	INCLUDE	..\TUME_ART\GENERIC\LEVEL01\LVL01T.PAL
LVL1_MAP	INCBIN	..\TUME_ART\GENERIC\LEVEL01\LVL01T.MA~
LVL1_FLR	INCBIN	..\TUME_ART\GENERIC\LEVEL01\LVL01T.FL~

	ENDIF

;------------------
;ROOKERY LEVEL

	SECTION	LVL2_SEC,?

	IF	LEVEL02_ART
           
LVL2_BLOCK	INCBIN	..\TUME_ART\LEVEL02\LVL02T.BL~
LVL2_BLK_SZ	EQU	*-LVL2_BLOCK
LVL2_CHAR	INCBIN	..\TUME_ART\LEVEL02\LVL02T.CH~
LVL2_CHR_SZ	EQU	*-LVL2_CHAR
LVL2_PAL	INCLUDE	..\TUME_ART\LEVEL02\LVL02T.PAL
LVL2_MAP	INCBIN	..\TUME_ART\LEVEL02\LVL02T.MA~
LVL2_FLR	INCBIN	..\TUME_ART\LEVEL02\LVL02T.FL~

	ELSE
           
LVL2_BLOCK	INCBIN	..\TUME_ART\GENERIC\LEVEL02\LVL02T.BL~
LVL2_BLK_SZ	EQU	*-LVL2_BLOCK
LVL2_CHAR	INCBIN	..\TUME_ART\GENERIC\LEVEL02\LVL02T.CH~
LVL2_CHR_SZ	EQU	*-LVL2_CHAR
LVL2_PAL	INCLUDE	..\TUME_ART\GENERIC\LEVEL02\LVL02T.PAL
LVL2_MAP	INCBIN	..\TUME_ART\GENERIC\LEVEL02\LVL02T.MA~
LVL2_FLR	INCBIN	..\TUME_ART\GENERIC\LEVEL02\LVL02T.FL~

	ENDIF

;------------------
;ATRIUM LEVEL

	SECTION	LVL3_SEC,?

	IF	LEVEL03_ART
           
LVL3_BLOCK	INCBIN	..\TUME_ART\LEVEL03\LVL03T.BL~
LVL3_BLK_SZ	EQU	*-LVL3_BLOCK
LVL3_CHAR	INCBIN	..\TUME_ART\LEVEL03\LVL03T.CH~
LVL3_CHR_SZ	EQU	*-LVL3_CHAR
LVL3_PAL	INCLUDE	..\TUME_ART\LEVEL03\LVL03T.PAL
LVL3_MAP	INCBIN	..\TUME_ART\LEVEL03\LVL03T.MA~
LVL3_FLR	INCBIN	..\TUME_ART\LEVEL03\LVL03T.FL~

	ELSE

LVL3_BLOCK	INCBIN	..\TUME_ART\GENERIC\LEVEL03\LVL03T.BL~
LVL3_BLK_SZ	EQU	*-LVL3_BLOCK
LVL3_CHAR	INCBIN	..\TUME_ART\GENERIC\LEVEL03\LVL03T.CH~
LVL3_CHR_SZ	EQU	*-LVL3_CHAR
LVL3_PAL	INCLUDE	..\TUME_ART\GENERIC\LEVEL03\LVL03T.PAL
LVL3_MAP	INCBIN	..\TUME_ART\GENERIC\LEVEL03\LVL03T.MA~
LVL3_FLR	INCBIN	..\TUME_ART\GENERIC\LEVEL03\LVL03T.FL~

	ENDIF

;------------------

	SECTION	LVL4_SEC1,?

LVL4_PAL	INCLUDE	..\TUME_ART\OLDSTUFF\ASCENT\DSCT2.PAL
LVL4_MAP	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\DSCT2.MA~

;------------------

	SECTION	LVL4_SEC2,?

LVL4_BLOCK	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\DSCENT01.BL~
LVL4_BLK_SZ	EQU	*-LVL4_BLOCK
LVL4_CHAR	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\DSCENT01.CH~
LVL4_CHR_SZ	EQU	*-LVL4_CHAR
LVL4_FLR	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\DSCENT01.FL~

;------------------

	SECTION	LVL4_SEC3,?

LVL4_MAP2	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\DSCT1.MA~

;------------------
;MUSEUM LEVEL

	SECTION	LVL5_SEC,?

	IF	LEVEL05_ART
           
LVL5_BLOCK	INCBIN	..\TUME_ART\LEVEL05\LVL05T.BL~
LVL5_BLK_SZ	EQU	*-LVL5_BLOCK
LVL5_CHAR	INCBIN	..\TUME_ART\LEVEL05\LVL05T.CH~
LVL5_CHR_SZ	EQU	*-LVL5_CHAR
LVL5_PAL	INCLUDE	..\TUME_ART\LEVEL05\LVL05T.PAL
LVL5_MAP	INCBIN	..\TUME_ART\LEVEL05\LVL05T.MA~
LVL5_FLR	INCBIN	..\TUME_ART\LEVEL05\LVL05T.FL~

	ELSE   
        
LVL5_BLOCK	INCBIN	..\TUME_ART\GENERIC\LEVEL05\LVL05T.BL~
LVL5_BLK_SZ	EQU	*-LVL5_BLOCK
LVL5_CHAR	INCBIN	..\TUME_ART\GENERIC\LEVEL05\LVL05T.CH~
LVL5_CHR_SZ	EQU	*-LVL5_CHAR
LVL5_PAL	INCLUDE	..\TUME_ART\GENERIC\LEVEL05\LVL05T.PAL
LVL5_MAP	INCBIN	..\TUME_ART\GENERIC\LEVEL05\LVL05T.MA~
LVL5_FLR	INCBIN	..\TUME_ART\GENERIC\LEVEL05\LVL05T.FL~

	ENDIF

;------------------

	SECTION	LVL6_SEC1,?

LVL6_PAL	INCLUDE	..\TUME_ART\OLDSTUFF\ASCENT\ASCENT.PAL
LVL6_MAP	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\ASCENT.MA~
LVL6_MAPB	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\ASCENTF2.MA~
LVL6_BLOCK	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\ASCENT.BL~
LVL6_BLK_SZ	EQU	*-LVL6_BLOCK
LVL6_CHAR	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\ASCENT.CH~
LVL6_CHR_SZ	EQU	*-LVL6_CHAR
LVL6_FLR	INCBIN	..\TUME_ART\OLDSTUFF\ASCENT\ASCENT.FL~


;------------------

	SECTION	MODE7A,PAGE10_GROUP

MODE7_DATA1	INCBIN	..\TUME_ART\OLDSTUFF\MODE7\MODE7.M7R

;MODE7_DATA1	DCW	1024,$1020	;
;	DCW	1024,$2001
;	DCW	1024,$3001
;	DCW	1024,$4001
;	DCW	1024,$5001
;	DCW	1024,$6001
;	DCW	1024,$7001
;	DCW	1024,$8001
MODE7_SZ1	EQU	*-MODE7_DATA1

;------------------

	SECTION	MODE7B,PAGE11_GROUP

MODE7_DATA2	INCBIN	..\TUME_ART\OLDSTUFF\MODE7\MODE7.CK1
MODE7_SZ2	EQU	*-MODE7_DATA2

MODE7_PAL	INCLUDE	..\TUME_ART\OLDSTUFF\MODE7\MODE7.PAL

;MODE7_PAL	DCW	16,$0000	;BLACK	INCLUDE	..\TUME_ART\OLDSTUFF\MODE7\MODE7.PAL
;	DCW	16,$001F	;RED
;	DCW	16,$03E0	;GREEN
;	DCW	16,$7C00	;BLUE
;	DCW	16,$03FF	;GREEN+RED
;	DCW	16,$7C1F	;BLUE+RED
;	DCW	16,$7FE0	;BLUE+GREEN

MODE7_MAP	DCB	1000,0	;INCBIN	..\TUME_ART\OLDSTUFF\MODE7\MODE7.M7R
MODE7_SZ3	EQU	*-MODE7_MAP

;------------------

	SECTION	LVL7_SEC,?

	IF	LEVEL07_ART

LVL7_BLOCK	INCBIN	..\TUME_ART\LEVEL07\LVL07T.BL~
LVL7_BLK_SZ	EQU	*-LVL7_BLOCK
LVL7_CHAR	INCBIN	..\TUME_ART\LEVEL07\LVL07T.CH~
LVL7_CHR_SZ	EQU	*-LVL7_CHAR
LVL7_PAL	INCLUDE	..\TUME_ART\LEVEL07\LVL07T.PAL
LVL7_MAP	INCBIN	..\TUME_ART\LEVEL07\LVL07T.MA~
LVL7_FLR	INCBIN	..\TUME_ART\LEVEL07\LVL07T.FL~

	ELSE

LVL7_BLOCK	INCBIN	..\TUME_ART\GENERIC\LEVEL07\LVL07T.BL~
LVL7_BLK_SZ	EQU	*-LVL7_BLOCK
LVL7_CHAR	INCBIN	..\TUME_ART\GENERIC\LEVEL07\LVL07T.CH~
LVL7_CHR_SZ	EQU	*-LVL7_CHAR
LVL7_PAL	INCLUDE	..\TUME_ART\GENERIC\LEVEL07\LVL07T.PAL
LVL7_MAP	INCBIN	..\TUME_ART\GENERIC\LEVEL07\LVL07T.MA~
LVL7_FLR	INCBIN	..\TUME_ART\GENERIC\LEVEL07\LVL07T.FL~

	ENDIF

;------------------
;BONUS LEVEL

	SECTION	BONUS_SEC,?

BONUS_BLOCK	INCBIN	..\TUME_ART\BONUS\BIGGEAR.BL~
BONUS_BLK_SZ	EQU	*-BONUS_BLOCK
BONUS_CHAR	INCBIN	..\TUME_ART\BONUS\BIGGEAR.CH~
BONUS_CHR_SZ	EQU	*-BONUS_CHAR
BONUS_PAL	INCLUDE	..\TUME_ART\BONUS\BIGGEAR.PAL
BONUS_MAP	INCBIN	..\TUME_ART\BONUS\BIGGEAR.MA~
BONUS_FLR	INCBIN	..\TUME_ART\BONUS\BIGGEAR.FL~

;------------------
;END OF THE GAME HERE

	END	BEGIN







	
