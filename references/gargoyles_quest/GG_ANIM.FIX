;------------------
;
;  GG_ANIM.658 -- ANIMATION ROUTINES
;
;------------------
;ANIMATION COMMANDS

DRAW	EQU	-1
LOOP	EQU	-2
STOP	EQU	-3
INC_FLAG	EQU	-4
SET_X_SCROLL	EQU	-5
SET_Y_SCROLL	EQU	-6

;------------------
;DRAW_FRAME -- DRAW ONE FRAME OF THE ANIMATION


;-----

OBJ_SIZE_TABLE	DB	$00,$60,$A0,$A0,$A0	;SIZE OF SPRITES
OBJ_MASK	DB	$01,$04,$10,$40

;-----


DRAW_FRAME	MODULE

	PHP
;-----
	SET_A8
	LDA	#^OBJ_SIZE_TABLE
	PHA
	PLB
	SET_XY16
	LDA	#1
	STA	DMA_FLAG	;NEED TO DO A DMA LATER
	STZ	EXTRA_SPRITE
	LDY	#0
	LDA	[FRAME_ADDR],Y	;GET # OF SPRITES IN FRAME
	STA	SPRITE_COUNT
	CMP	MAX_SPRITE	;USES ALL THE SPRITE ?
	BEQ	@10
	SEC
	LDA	MAX_SPRITE	;MAX USED BY THIS ANIM
	SBC	[FRAME_ADDR],Y	;= # TO CLEAR AT END
	STA	EXTRA_SPRITE
@10
	INY
	INY		;POINT TO 1ST X OFFSET
;
	LDX	#0
	STZ	THIS_SPRITE	;SPRITE # BEING WORKED ON
	STZ	THIS_SPRITE_MSB	;SPRITE # BEING WORKED ON
	STZ	THIS_SPRITE_BYTE	;SPRITE # BEING WORKED ON
;-----
;FIRST SEE IF THIS NEEDS TO BE A LARGE SPRITE OR NOT
@100	LDA	SPRITE_SIZE	;IS LARGE SPRITE ON ?
	CMP	#64
	BNE	@DF_SMALL
	PHX
	PHY
	SET_XY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ASL		;MAKE SIZE BIT
	ORA	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
;-----	
@DF_SMALL	LDA	FLIP_DATA
	BIT	#$40	;X FLIPPED ?
	BNE	@DF_XFLIP

	LDA	[FRAME_ADDR],Y	;GET X OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	CLC
	ADC	X_OFF
	JMP	@CONT2
;-----
	LONGA OFF
@DF_XFLIP	LDA	[FRAME_ADDR],Y	;GET X OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	STA	TEMP_X
	LDA	X_OFF	;SUB FROM X POSITION
	SEC
	SBC	TEMP_X
@CONT2	STA	OAM_DATA,X
	CMP	#$0100
	BGE	@SET_XMSB

;-----
;CLEAR THE X MSB OF THIS SPRITE
	PHX
	PHY
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	EOR	#$FF
	AND	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
	JMP	@CONT
;-----
;SET THE X MSB OF THIS SPRITE
@SET_XMSB	PHX
	PHY
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
;-----
@CONT	INX
	INY
;
	LDA	FLIP_DATA
	BIT	#$80	;Y FLIPPED ?
	BNE	@DF_YFLIP

	LDA	[FRAME_ADDR],Y	;GET Y OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	CLC
	ADC	Y_OFF
	JMP	@CONT3
;
@DF_YFLIP	LDA	[FRAME_ADDR],Y	;GET Y OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	STA	TEMP_X
	LDA	Y_OFF	;SUB FROM X POSITION
	SEC
	SBC	TEMP_X
@CONT3	STA	OAM_DATA,X
;
	INX
	INY

	LDA	[FRAME_ADDR],Y	;GET TILE #
	SET_A8

	STA	OAM_DATA,X	;TILE #
	INX
	XBA		;MOVE HIGH BIT OF TILE #

;***** NEED TO PUT IN CODE FOR LARGER TILE #S AND DIFFERENT PALETTES AND PRIORITIES

	ORA	FLIP_DATA	;ADD SPRITE FLIP DATA
	ORA	#$20	;TEMP PRIORITY VALUE
	STA	OAM_DATA,X	;FLIP, PRIORITY, COLOR
	INX
	INY
	INY
	STY	DMA_LIST	;SAVE POINTER TO DMA LIST
	INY		;TO NEXT X
;-----
	INC	THIS_SPRITE	;NEXT SPRITE
	LDA	THIS_SPRITE
	LSR
	LSR
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION
;
	LDA	THIS_SPRITE	
	CMP	SPRITE_COUNT	;DID ALL SPRITE FOR THIS OBJECT ?
	BEQ	@DF_DONE
	JMP	@100
@DF_DONE	SET_A16
;-----
	SET_AXY8
	LDA	EXTRA_SPRITE	;CLEAR ANY EXTRA ONES
	TAY
	CPY	#0
	BEQ	@300
@200
	PHY
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;SET THE MSB
	STA	OAM_DATA+512,Y
	PLY
	INC	THIS_SPRITE	;NEXT SPRITE
	LDA	THIS_SPRITE
	LSR
	LSR
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION

	DEY
	BNE	@200
@300
;-----
	LDA	#1
	STA	OAM_DMA_FLAG	;NEED TO SEND DATA TO OAM MEMORY
;-----
	LDA	#1
	STA	VRAM_DMA_FLAG	;NEED TO SEND DATA TO VRAM
	SET_AXY16
	LDX	DMA_HEAP_PTR	;GET DMA HEAP POINTER
	SET_A8
	LDA	#1
	STA	DMA_HEAP,X	;SPRITE DMA LIST COMMAND
	INX
	SET_A16
	LDA	DMA_LIST	;GET OFFSET OF DMA_LIST
	CLC
	ADC	FRAME_ADDR	;ADD OFFSET OF FRAME DATA
	STA	DMA_HEAP,X	;SAVE OFFSET TO DMA HEAP
	INX
	INX
	LDA	#0
	ADC	FRAME_ADDR+2	;ADD BANK OF FRAME DATA
	STA	DMA_HEAP,X
	INX
	INX
	STZ	DMA_HEAP,X	;SET END
	STX	DMA_HEAP_PTR	;SAVE POINTER
;-----	
	PLP
	RTL

	MODEND

;------------------
;DO_ANIM_CMDS -- EXECUTE THE ANIM COMMANDS

DO_ANIM_CMDS	MODULE

	PHP
	SET_XY16
@10	LDY	#0
	SET_A8
	LDA	[THIS_OBJ_ANIM],Y	;GET COMMAND
	CMP	#LOOP	;DO A LOOP
	BNE	@200
    	SET_A16
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET ADDRESS TO LOOP TO
	TAX
	INY
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET BANK
	STX	THIS_OBJ_ANIM	
	STA	THIS_OBJ_ANIM+2
	LDY	OBJ_DWORD		;SET THE OBJECT BEING DONE
	STA	OBJ_ANIM+2,Y		;SAVE THE NEW ANIM ADDRESS
	TXA		
	STA	OBJ_ANIM,Y
	JMP	@10
;-----
	LONGA OFF
@200	CMP	#STOP		;STOP ANIMATION
	BNE	@300
	SET_A16
	LDX	OBJ_WORD
	LDA	#-1
	STA	OBJ_FLAG,X		;SET TO STOPED
	PLP
	RTL
;-----
	LONGA OFF
@300	CMP	#INC_FLAG		;INC THE OBJECT FLAG ?
	BNE	@400
	SET_A16
	LDX	OBJ_WORD
	INC	OBJ_FLAG,X		;SET TO STOPED
;
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#1
	STA	OBJ_ANIM,X
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
	INY
	JMP	@DO_ANIM
;-----
	LONGA OFF
@400	CMP	#SET_Y_SCROLL
	BNE	@500
	SET_A16
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET NEW Y SCROLL RATE
	STA	YSCROLL_RATE
	INY
	INY
;
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#3
	STA	OBJ_ANIM,X
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
	JMP	@DO_ANIM
;-----
@500


;-----
@DO_ANIM	SET_A16
	INY
	CLC
	LDA	NMI_CTR
	ADC	[THIS_OBJ_ANIM],Y 	;ADD TIME TO NEXT FRAME
	LDX	OBJ_WORD		;GET OBJECT WORD PTR
	STA	OBJ_TIME,X		;SAVE TIME TO NEXT CALL
	INY
	INY
;
	PHX
	TXA
	LSR	A		;BYTE PTR
	TAX
;
	STZ	FLIP_WIDTH
	STZ	FLIP_HEIGTH
;
	SET_A8
	LDA	OBJ_SPR_SIZE,X		;SET SPRITE SIZE
	STA	SPRITE_SIZE
;
	LDA	OBJ_MAX_SPR,X		;SET MAX SPRITES
	STA	MAX_SPRITE
;
	LDA	OBJ_WIDTH,X		;GET OBJECT WIDTH
	STA	FLIP_WIDTH
;
	LDA	OBJ_HEIGTH,X		;GET OBJECT HEIGTH
	STA	FLIP_HEIGTH
;
	LDA	OBJ_FLIP,X		;GET FLIP DATA
	STA	FLIP_DATA
	PLX
;
	BIT	#$40	;FLIPPED IN X ?
	BEQ	@NO_X_FLIP
	SET_A16
	LDA	OBJ_X,X	;GET CURRENT X
	SEC
	SBC	[THIS_OBJ_ANIM],Y	;SUB DELTA X
	STA	OBJ_X,X
	SEC
	SBC	MAP1_X	;ADJUST FOR SCREEN POSITION
	CLC
	ADC	FLIP_WIDTH	;ADJUST FOR FLIP
	JMP	@NEW_OBJ_X
;
@NO_X_FLIP	SET_A16
	LDA	OBJ_X,X	;GET CURRENT X
	CLC
	ADC	[THIS_OBJ_ANIM],Y	;ADD DELTA X
	STA	OBJ_X,X	;SAVE NEW X
	SEC
	SBC	MAP1_X	;ADJUST FOR SCREEN POSITION ON MAP
@NEW_OBJ_X	STA	X_OFF	;SAVE FOR THIS DRAW
	INY
	INY
;
	SET_A8
	LDA	FLIP_DATA	;SEE IF FLIPPED IN Y
	BIT	#$80
	BEQ	@NO_Y_FLIP
	SET_A16
	LDA	OBJ_Y,X
	SEC
	SBC	[THIS_OBJ_ANIM],Y	;SUB DELTA Y
	JMP	@NEW_OBJ_Y
;
@NO_Y_FLIP	SET_A16
	CLC
	LDA	OBJ_Y,X	;GET CURRENT Y
	ADC	[THIS_OBJ_ANIM],Y	;ADD DELTA Y
@NEW_OBJ_Y	STA	OBJ_Y,X
	SEC
	SBC	MAP1_Y	;ADJUST FOR SCREEN POSITION ON MAP
	STA	Y_OFF	;SAVE FOR THIS DRAW
	INY
	INY
;
	LDA	[THIS_OBJ_ANIM],Y	;GET ADDRESS OF PICTURE TO DRAW
	STA	FRAME_ADDR	;SET ADDRESS OF FRAME DATA
	INY
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET BANK
	STA	FRAME_ADDR+2
;
	JSL	DRAW_FRAME
;
	SET_AXY16
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#11
	STA	OBJ_ANIM,X
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
;-----
@999	PLP
	RTL

	MODEND

;------------------
;DMA_ANIM -- DMA THE SPRITE DATA TO VRAM

DMA_ANIM	MODULE
 
	PHP
;-----
	SET_A8
	LDA	#$80
	STA	VMAINC
	LDA	#1
	PHA
	PLB

	SET_AXY16
	LDY	DMA_LIST
	SET_A8
	LDA	[FRAME_ADDR],Y	;GET # OF DMA TRANSFERS TO DO

	STA	DMA_TOTAL
	INY
;
DMA_LP	SET_A8

	STZ	MDMAEN	;CLEAR DMA CHANNEL

	LDA	#$01
	STA	WDMAP0	;DMA PARAMETER DATA FOR CHANNEL 0

	LDA	#<WVMDATAL	;WRITE TO HERE
	STA	BADMA0

	SET_A16
	LDA	[FRAME_ADDR],Y	;GET OFFSET OF DATA IN CHR FILE
	INY
	INY
	INY
	INY

	SET_A8
	STA	A1ADMAL0	;LOW
	XBA
	STA	A1ADMAH0	;HIGH
	LDA	#1
	STA	A1ADMAB0	;BANK

	SET_A16
	LDA	[FRAME_ADDR],Y	;GET DEST
	CLC
	ADC	#$2000
	STA	VMADDL
	INY
	INY

     	SET_A16
	LDA	[FRAME_ADDR],Y	;GET # OF BYTES OF DATA
	INY
	INY

	SET_A8
	STA	DMADATL0	;LOW
	XBA
	STA	DMADATH0	;HIGH

	SET_A8
	LDA	#1
	STA	MDMAEN	;START DMA

	LDA	DMA_TOTAL
	SEC
	SBC	#1
	STA	DMA_TOTAL
	CMP	#$00
	BNE	DMA_LP
;-----
	PLP
	RTS

	MODEND

;------------------
;CHAR_ANIM_HANDLER -- SEE IF AN ANIMATION NEEDS TO BE DONE

CHAR_ANIM_HANDLER	MODULE

	PHP
	SET_AXY16
;-----
	LDA	CA1_ADDRESS	;SEE IF ANY ANIM SET
	CMP	#0
	BEQ	CAH_OFF
	DEC	CA1_TIMER	;COUTDOWN TO NEXT FRAME
	BEQ	CAH_DO
CAH_OFF	PLP
	RTL
;-----
CAH_DO	LDA	CA1_RATE
	STA	CA1_TIMER	;RESET TIMER
;
	LDA	CA1_FRAME
	ASL	A
	ASL	A
	ASL	A	;FRAME # x 8 MAKES OFFSET
	TAY
;
	LDX	DMA_HEAP_PTR
	SET_A8
	LDA	#^DMA_HEAP
	PHA
	PLB		;SET BANK OF DMA_HEAP
;
	LDA	#2	;SINGLE DMA
	STA	DMA_HEAP,X	;DMA COMMAND
	INX
;
	LDA	#$80
	STA	DMA_HEAP,X
	INX
;
	SET_A16
	LDA	[CA1_ADDRESS],Y	;GET OFFSET OF SOURCE
	INY
	INY
	STA	DMA_HEAP,X
	INX
	INX
;
	LDA	[CA1_ADDRESS],Y	;GET BANK
	INY
	INY
	SET_A8
	STA	DMA_HEAP,X
	INX
;
	SET_A16
	LDA	[CA1_ADDRESS],Y	;GET VRAM DEST
	INY
	INY
	CLC
	ADC	CA1_CHAR_BASE	;ADD BASE ADDRESS OF CHAR DATA
	STA	DMA_HEAP,X
	INX
	INX
;
	LDA	[CA1_ADDRESS],Y	;GET # OF BYTES
	INY
	INY
	STA	DMA_HEAP,X
	INX
	INX
	STZ	DMA_HEAP,X	;END OF DMA_HEAP
	STX	DMA_HEAP_PTR	;SAVE PTR
;
	LDA	#1
	STA	VRAM_DMA_FLAG	;FORCE DMA
;-----
	INC	CA1_FRAME	;NEXT FRAME #
	LDA	CA1_FRAME
	CMP	CA1_FRAMES	;DID ALL ?
	BNE	CAH_EXIT
	STZ	CA1_FRAME	;LOOP THEN
;-----
CAH_EXIT	PLP
	RTL


	MODEND

;------------------
;------------------
;------------------
;------------------
;------------------
