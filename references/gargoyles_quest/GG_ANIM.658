;------------------
;
;  GG_ANIM.658 -- ANIMATION ROUTINES
;
;------------------
;ANIMATION COMMANDS

MOVE	EQU	0
DRAW	EQU	1
LOOP	EQU	2
STOP	EQU	3
INC_FLAG	EQU	4
SET_X_SCROLL	EQU	5
SET_Y_SCROLL	EQU	6


;------------------
;------------------
;DO_ANIM_CMDS -- EXECUTE THE ANIM COMMANDS

;-----

ANIM_CMDS	DW	ANIM_MOVE	;0
	DW	ANIM_DRAW	;1
	DW	ANIM_LOOP	;2
	DW	ANIM_STOP	;3
	DW	ANIM_INC_FLAG	;4
	DW	ANIM_XSCROLL	;5
	DW	ANIM_YSCROLL	;6

;-----

DO_ANIM_CMDS	MODULE

	PHP
	PHB
;-----
	SET_A8
	LDA	#^ANIM_CMDS
	PHA
	PLB
;-----
	SET_XY16
ANIM_MAIN_LP	LDY	#0
	SET_A16
	LDA	#0
	SET_A8
	LDA	[THIS_OBJ_ANIM],Y	;GET COMMAND
	ASL	A
	TAX
	JMP	(ANIM_CMDS,X)

	MODEND

;------------------
;LOOP TO THE NEXT 4 BYTE ADDRESS

ANIM_LOOP	MODULE

    	SET_A16
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET ADDRESS TO LOOP TO
	TAX
	INY
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET BANK
	STX	THIS_OBJ_ANIM	
	STA	THIS_OBJ_ANIM+2
	LDY	OBJ_DWORD		;SET THE OBJECT BEING DONE
	STA	OBJ_ANIM+2,Y		;SAVE THE NEW ANIM ADDRESS
	TXA		
	STA	OBJ_ANIM,Y
	JMP	ANIM_MAIN_LP

	MODEND

;------------------
;STOP ANIMATION

ANIM_STOP	MODULE

	SET_A16
	LDX	OBJ_WORD
	LDA	#-1
	STA	OBJ_FLAG,X		;SET TO STOPED
	PLB
	PLP
	RTL

	MODEND

;------------------
;INC THIS OBJECTS FLAG

ANIM_INC_FLAG	MODULE

	SET_A16
	LDX	OBJ_WORD
	INC	OBJ_FLAG,X		;SET TO STOPED
;
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#1
	STA	OBJ_ANIM,X
	STA	THIS_OBJ_ANIM
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
	STA	THIS_OBJ_ANIM+2
	INY
	JMP	ANIM_MAIN_LP

	MODEND

;------------------
;SET THE MAX_YRATE VALUE

ANIM_YSCROLL	MODULE

	SET_A16
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET NEW Y SCROLL RATE
;	STA	YSCROLL_RATE
	STA	MAX_YRATE
	INY
	INY
;
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#3
	STA	OBJ_ANIM,X
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
	JMP	ANIM_MAIN_LP

	MODEND

;------------------
;SET THE MAX_XRATE VALUE

ANIM_XSCROLL	MODULE

	SET_A16
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET NEW X SCROLL RATE
;	STA	XSCROLL_RATE
	STA	MAX_XRATE
	INY
	INY
;
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#3
	STA	OBJ_ANIM,X
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
	JMP	ANIM_MAIN_LP

	MODEND

;------------------
;DRAW NEW ANIMATION FRAME

ANIM_DRAW	MODULE
	
	SET_A16
	INY

	LDX	OBJ_WORD		;GET OBJECT WORD PTR
	TXA
	LSR	A		;BYTE PTR
	TAX
;
	STZ	FLIP_WIDTH
	STZ	FLIP_HEIGTH
;
	SET_A8
;
	LDA	OBJ_SPRITE,X		;STARTING SPRITE #
	STA	THIS_SPRITE
;
	LDA	OBJ_SPR_SIZE,X		;SET SPRITE SIZE
	STA	SPRITE_SIZE
;
	LDA	OBJ_MAX_SPR,X		;SET MAX SPRITES
	STA	MAX_SPRITE
;
	LDA	OBJ_WIDTH,X		;GET OBJECT WIDTH
	STA	FLIP_WIDTH
;
	LDA	OBJ_HEIGTH,X		;GET OBJECT HEIGTH
	STA	FLIP_HEIGTH
;
	LDA	OBJ_FLIP,X		;GET FLIP DATA
	STA	FLIP_DATA
;        
	LDX	OBJ_WORD		;GET OBJECT WORD PTR
;
	BIT	#$40	;FLIPPED IN X ?
	BEQ	@NO_X_FLIP
	SET_A16
	LDA	OBJ_X,X	;GET CURRENT X
	SEC
	SBC	[THIS_OBJ_ANIM],Y	;SUB DELTA X
	STA	OBJ_X,X
	SEC
	SBC	MAP1_X	;ADJUST FOR SCREEN POSITION
	CLC
	ADC	FLIP_WIDTH	;ADJUST FOR FLIP
	JMP	@NEW_OBJ_X
;
@NO_X_FLIP	SET_A16
	LDA	OBJ_X,X	;GET CURRENT X
	CLC
	ADC	[THIS_OBJ_ANIM],Y	;ADD DELTA X
	STA	OBJ_X,X	;SAVE NEW X
	SEC
	SBC	MAP1_X	;ADJUST FOR SCREEN POSITION ON MAP
@NEW_OBJ_X	STA	X_OFF	;SAVE FOR THIS DRAW
;
;	CPX	#0	;IS THIS GOLIATH ?
;	BNE	@NOT_GOL
;	LDA	[THIS_OBJ_ANIM],Y	;GET DELTA X
;	STA	OBJ_DX
;
;	INY
;	INY
;
;	LDA	[THIS_OBJ_ANIM],Y	;GET DELTA Y
;	BIT	#$8000
;	BEQ	@DYPOS
;	EOR	#$FFFF
;	INC	A
;@DYPOS	STA	OBJ_DY
;	JMP	@IS_GOL
;
@NOT_GOL	INY
	INY
;
@IS_GOL	SET_A8
	LDA	FLIP_DATA	;SEE IF FLIPPED IN Y
	BIT	#$80
	BEQ	@NO_Y_FLIP
	SET_A16
	LDA	OBJ_Y,X
	SEC
	SBC	[THIS_OBJ_ANIM],Y	;SUB DELTA Y
	JMP	@NEW_OBJ_Y
;
@NO_Y_FLIP	SET_A16
	CLC
	LDA	OBJ_Y,X	;GET CURRENT Y
	ADC	[THIS_OBJ_ANIM],Y	;ADD DELTA Y
@NEW_OBJ_Y	STA	OBJ_Y,X
	SEC
	SBC	MAP1_Y	;ADJUST FOR SCREEN POSITION ON MAP
	STA	Y_OFF	;SAVE FOR THIS DRAW
	INY
	INY
;-----
;SAVE THE ADDRESS OF THE FRAME DATA FOR MOVE COMMANDS
;
	LDX	OBJ_DWORD	;GET DWORD PTR
	LDA	[THIS_OBJ_ANIM],Y	;GET ADDRESS OF PICTURE TO DRAW
	STA	OBJ_FRAME,X	;SAVE FRAME ADDRESS
	STA	FRAME_ADDR	;SET ADDRESS OF FRAME DATA
	INY
	INY
	LDA	[THIS_OBJ_ANIM],Y	;GET BANK
	STA	OBJ_FRAME+2,X	;SAVE FRAME ADDRESS
	STA	FRAME_ADDR+2
;-----
	JSL	DRAW_FRAME	;DRAW AND MOVE THE OBJECT SPRITES
;-----
;SET THE ADDRESS OF OBJ_ANIM TO THE NEXT COMMAND
	SET_AXY16
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#9	;SIZE OF DRAW COMMAND DATA
	STA	OBJ_ANIM,X
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
;-----
@999	PLB
	PLP		;NO MORE COMMAND EXECUTED AFTER THIS ONE
	RTL

	MODEND

;------------------
;MOVE CURRENT OBJECT SPRITE

ANIM_MOVE	MODULE
	
	SET_A16
	INY

	LDX	OBJ_WORD		;GET OBJECT WORD PTR
	TXA
	LSR	A		;BYTE PTR
	TAX
;
	STZ	FLIP_WIDTH
	STZ	FLIP_HEIGTH
;
	SET_A8
;
	LDA	OBJ_SPRITE,X		;STARTING SPRITE #
	STA	THIS_SPRITE
;
	LDA	OBJ_SPR_SIZE,X		;SET SPRITE SIZE
	STA	SPRITE_SIZE
;
	LDA	OBJ_MAX_SPR,X		;SET MAX SPRITES
	STA	MAX_SPRITE
;
	LDA	OBJ_WIDTH,X		;GET OBJECT WIDTH
	STA	FLIP_WIDTH
;
	LDA	OBJ_HEIGTH,X		;GET OBJECT HEIGTH
	STA	FLIP_HEIGTH
;
	LDA	OBJ_FLIP,X		;GET FLIP DATA
	STA	FLIP_DATA
;        
	LDX	OBJ_WORD		;GET OBJECT WORD PTR
;
	BIT	#$40	;FLIPPED IN X ?
	BEQ	@NO_X_FLIP
	SET_A16
	LDA	OBJ_X,X	;GET CURRENT X
	SEC
	SBC	[THIS_OBJ_ANIM],Y	;SUB DELTA X
	STA	OBJ_X,X
	SEC
	SBC	MAP1_X	;ADJUST FOR SCREEN POSITION
	CLC
	ADC	FLIP_WIDTH	;ADJUST FOR FLIP
	JMP	@NEW_OBJ_X
;
@NO_X_FLIP	SET_A16
	LDA	OBJ_X,X	;GET CURRENT X
	CLC
	ADC	[THIS_OBJ_ANIM],Y	;ADD DELTA X
	STA	OBJ_X,X	;SAVE NEW X
	SEC
	SBC	MAP1_X	;ADJUST FOR SCREEN POSITION ON MAP
@NEW_OBJ_X	STA	X_OFF	;SAVE FOR THIS DRAW
;
;	CPX	#0	;IS THIS GOLIATH ?
;	BNE	@NOT_GOL
;	LDA	[THIS_OBJ_ANIM],Y	;GET DELTA X
;	STA	OBJ_DX
;
;	INY
;	INY
;
;	LDA	[THIS_OBJ_ANIM],Y	;GET DELTA Y
;	BIT	#$8000
;	BEQ	@DYPOS
;	EOR	#$FFFF
;	INC	A
;@DYPOS	STA	OBJ_DY
;	JMP	@IS_GOL
;
@NOT_GOL	INY
	INY
;
@IS_GOL	SET_A8
	LDA	FLIP_DATA	;SEE IF FLIPPED IN Y
	BIT	#$80
	BEQ	@NO_Y_FLIP
	SET_A16
	LDA	OBJ_Y,X
	SEC
	SBC	[THIS_OBJ_ANIM],Y	;SUB DELTA Y
	JMP	@NEW_OBJ_Y
;
@NO_Y_FLIP	SET_A16
	CLC
	LDA	OBJ_Y,X	;GET CURRENT Y
	ADC	[THIS_OBJ_ANIM],Y	;ADD DELTA Y
@NEW_OBJ_Y	STA	OBJ_Y,X
	SEC
	SBC	MAP1_Y	;ADJUST FOR SCREEN POSITION ON MAP
	STA	Y_OFF	;SAVE FOR THIS DRAW
;
	LDX	OBJ_DWORD	;GET DWORD PTR
	LDA	OBJ_FRAME,X	;GET ADDRESS OF PICTURE TO DRAW
	STA	FRAME_ADDR	;SET ADDRESS OF FRAME DATA
	LDA	OBJ_FRAME+2,X	;GET BANK
	STA	FRAME_ADDR+2
;-----
	JSL	MOVE_FRAME
;-----
	SET_AXY16
	LDX	OBJ_DWORD	;GET DWORD PTR
	CLC
	LDA	OBJ_ANIM,X
	ADC	#5	;SIZE OF MOVE COMMAND DATA
	STA	OBJ_ANIM,X
	INX
	INX
	LDA	OBJ_ANIM,X
	ADC	#0
	STA	OBJ_ANIM,X
;-----
@999	PLB
	PLP		;NO MORE COMMAND EXECUTED AFTER THIS ONE
	RTL

	MODEND

;------------------
;DRAW_FRAME -- DRAW ONE FRAME OF THE ANIMATION


;-----

OBJ_SIZE_TABLE	DB	$00,$60,$A0,$A0,$A0	;SIZE OF SPRITES
OBJ_MASK	DB	$01,$04,$10,$40

;-----


DRAW_FRAME	MODULE

	PHP
;-----
	SET_A8
	LDA	#^OBJ_SIZE_TABLE
	PHA
	PLB
	SET_XY16
	LDA	#1
	STA	DMA_FLAG	;NEED TO DO A DMA LATER
	STZ	EXTRA_SPRITE
	LDY	#0
	LDA	[FRAME_ADDR],Y	;GET # OF SPRITES IN FRAME
	STA	SPRITE_COUNT
	CMP	MAX_SPRITE	;USES ALL THE SPRITE ?
	BEQ	@10
	SEC
	LDA	MAX_SPRITE	;MAX USED BY THIS ANIM
	SBC	[FRAME_ADDR],Y	;= # TO CLEAR AT END
	STA	EXTRA_SPRITE
@10
	INY		;POINT TO 1ST PALETTE DATA
;	INY		;POINT TO 1ST X OFFSET
;
	SET_A16
	LDA	#0
	TAX
	SET_A8
	LDA	THIS_SPRITE
	ASL
	ASL
	TAX
	LDA	THIS_SPRITE
	LSR
	LSR
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION
;-----
;GET THE COLOR INFO FOR THIS SUB-SPRITE
@100	LDA	[FRAME_ADDR],Y	;GET PALETTE DATA
	STA	SPRITE_COLOR
	INY
;-----
;FIRST SEE IF THIS NEEDS TO BE A LARGE SPRITE OR NOT
	LDA	SPRITE_SIZE	;IS LARGE SPRITE ON ?
	CMP	#64
	BNE	@DF_SMALL
	PHX
	PHY
	SET_XY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ASL		;MAKE SIZE BIT
	ORA	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
;-----	
@DF_SMALL	LDA	FLIP_DATA
	BIT	#$40	;X FLIPPED ?
	BNE	@DF_XFLIP

	LDA	[FRAME_ADDR],Y	;GET X OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	CLC
	ADC	X_OFF
	JMP	@CONT2
;-----
	LONGA OFF
@DF_XFLIP	LDA	[FRAME_ADDR],Y	;GET X OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	STA	TEMP_X
	LDA	X_OFF	;SUB FROM X POSITION
	SEC
	SBC	TEMP_X
@CONT2	CMP	#-64
	BGE	@XOK1
	CMP	#256
	BLT	@XOK1
	LDA	#$120

@XOK1	SET_A8
	STA	OAM_DATA,X
	SET_A16
	CMP	#$0100
	BGE	@SET_XMSB

;-----
;CLEAR THE X MSB OF THIS SPRITE
	PHX
	PHY
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	EOR	#$FF
	AND	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
	JMP	@CONT
;-----
;SET THE X MSB OF THIS SPRITE
@SET_XMSB	PHX
	PHY
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
;-----
@CONT	INX
	INY
;
	LDA	FLIP_DATA
	BIT	#$80	;Y FLIPPED ?
	BNE	@DF_YFLIP

	LDA	[FRAME_ADDR],Y	;GET Y OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	CLC
	ADC	Y_OFF
	JMP	@CONT3
;
@DF_YFLIP	LDA	[FRAME_ADDR],Y	;GET Y OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	STA	TEMP_X
	LDA	Y_OFF	;SUB FROM Y POSITION
	SEC
	SBC	TEMP_X
@CONT3	SET_A8
	STA	OAM_DATA,X
	SET_A16
;-----
	CMP	#240	;PAST BOTTOM OF SCREEN
	BLT	@Y_OK
	PHX	     	;SO MOVE IT OFF THE SCREEN
	PHY
	DEX		;BACK TO X POSITION
	SET_A8
	LDA	#$10
	STA	OAM_DATA,X	;PUT AT $110
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_AXY16
	PLY
	PLX
;-----
@Y_OK	INX
	INY

	LDA	[FRAME_ADDR],Y	;GET TILE #
	SET_A8

	STA	OAM_DATA,X	;TILE #
	INX
	XBA		;MOVE HIGH BIT OF TILE #

;***** NEED TO PUT IN CODE FOR LARGER TILE #S AND DIFFERENT PALETTES AND PRIORITIES

	ORA	FLIP_DATA	;ADD SPRITE FLIP DATA
	ORA	SPRITE_COLOR	;ADD SPRITE COLOR DATA
	ORA	#$20	;TEMP PRIORITY VALUE
	STA	OAM_DATA,X	;FLIP, PRIORITY, COLOR
	INX
	INY
	INY
	STY	DMA_LIST	;SAVE POINTER TO DMA LIST
;	INY		;TO NEXT X
;-----
	INC	THIS_SPRITE	;NEXT SPRITE
	LDA	THIS_SPRITE
	LSR
	LSR
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION
;
	DEC	SPRITE_COUNT	;DID ALL SPRITE FOR THIS OBJECT ?
	BEQ	@DF_DONE
	JMP	@100
@DF_DONE	SET_A16
;-----
	SET_AXY8
	LDA	EXTRA_SPRITE	;CLEAR ANY EXTRA ONES
	TAY
	CPY	#0
	BEQ	@300
@200
	PHX
	PHY
	LDA	#$20
	STA	OAM_DATA,X	;CLEAR LOW X BYTE
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;SET THE MSB
	STA	OAM_DATA+512,Y
	PLY
	PLA
	CLC
	ADC	#4	;NEXT OAM OBJECT
	TAX
	INC	THIS_SPRITE	;NEXT SPRITE
	LDA	THIS_SPRITE
	LSR
	LSR
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION

	DEY
	BNE	@200
@300
;-----
	LDA	#1
	STA	OAM_DMA_FLAG	;NEED TO SEND DATA TO OAM MEMORY
;-----
	LDA	#1
	STA	VRAM_DMA_FLAG	;NEED TO SEND DATA TO VRAM
	SET_AXY16
;
	LDX	DMA_HEAP_PTR
	SET_A8
	LDA	#^DMA_HEAP
	PHA
	PLB		;SET BANK OF DMA_HEAP
;
	LDA	#1
	STA	DMA_HEAP,X	;SPRITE DMA LIST COMMAND
	INX
	SET_A16
	LDA	DMA_LIST	;GET OFFSET OF DMA_LIST
	CLC
	ADC	FRAME_ADDR	;ADD OFFSET OF FRAME DATA
	STA	DMA_HEAP,X	;SAVE OFFSET TO DMA HEAP
	INX
	INX
	LDA	#0
	ADC	FRAME_ADDR+2	;ADD BANK OF FRAME DATA
	STA	DMA_HEAP,X
	INX
	INX
	STZ	DMA_HEAP,X	;SET END
	STX	DMA_HEAP_PTR	;SAVE POINTER
;-----
;SETUP THE ANIMATIONS HIT TABLE ADDRESS
	LDY	DMA_LIST
	LDX	OBJ_DWORD	;GET LONG WORD PTR TO THIS OBJ
	LDA	#0	;CLEAR HIGH BYTE
	SET_A8
	LDA	[FRAME_ADDR],Y	;GET # OF DMA'S
	INY
	SET_A16
	ASL	A
	ASL	A
	ASL	A	;# x 8 = OFFSET
	STA	TEMP_X
	TYA
	CLC
	ADC	TEMP_X
	CLC
	ADC	FRAME_ADDR	;POINT TO COLLISION DATA
	STA	OBJ_HIT_TBL,X
	LDA	#0
	ADC	FRAME_ADDR+2
	STA	OBJ_HIT_TBL+2,X
;-----	
	PLP
	RTL

	MODEND

;------------------
;MOVE_FRAME -- DRAW ONE FRAME OF THE ANIMATION

MOVE_FRAME	MODULE

	PHP
;-----
	SET_A8
	LDA	#^OBJ_SIZE_TABLE
	PHA
	PLB
	SET_XY16
	LDY	#0
	LDA	[FRAME_ADDR],Y	;GET # OF SPRITES IN FRAME
	STA	SPRITE_COUNT
	INY		;POINT TO 1ST PALETTE DATA
;
	SET_A16
	LDA	#0
	SET_A8
	LDA	THIS_SPRITE	;GET STARTING SPRITE #
	ASL
	ASL
	TAX		;POINT INTO OAM DATA 4 BYTES / SPRITE
	LDA	THIS_SPRITE	;MAKE MSB POINTER
	LSR
	LSR
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE	;MAKE MSB BYTE POINTER
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION
;-----
@100	INY		;POINT TO X OFFSET
	LDA	FLIP_DATA
	BIT	#$40	;X FLIPPED ?
	BNE	@DF_XFLIP
;-----
;NORMAL SPRITE
	LDA	[FRAME_ADDR],Y	;GET X OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	CLC
	ADC	X_OFF
	JMP	@CONT2
;-----
	LONGA OFF
@DF_XFLIP	LDA	[FRAME_ADDR],Y	;GET X OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	STA	TEMP_X
	LDA	X_OFF	;SUB FROM X POSITION
	SEC
	SBC	TEMP_X
@CONT2	CMP	#-64
	BGE	@XOK1
	CMP	#256
	BLT	@XOK1
	LDA	#$120

@XOK1	SET_A8
	STA	OAM_DATA,X
	SET_A16
	CMP	#$0100
	BGE	@SET_XMSB

;-----
;CLEAR THE X MSB OF THIS SPRITE
	PHX
	PHY
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	EOR	#$FF
	AND	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
	JMP	@CONT
;-----
;SET THE X MSB OF THIS SPRITE
@SET_XMSB	PHX
	PHY
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_XY16
	PLY
	PLX
;-----
@CONT	INX		;POINT TO OAM Y
	INY		;POINT TO Y OFFSET IN FRAME DATA
;
	LDA	FLIP_DATA
	BIT	#$80	;Y FLIPPED ?
	BNE	@DF_YFLIP

	LDA	[FRAME_ADDR],Y	;GET Y OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	CLC
	ADC	Y_OFF
	JMP	@CONT3
;
@DF_YFLIP	LDA	[FRAME_ADDR],Y	;GET Y OFFSET OF SUB SPRITE
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	STA	TEMP_X
	LDA	Y_OFF	;SUB FROM Y POSITION
	SEC
	SBC	TEMP_X
@CONT3	SET_A8
	STA	OAM_DATA,X	;SET Y
	SET_A16
;-----
	CMP	#240	;PAST BOTTOM OF SCREEN
	BLT	@Y_OK
	PHX	     	;SO MOVE IT OFF THE SCREEN
	PHY
	DEX		;BACK TO X POSITION
	SET_A8
	LDA	#$10
	STA	OAM_DATA,X	;PUT AT $110
	SET_AXY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;REMOVE ANY MSB
	STA	OAM_DATA+512,Y	;RESET IT TO ZERO
	SET_AXY16
	PLY
	PLX
;-----
@Y_OK	INX
	INX		;SKIP TO FLIP DATA
;-----
;PUT IN CURRENT FLIP DATA
	SET_A8
	LDA	OAM_DATA,X
	AND	#$3F	;MASK OUT THE FLIP DATA
	ORA	FLIP_DATA
	STA	OAM_DATA,X
;	SET_A16
;-----
	INX    		;SKIP TO NEXT OAM X
;
	INY
	INY
	INY		;TO NEXT PAL DATA
	STY	DMA_LIST	;SAVE POINTER TO DMA LIST
;-----
;	SET_A8
	INC	THIS_SPRITE	;NEXT SPRITE
	LDA	THIS_SPRITE
	LSR
	LSR
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION
;
	DEC	SPRITE_COUNT	;DID ALL SPRITE FOR THIS OBJECT ?
	BEQ	@DF_DONE
	JMP	@100
@DF_DONE
;-----
	LDA	#1
	STA	OAM_DMA_FLAG	;NEED TO SEND DATA TO OAM MEMORY
;-----
;SETUP THE ANIMATIONS HIT TABLE ADDRESS
	SET_A16
	LDY	DMA_LIST
	LDX	OBJ_DWORD	;GET LONG WORD PTR TO THIS OBJ
	LDA	#0	;CLEAR HIGH BYTE
	SET_A8
	LDA	[FRAME_ADDR],Y	;GET # OF DMA'S
	INY
	SET_A16
	ASL	A
	ASL	A
	ASL	A	;# x 8 = OFFSET
	STA	TEMP_X
	TYA
	CLC
	ADC	TEMP_X
	CLC
	ADC	FRAME_ADDR	;POINT TO COLLISION DATA
	STA	OBJ_HIT_TBL,X
	LDA	#0
	ADC	FRAME_ADDR+2
	STA	OBJ_HIT_TBL+2,X
;-----	
	PLP
	RTL

	MODEND


;------------------
;CHAR_ANIM_HANDLER -- SEE IF AN ANIMATION NEEDS TO BE DONE

CHAR_ANIM_HANDLER	MODULE

	PHP
	SET_AXY16
;-----
	LDA	CA1_ADDRESS	;SEE IF ANY ANIM SET
	CMP	#0
	BEQ	CAH_OFF
	DEC	CA1_TIMER	;COUTDOWN TO NEXT FRAME
	BEQ	CAH_DO
CAH_OFF	PLP
	RTL
;-----
CAH_DO	LDA	CA1_RATE
	STA	CA1_TIMER	;RESET TIMER
;
	LDA	CA1_FRAME
	ASL	A
	ASL	A
	ASL	A	;FRAME # x 8 MAKES OFFSET
	TAY
;-----
	LDX	DMA_HEAP_PTR
	SET_A8
	LDA	#^DMA_HEAP
	PHA
	PLB		;SET BANK OF DMA_HEAP
;-----
	LDA	#2	;SINGLE DMA
	STA	DMA_HEAP,X	;DMA COMMAND
	INX
;-----
	LDA	#$80
	STA	DMA_HEAP,X
	INX
;-----
	SET_A16
	LDA	[CA1_ADDRESS],Y	;GET OFFSET OF SOURCE
	INY
	INY
	STA	DMA_HEAP,X
	INX
	INX
;-----
	LDA	[CA1_ADDRESS],Y	;GET BANK
	INY
	INY
	SET_A8
	STA	DMA_HEAP,X
	INX
;-----
	SET_A16
	LDA	[CA1_ADDRESS],Y	;GET VRAM DEST
	LSR	A	;ADDRESS IS 2x OF VRAM VALUE
	INY
	INY
	CLC
	ADC	CA1_CHAR_BASE	;ADD BASE ADDRESS OF CHAR DATA
	STA	DMA_HEAP,X
	INX
	INX
;-----
	LDA	[CA1_ADDRESS],Y	;GET # OF BYTES
	INY
	INY
	STA	DMA_HEAP,X
	INX
	INX
	STZ	DMA_HEAP,X	;END OF DMA_HEAP
	STX	DMA_HEAP_PTR	;SAVE PTR
;-----
	LDA	#1
	STA	VRAM_DMA_FLAG	;FORCE DMA
;-----
	INC	CA1_FRAME	;NEXT FRAME #
	LDA	CA1_FRAME
	CMP	CA1_FRAMES	;DID ALL ?
	BNE	CAH_EXIT
	STZ	CA1_FRAME	;LOOP THEN
;-----
CAH_EXIT	PLP
	RTL


	MODEND

;------------------
;CA_TEST -- TEST CHARACTER ANIMATION

CA_TEST	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#^AMAP_BGANIM0_START
	STA	CA1_ADDRESS+2
;
	SET_A8
	PHA
	PLB
	SET_A16
;
	LDA	#<AMAP_BGANIM0_START
	STA	CA1_ADDRESS
;
	STZ	CA1_FRAME
;
	LDA	CAST01A_FRAMES	;GET # OF FRAMES
	STA	CA1_FRAMES
;
	LDA	CAST01A_RATE	;GET FRAME RATE TIMER
	STA	CA1_RATE
	STA	CA1_TIMER	;INIT COUNTDOWN
;
	LDA	BG1_CHAR	;GET BASE ADDRESS OF CHARS IN VRAM
	STA	CA1_CHAR_BASE
;-----
	PLP
	RTL

	MODEND

;------------------
;------------------
;------------------
;------------------
