



;***************************************************
;
;	GOLIATH CONTOUR CHECKING ROUTINES
;
;***************************************************





;------------------
;WALK_CONTOUR -- RETURN CONTOUR VALUE
;
;1) LOOK IN CURRENT BLOCK
;	IF CONTOUR FOUND THEN RETURN VALUE
;2) IF NOT FOUND LOOK IN BLOCK ABOVE
;	IF CONTOUR FOUND THEN RETURN VALUE = CONTOUR - 16
;3) IF NOT THEN LOOK IN BLOCK BELOW
;	IF CONTOUR FOUND THEN RETURN VALUE = CONTOUR + 16
;4) IF NOT FOUND THEN RETURN ZERO


WALK_CONTOUR	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH	;HALF OF WIDTH
	SET_A16
	LSR	A
	CLC
	ADC	OBJ_X	; + X
	STA	CON_X	;= CONTOUR X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y
	CLC
	ADC	#GOL_HEIGTH	;OBJ_HEIGTH
	STA	CON_Y	;
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN CURRENT BLOCK
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_UP
;-----
@CON_OK	PLP
	RTL
;-----
;LOOK IN BLOCK ABOVE
@CON_UP	SET_A16
	LDA	MAP1_WIDTH
	ASL	A
	STA	TEMP_X
	LDA	MAP_OFF
	SEC
	SBC	TEMP_X	;LOOK 1 BLOCK UP
	TAY
;
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_DOWN
	SET_A16
	SEC
	SBC	#16	;ADJUST FOR BLOCK ABOVE
	BNE	@CON_OK
	LDA	#-1
	PLP
	RTL
;-----
@CON_DOWN	SET_A16
	LDA	MAP1_WIDTH
	ASL	A
	CLC
	ADC	MAP_OFF	;NEXT ROW DOWN
	TAY
;
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_OK
	SET_A16
	CLC
	ADC	#16	;ADJUST FOR BLOCK ABOVE
	PLP
	RTL
	
	MODEND

;------------------
;WALK_UP_CON -- RETURN CONTOUR VALUE

WALK_UP_CON	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH	;HALF OF WIDTH
	SET_A16
	LSR	A
	CLC
	ADC	OBJ_X	; + X
	STA	CON_X	;= CONTOUR X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y
	CLC
	ADC	#GOL_HEIGTH	;OBJ_HEIGTH
	STA	CON_Y	;
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN BLOCK ABOVE FIRST
	LDA	MAP1_WIDTH
	ASL	A
	STA	TEMP_X
	LDA	MAP_OFF
	SEC
	SBC	TEMP_X	;LOOK 1 BLOCK UP
	TAY
;
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_NORMAL
	SET_A16
	SEC
	SBC	#16	;ADJUST FOR BLOCK ABOVE
	BNE	@CON_OK
	LDA	#-1
	PLP
	RTL
;-----
;LOOK IN CURRENT BLOCK
@CON_NORMAL	SET_A16
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_DOWN
;-----
@CON_OK	PLP
	RTL
;-----
@CON_DOWN	SET_A16
	LDA	MAP1_WIDTH
	ASL	A
	CLC
	ADC	MAP_OFF	;NEXT ROW DOWN
	TAY
;
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_OK
	SET_A16
	CLC
	ADC	#16	;ADJUST FOR BLOCK ABOVE
	PLP
	RTL
	
	MODEND

;------------------
;WALK_DOWN_CON -- RETURN CONTOUR VALUE

WALK_DOWN_CON	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH	;HALF OF WIDTH
	SET_A16
	LSR	A
	CLC
	ADC	OBJ_X	; + X
	STA	CON_X	;= CONTOUR X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y
	CLC
	ADC	#GOL_HEIGTH	;OBJ_HEIGTH
	STA	CON_Y	;
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK AT BLOCK BELOW FIRST
	LDA	MAP1_WIDTH
	ASL	A
	CLC
	ADC	MAP_OFF	;NEXT ROW DOWN
	TAY
;
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_NORMAL	;LOOK IN MIDDLE
	SET_A16
	CLC
	ADC	#16	;ADJUST FOR BLOCK ABOVE
	PLP
	RTL
;-----
;LOOK IN CURRENT BLOCK
@CON_NORMAL	SET_A16
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_UP
;-----
@CON_OK	PLP
	RTL
;-----
;LOOK IN BLOCK ABOVE LAST
@CON_UP	SET_A16
	LDA	MAP1_WIDTH
	ASL	A
	STA	TEMP_X
	LDA	MAP_OFF
	SEC
	SBC	TEMP_X	;LOOK 1 BLOCK UP
	TAY
;
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
	CMP	#0
	BEQ	@CON_OK
	SET_A16
	SEC
	SBC	#16	;ADJUST FOR BLOCK ABOVE
	BNE	@CON_OK
	LDA	#-1
	PLP
	RTL
	
	MODEND

;------------------
;FALL_CONTOUR -- RETURN CONTOUR VALUE
;
;1) LOOK IN CURRENT BLOCK
;	IF CONTOUR FOUND THEN RETURN VALUE
;2) IF NOT FOUND LOOK IN BLOCK ABOVE
;	IF CONTOUR FOUND THEN RETURN VALUE = CONTOUR - 16
;3) IF NOT THEN LOOK IN BLOCK BELOW
;	IF CONTOUR FOUND THEN RETURN VALUE = CONTOUR + 16
;4) IF NOT FOUND THEN RETURN ZERO


FALL_CONTOUR	MODULE

	PHP
	SET_A8
	LDA	GOL_CON_FLAG	;SHOULD I LOOK FOR A CONTOUR ?
	BEQ	@GET_CON
	SET_A16
	LDA	#0
	PLP
	RTL
;-----
@GET_CON	SET_AXY16
	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH	;HALF OF WIDTH
	SET_A16
	LSR	A
	CLC
	ADC	OBJ_X	; + X
	STA	CON_X	;= CONTOUR X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y
	CLC
	ADC	#GOL_HEIGTH-1	;OBJ_HEIGTH
	STA	CON_Y	;
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN CURRENT BLOCK
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	LDA	[FLR_IN_RAM],Y	;GET FLOOR DATA
	STA	TEMP_X
	LDA	CON_X
	AND	#$000F
	CLC
	ADC	TEMP_X
	TAY
	LDA	#0	;CLEAR HIGH WORD
	SET_A8
	LDA	[CON_IN_ROM],Y	;GET CONTOUR DATA
;-----
	PLP
	RTL

	MODEND

;------------------





;***************************************************
;
;	GOLIATH'S SPECIAL TILES SEARCH ROUTINES
;
;***************************************************





;------------------
;CK_SPECIAL -- GET # OF ANY SPECIAL TILES

CK_SPECIAL	MODULE

	PHP
	SET_A8
	LDA	OBJ_FLIP
	BIT	#$40
	BEQ	CK_SP_RT
	JMP	CK_SP_LFT

	MODEND

;------------------
;CK_SPECIAL_RIGHT -- RETURN SPECIAL TILE VALUE

CK_SPECIAL_RIGHT	MODULE

	PHP
CK_SP_RT	SET_AXY16
;-----
	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH	;WIDTH
	SET_A16
	CLC
	ADC	OBJ_X	; + X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	#GOL_HEIGTH
	LSR	A	;HALF OF HEIGTH
	CLC
	ADC	OBJ_Y	;OBJ Y
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN BLOCK AHEAD
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY	 	;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	TAX
;-----
	LDY	MAP_OFF
	INY		;ONE TILE TO THE RIGHT
	INY
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY	 	;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
;-----
	TAY
	TXA
;-----
	PLP
	RTL

	MODEND

;------------------
;CK_SPECIAL_LEFT -- RETURN SPECIAL TILE VALUE

CK_SPECIAL_LEFT	MODULE

	PHP
CK_SP_LFT	SET_AXY16
;-----
	LDA	OBJ_X
	CLC
	ADC	#10
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	#GOL_HEIGTH
	LSR	A	;HALF OF HEIGTH
	CLC
	ADC	OBJ_Y	;OBJ_HEIGTH
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN BLOCK TO LEFT
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
;-----
	PLP
	RTL

	MODEND

;------------------
;CK_SPECIAL_FEET -- RETURN SPECIAL TILE VALUE AT FEET LOCATION

CK_SPECIAL_FEET	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH	;HALF OF WIDTH
	SET_A16
	LSR	A
	CLC
	ADC	OBJ_X	; + X
	STA	CON_X	;= CONTOUR X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y
	CLC
	ADC	#GOL_HEIGTH	;OBJ_HEIGTH
	STA	CON_Y	;
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN BLOCK AT FEET
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY	 	;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
;-----
	PLP
	RTL

	MODEND

;------------------
;CK_SPECIAL_HEAD -- RETURN SPECIAL TILE VALUE

CK_SPECIAL_HEAD	MODULE

	PHP
	SET_AXY16
;-----
	SET_A8
	LDA	OBJ_FLIP
	CMP	#0
	SET_A16
	BNE	@HEAD_LEFT
;-----
	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH	;HALF OF WIDTH
	SET_A16
	JMP	@HEAD_CONT
;-----
@HEAD_LEFT	LDA	#10
@HEAD_CONT	CLC
	ADC	OBJ_X	; + X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y	;OBJ_HEIGTH

	CLC
	ADC	#10	;HEAD BUFFER

	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN BLOCK AT TOP
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
;-----
	PLP
	RTL

	MODEND
;------------------
;CK_SPECIAL_CRAWL -- RETURN SPECIAL TILE VALUE

CK_SPECIAL_CRAWL	MODULE

	PHP
	SET_AXY16
;-----
	SET_A8
	LDA	OBJ_FLIP
	CMP	#0
	SET_A16
	BNE	@HEAD_LEFT
;-----
	LDA	#0
	SET_A8
	LDA	#64-20
	SET_A16
	JMP	@HEAD_CONT
;-----
@HEAD_LEFT	LDA	#10
@HEAD_CONT	CLC
	ADC	OBJ_X	; + X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y	;OBJ_HEIGTH

;	CLC
;	ADC	#10	;HEAD BUFFER

	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN BLOCK AT TOP
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
;-----
	PLP
	RTL

	MODEND

;------------------
;READ_SPECIAL -- READ A 4 x 4 TILE GRID OF SPECIAL TILES
;
;
;	00,01,02,03
;	10,11,12,13
;	20,21,22,23
;	30,31,32,33
;

READ_SPECIAL	MODULE

	PHP
	SET_AXY16
;-----
	LDA	OBJ_X	;GOLIATH X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y	;GOLIATH Y
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOAD ALL THE TILES
	LDX	#0

@READ_LOOP	LDY	MAP_OFF
;-----
;COLUMN 0
	PHY
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	STA	SPECIAL_00,X
;-----
;COLUMN 1
	PLY
	INY
	INY
	PHY
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	STA	SPECIAL_01,X
 ;-----
 ;COLUMN 2
	PLY
	INY
	INY
	PHY
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	STA	SPECIAL_02,X
;-----
;COLUMN 3
	PLY
	INY
	INY
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	STA	SPECIAL_03,X
;-----
	TXA
	CLC
	ADC	#8	;NEXT ROW
	TAX
	CPX	#4*8	;DID ALL ?
	BEQ	@READ_DONE
	LDA	MAP_OFF
	CLC
	ADC	MAP1_WIDTH	;POINT TO NEXT ROW
	ADC	MAP1_WIDTH	;WORD VALUES
	STA	MAP_OFF
	JMP	@READ_LOOP
;-----
@READ_DONE	PLP
	RTL

	MODEND

;------------------
;CK_ST_CLIMB -- GET # OF ANY SPECIAL TILES

CK_ST_CLIMB	MODULE

	PHP
	SET_A8
	LDA	OBJ_FLIP
	BIT	#$40
	SET_A16
	BEQ	@CK_RIGHT
	JMP	@CK_LEFT
;-----
@CK_RIGHT	LDA	#0
	SET_A8
	LDA	#GOL_WIDTH-16	;WIDTH - 1 TILE
	SET_A16
	CLC
	ADC	OBJ_X	; + X
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y	;OBJ Y
	CLC
	ADC	#10
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN TOP RIGHT - 1 BLOCK
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY	 	;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	STA	SPECIAL_01
;-----
;LOOK IN TOP RIGHT - MIDDLE BLOCK
	LDY	MAP_OFF
	INY
	INY
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
 	INY
	INY	 	;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	STA	SPECIAL_00
;-----
	PLP
	RTL
;-----
@CK_LEFT	LDA	OBJ_X
	CLC
	ADC	#10
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	TAY
	LDX	MAP1_WIDTH	;MAP WIDTH
	LDA	OBJ_Y	;OBJ_HEIGTH
	CLC
	ADC	#10
	LSR	A
	LSR	A
	LSR	A
	LSR	A	; / 16
	JSL	CALC_OFF	;CALC THE OFFSET IN MAP
;-----
;LOOK IN BLOCK TOP LEFT
	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
	INY
	INY		;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	STA	SPECIAL_00
	SET_A16
;-----
;NEXT BLOCK TO RIGHT
	LDY	MAP_OFF
	INY
	INY
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	TAY
 	INY
	INY	 	;SKIP CONTOUR OFFSET
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	SET_A16
	STA	SPECIAL_01
;-----
	LDA	SPECIAL_00
;-----
	PLP
	RTL

	MODEND

;------------------
;------------------
;------------------
;------------------
