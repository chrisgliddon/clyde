;------------------
;
;GG_GOL.658
;
;  GOLIATH CONTROL ROUTINES
;
;
;------------------
;EQUATES


;-----
;SPECIAL TILE DEFINITIONS

ST_WALL_TEST	EQU	$80
ST_CLIMB_TEST	EQU	$93

ST_NULL	EQU	$00

ST_CLIMB0	EQU	$80	;CLIMBING AREA
ST_CLIMB1	EQU	$81	;CLIMB FACING LEFT
ST_CLIMB2	EQU	$82	;CLIMB FACING RIGHT
ST_CLIMB3	EQU	$83	;CLIMB ROPE

ST_TRANS0	EQU	$A0	;CLIMBING A COULMN FROM EITHER DIRECTION
ST_TRANS2	EQU	$A2	;CEILING CRAWL RIGHT GO AROUND LIP OF AN EDGE
ST_TRANS3	EQU	$A3	;CEILING CRAWL LEFT GO AROUND LIP OF AN EDGE
ST_TRANS4	EQU	$A4	;CLIMBING FACING LEFT CLIMB OVER LIP OF EDGE
ST_TRANS5	EQU	$A5	;CLIMBING FACING RIGHT CLIMB OVER LIP OF EDGE

ST_WALL	EQU	$8F	;SOLID WALL NO PENETRATION
ST_CEILING	EQU	$90	;SOLID CEILING NO PENETRATION

ST_CATAPULT0	EQU	$14	;CATAPULT TRIGGER AREAS
ST_CATAPULT1	EQU	$15
ST_CATAPULT2	EQU	$16
ST_CATAPULT3	EQU	$17

ST_BOULDER0	EQU	$28	;FLAMEING BOULDER TRIGGER AREAS
ST_BOULDER1	EQU	$29
ST_BOULDER2	EQU	$2A
ST_BOULDER3	EQU	$2B
ST_BOULDER4	EQU	$2C
ST_BOULDER5	EQU	$2D
ST_BOULDER6	EQU	$2E
ST_BOULDER7	EQU	$2F
ST_BOULDER8	EQU	$30

ST_DOOR0	EQU	$3C	;BREAKABLE DOORS
ST_DOOR1	EQU	$3D
ST_DOOR2	EQU	$3E

ST_COL_FLR0	EQU	$50	;COLLAPSING FLOOR AREAS
ST_COL_FLR1	EQU	$51
ST_COL_FLR2	EQU	$52
ST_COL_FLR3	EQU	$53
ST_COL_FLR4	EQU	$54
ST_COL_FLR5	EQU	$55
ST_COL_FLR6	EQU	$56
ST_COL_FLR7	EQU	$57
ST_COL_FLR8	EQU	$58
ST_COL_FLR9	EQU	$59
ST_COL_FLR10	EQU	$5A



;------------------
;ANIM LIST -- HERE ARE THE ADDRESS OF ALL THE ANIMATION USED IN THE GAME.
;THIS LIST CAN BE COPIED TO RAM IF ANY OF THE ANIM ARE TO BE STORED THERE.

WALK	EQU	0
JUMP	EQU	1
FALL	EQU	2
PUNCH	EQU	3
IDLE	EQU	4
FLY	EQU	5
TRAJ	EQU	6
CLIMB	EQU	7
R2S	EQU	8
SPRINT	EQU	9
JFLP	EQU	10
GLIDE	EQU	11
CEILING_IDLE	EQU	12
CEILING_CRAWL	EQU	13
CLIMB_IDLE	EQU	14


GOL_ANIMS	ANIM	GOL_CTRL_WALK,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_RUN_ANM,&	;ADDRESS OF ANIMATION DATA
		3,3	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_JUMP,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_JMP_ANM,&	;ADDRESS OF ANIMATION DATA
		4,4	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_FALL,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_FALL_ANM,&	;ADDRESS OF ANIMATION DATA
		2,4	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_PUNCH,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_PUN_ANM,&	;ADDRESS OF ANIMATION DATA
		2,2	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_IDLE,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_IDLE_ANM,&	;ADDRESS OF ANIMATION DATA
		2,2	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_FLY,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_RUN_ANM,&	;ADDRESS OF ANIMATION DATA
		2,2	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_TRAJ,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_IDLE_ANM,&	;ADDRESS OF ANIMATION DATA
		8,8	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_CLIMB,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_CLIM_ANM,&	;ADDRESS OF ANIMATION DATA
		2,2	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_R2S,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_R2S_ANM,&	;ADDRESS OF ANIMATION DATA
		3,3	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_SPRT,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_SPRT_ANM,&	;ADDRESS OF ANIMATION DATA
		3,3	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_JFLP,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_JFLP_ANM,&	;ADDRESS OF ANIMATION DATA
		3,3	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_GLIDE,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_IDLE_ANM,&	;ADDRESS OF ANIMATION DATA
		2,2	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_CIDLE,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_CIDL_ANM,&	;ADDRESS OF ANIMATION DATA
		3,3	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_CRAWL,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_CCRW_ANM,&	;ADDRESS OF ANIMATION DATA
		3,3	;X,Y SCROLL RATE
;
	ANIM	GOL_CTRL_CLIDL,&	;ADDRESS OF CONTROL ROUTINE
		0,&	;ADDRESS OF HIT ROUTINE
		GOL_IDLE_ANM,&	;ADDRESS OF ANIMATION DATA
		2,2	;X,Y SCROLL RATE
;

;------------------
;TRAJECTORY TABLES

TRAJECTORIES	DL	TRJ1A1	;CATAPULT 1 ON LEVEL 1A
	DL	TRJ1A2
	DL	TRJ1A3
	DL	TRJ1A4


TRJ1A1	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
;
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
;
	DW	-16,8
	DW	-16,8
	DW	-16,8
	DW	-16,8
;
	DW	0,0

TRJ1A2	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-16
;
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
;
	DW	16,8
	DW	16,8
	DW	16,8
	DW	16,8
;
	DW	0,0

TRJ1A3	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
	DW	-16,-16
;
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
	DW	-16,0
;
	DW	-16,8
	DW	-16,8
	DW	-16,8
	DW	-16,8
;
	DW	0,0

TRJ1A4	DW	16,-16
	DW	16,-16
	DW	16,-16
	DW	16,-8
	DW	16,-8
	DW	16,-8
	DW	16,-8
	DW	16,-8
	DW	16,-8
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
	DW	16,-4
;
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0

	DW	16,2
	DW	16,2
	DW	16,2
	DW	16,2
	DW	16,2
	DW	16,2
	DW	16,2
	DW	16,2
	DW	16,2
	DW	16,2

	DW	16,3
	DW	16,3
	DW	16,3
	DW	16,3
	DW	16,3
	DW	16,3
	DW	16,3
	DW	16,3

	DW	16,4
	DW	16,4
	DW	16,4
	DW	16,4
	DW	16,4

	DW	16,5
	DW	16,5
	DW	16,6
	DW	16,5
	DW	16,5
	DW	16,6

	DW	16,8
	DW	16,8

	DW	16,0
	DW	16,0
	DW	16,0
	DW	16,0

	DW	0,0
;-----
;CATAPULT DIRECTIONS

LVL1A_CAT_DIR	DB	$40,$00,$40,$00		;OBJ FLIP DIR 

;-----

GOL_HEIGTH	EQU	70
GOL_WIDTH	EQU	44

;------------------
;INIT_GOLIATH -- START GOLIATH UP
;SET:
;	X,Y
;	CONTROL ROUTINE
;	HIT TABLE CLEARED
;	ANIM #

INIT_GOLIATH	MODULE


	PHP
;-----
;SET ANY BYTE FLAGS HERE
	SET_A8
	STZ	GOL_CON_FLAG	;DO LOOK FOR CONTOURS
	LDA	#0
	STA	OBJ_SPRITE	;STARING SPRITE #

;-----
;SET THE STARTING X,Y
	SET_AXY16
	LDA	MAP1_X	;USE CURRENT MAP POSITION
	CLC
	ADC	#$60
	STA	OBJ_X	;X
	LDA	MAP1_Y
	CLC
	ADC	#$50
	STA	OBJ_Y	;Y
;-----
	STZ	FLAP_FLAG	;ALLOW A FLAP
;-----
	LDA	#FALL	;HIS DEFAULT START IS FALLING
	JMP	SET_GOL_ANIM

	MODEND

;------------------
;INIT_GOL_FLY -- START GOLIATH UP
;SET:
;	X,Y
;	CONTROL ROUTINE
;	HIT TABLE CLEARED
;	ANIM #

INIT_GOL_FLY	MODULE


	PHP
	SET_AXY16
;-----
;SET THE STARTING X,Y
	LDA	MAP1_X	;USE CURRENT MAP POSITION
	CLC
	ADC	#$70
	STA	OBJ_X	;X
	LDA	MAP1_Y
	CLC
	ADC	#$70
	STA	OBJ_Y	;Y
;-----
	LDA	#FLY
	JMP	SET_GOL_ANIM

	MODEND

;------------------
;SET_GOL_TRAJ -- SET GOLIATH INTO A TRAJECTORY
;
;	ACC = TRAJECTORY #

SET_GOL_TRAJ	MODULE

	PHP
	SET_AXY16
;-----
;ENTER HERE IF JUMPING IN

JMP_GOL_TRAJ	ASL	A
	ASL	A
	TAY
	LDA	TRAJECTORIES,Y	;GET OFFSET OF TABLE
	STA	GOL_TRAJ
	INY
	INY
	LDA	TRAJECTORIES,Y	;GET BANK
	STA	GOL_TRAJ+2
;-----
	LDA	#TRAJ
	JMP	RUN_GOL_ANIM	;GO DRAW IT

	MODEND

;------------------
;SET_GOL_ANIM -- SET A NEW ANIMATION FOR GOLIATH AND RUN IT'S CONTROL ROUTINE
;
;	JMP IN HERE WITH ANIM # IN ACC

SET_GOL_ANIM	MODULE

	JSL	NEW_GOL_ANIM  	;SETUP NEW ANIM
;-----
	PLP
	JML	(OBJ_CONTROL)	;RUN IT'S CONTROL ROUTINE

	MODEND

;------------------
;RUN_GOL_ANIM -- SET A NEW ANIMATION FOR GOLIATH AND DRAW 1ST FRAME
;
;	JMP IN HERE WITH ANIM # IN ACC

RUN_GOL_ANIM	MODULE

	JSL	NEW_GOL_ANIM 	;SETUP NEW ANIM
;-----
;EXECUTE THE FIRST COMMAND TO DRAW GOLIATH UP ON SCREEN
	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
	PLP
	RTL

	MODEND

;------------------
;NEW_GOL_ANIM -- SETUP THE DATA FOR A NEW ANIMATION FOR GOLIATH
;
;	ACC = ANIMATION #

NEW_GOL_ANIM	MODULE


	SET_AXY16
	PHA
;-----
	LDX	#0	;OBJECT #
	JSL	CLR_OBJ_SPRITES	;REMOVE ALL SUB SPRITES FROM SCREEN
;-----
	PLA	         	;ANIMATION #
	AND	#$00FF
	ASL	A
	ASL	A
	ASL	A
	ASL	A	;# x 16
	TAX		;OFFSET OF ANIMATION DATA IN GOL_ANIMS TABLE
;-----
;SET THE CONTROL ROUTINE
	LDA	GOL_ANIMS,X	;CONTROL ROUTINE
	STA	OBJ_CONTROL
	LDA	GOL_ANIMS+2,X
	STA	OBJ_CONTROL+2
;-----
;SET THE ADDRESS OF THE HIT CONTROL ROUTINE
	LDA	GOL_ANIMS+4,X
	STA	OBJ_HIT  	;SET HIT ROUTINE ADDRESS
	LDA	GOL_ANIMS+6,X
	STZ	OBJ_HIT+2
;-----
;SET THE ADDRESS OF THE ANIM TO PLAY
	LDA	GOL_ANIMS+8,X	;ADDRESS OF ANIM
	STA	OBJ_ANIM
	LDA	GOL_ANIMS+10,X
	STA	OBJ_ANIM+2
;-----
;*** NO LONGER USED ***
;SET THE X,Y SCROLL RATE
;	LDA	GOL_ANIMS+12,X
;	STA	XSCROLL_RATE	;SET NEW SCROLL RATE
;	STA	MAX_XRATE	;SET NEW SCROLL RATE
;	LDA	GOL_ANIMS+14,X
;	STA	YSCROLL_RATE
;	STA	MAX_YRATE	;SET NEW SCROLL RATE
;-----
;SET THE OBJ_FLAG
	STZ	OBJ_FLAG	;CLEAR IT FOR NOW
;-----
;SET THE POINTERS USED BY OTHER ROUTINES
	STZ	OBJ_DWORD	;DOUBLE WORD PTR
	STZ	OBJ_WORD	;WORD PTR
;-----
	LDA	OBJ_ANIM	;SETUP THE ANIM ADDRESS
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;FOR A CALL
	STA	THIS_OBJ_ANIM+2
;-----
;GET DATA FROM THE ANIMATION HEADER
	SET_A8
	LDY	#0
;
	LDA	[THIS_OBJ_ANIM],Y	;GET SPRITE SIZE
	STA	OBJ_SPR_SIZE
	INY
;
	LDA	[THIS_OBJ_ANIM],Y	;GET MAX SPRITES
	STA	OBJ_MAX_SPR
	INY
;
	LDA	[THIS_OBJ_ANIM],Y	;GET WIDTH
	STA	OBJ_WIDTH
	INY
;
	LDA	[THIS_OBJ_ANIM],Y	;GET HEIGTH
	STA	OBJ_HEIGTH
	INY
	INY		;SKIP COMMAND ?????
;-----
;NOW SET THE ANIMATIONS ADDRESS PAST THE HEADER
	SET_A16
	LDA	OBJ_ANIM
	CLC
	ADC	#4	;SKIP DATA AT HEAD OF ANIM
	STA	OBJ_ANIM
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2
	ADC	#0
	STA	OBJ_ANIM+2
	STA	THIS_OBJ_ANIM+2	;SET THE ADDRESS OF THE ANIM LIST
;-----
	RTL

	MODEND

;------------------
;IDLE_OR_WALK	-- DECIDE IF GOLIATH SHOULD BE WALKING OR IDLE

IDLE_OR_WALK	MODULE

	SET_A16
	STZ	FLAP_FLAG	;ALLOW A FLAP
	LDA	JOY1_DATA
	BIT	#JOY_RIGHT	;PRESSING TO THE RIGHT ?
	BNE	SHOULD_WALK	;YES WALK RIGHT
;
	BIT	#JOY_LEFT	;PRESSING TO THE LEFT ?
	BNE	SHOULD_WALK	;YES WALK LEFT
;
	LDA	#IDLE	;NO DIRECTION
	JMP	RUN_GOL_ANIM	;SO DO IDLE
;-----
SHOULD_WALK	LDA	OBJ_XSPEED	;SEE HOW FAST TO GO
	CMP	#5
	BLT	@JUST_WALK
	LDA	#SPRINT	;DO SPRINT NOW
	JMP	SET_GOL_ANIM
;-----
@JUST_WALK	LDA	#WALK	;START WALKING
	JMP	SET_GOL_ANIM

	MODEND

;------------------





;***************************************************
;
;	GOLIATH'S CONTROL ROUTINES
;
;***************************************************





;------------------
;
;	III  DDDD   L      EEEEE
;	 I   D	 D  L      E
;	 I   D	 D  L      EEEE
;	 I   D	 D  L      E
;	III  DDDD   LLLLL  EEEEE
;
;GOL_CTRL_IDLE -- GOLIATH IDLE
;...IDLE

GOL_CTRL_IDLE	MODULE

	PHP
	SET_AXY16
;-----
	STZ	OBJ_XSPEED	;SET GOLIATH SPEED
	STZ	OBJ_YSPEED
	STZ	FLAP_FLAG	;ALLOW A FLAP
;-----
	JSL	EXPLORE
;-----
	LDA	JOY1_DATA
	BIT	#JOY_UP
	BEQ	@NOT_CLIMB
	JSL	CK_SPECIAL	;GET SPECIAL TILE
	CMP	#ST_CLIMB0	;CLIMABLE FROM BOTH SIDES ?
	BEQ	@DO_CLIMB
	CMP	#ST_CLIMB1	;CLIMABLE WHILE FACING LEFT ?
	BEQ	@LEFT_CLIMB
	CMP	#ST_CLIMB2	;CLIMABLE WHILE FACING RIGHT ?
	BNE	@NOT_CLIMB	;NOT A CLIMBING TILE
	SET_A8
	LDA	OBJ_FLIP	;GET DIRECTION OF GOLIATH
	CMP	#0	;FACING RIGHT ?
	SET_A16
	BEQ	@DO_CLIMB	;GO CLIMB WALL
	JMP	@NOT_CLIMB
;-----
@LEFT_CLIMB	SET_A8
	LDA	OBJ_FLIP	;GET DIRECTION OF GOLIATH
	CMP	#$40	;FACING LEFT ?
	SET_A16
	BNE	@NOT_CLIMB
;-----
@DO_CLIMB	LDA	#CLIMB_IDLE	;START CLIMB ANIM
	JMP	RUN_GOL_ANIM
@NOT_CLIMB
;-----
;SEE IF GOLIATH SHOULD JUMP
	LDA	DB1_DATA
	BIT	#JOY_BBUTT	;JUMP BUTTON PRESSED ?
	BNE	@DO_JUMP	;YES DO A VERTICAL JUMP
;
	BIT	#JOY_YBUTT	;DO A PUNCH ?
	BEQ	@NO_JUMP	;NO NOT A PUNCH
;-----
;START A PUNCH
	LDA	#PUNCH	;SET PUCNH ANIM
	JMP	SET_GOL_ANIM
;-----
@DO_JUMP	LDA	#3
;	STA	OBJ_XSPEED	;SET JUMP X SPEED
	LDA	#JUMP	;SET JUMP ANIMATION
	JMP	SET_GOL_ANIM
;-----
;WHAT DIR TO WALK ?
@NO_JUMP	LDA	JOY1_DATA	;GET JOYSTICK DATA
	BIT	#JOY_RIGHT	;MOVE RIGHT ?
	BEQ	@NOT_RIGHT	;NOT RIGHT
;-----
@WALK_RIGHT	SET_A8
	LDA	OBJ_FLIP	;ALREADY POINTED THERE ?
	BEQ	@DO_WALK_RIGHT
	LDA	#0
	STA	OBJ_FLIP	;TURN AROUND
	JMP	@IDLE_DONE
;-----
@DO_WALK_RIGHT
	SET_A16
	LDA	#WALK	;DO WALK ANIM
	JMP	SET_GOL_ANIM
;-----
@NOT_RIGHT	BIT	#JOY_LEFT	;MOVE LEFT ?
	BEQ	@IDLE_DONE	;NOT LEFT
	SET_A8
	LDA	OBJ_FLIP
	BNE	@DO_WALK_LEFT
	LDA	#$40	;HORZ FLIP
	STA	OBJ_FLIP
	JMP	@IDLE_DONE
@DO_WALK_LEFT
	SET_A16
	LDA	#WALK	;DO WALK ANIM
	JMP	SET_GOL_ANIM
;-----
@IDLE_DONE	SET_A16
	LDA	OBJ_ANIM	;SET ANIM 
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2
;-----
	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
	PLP
	RTL

        	MODEND

;------------------
;GOL_CTRL_WALK -- GOLIATH WALKING

GOL_CTRL_WALK	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#3	;SET GOLIATHS SPEED
	STA	OBJ_XSPEED
;-----
;SEE IF TIME TO ACCELERATE TO SPRINT
	LDA	OBJ_FLAG
	CMP	#1	;DID ONE ANIM CYCLE ?
	BNE	R2S_CONT
	LDA	#R2S	;DO RUN TO SPRINT TRANSITION ANIMATION
	JMP	SET_GOL_ANIM
;-----
;SEE IF GOLIATH SHOULD JUMP
R2S_CONT	LDA	DB1_DATA
	BIT	#JOY_BBUTT	;JUMP BUTTON PRESSED ?
	BEQ	@NO_JUMP	;NO NOT A JUMP
	LDA	#JUMP
	JMP	SET_GOL_ANIM	;SET THE JUMP ANIM
;-----
;WHAT DIR TO WALK ?
@NO_JUMP	LDA	JOY1_DATA	;GET JOYSTICK DATA
	BIT	#JOY_RIGHT	;MOVE RIGHT ?
	BNE	@WALK_RIGHT	;YES WALKING RIGHT!
	JMP	@NOT_RIGHT
;-----
@WALK_RIGHT	SET_A8
	LDA	#0
	STA	OBJ_FLIP	;NO FLIP
	SET_A16
;-----
;SEE IF IT HIT ANYTHING
	JSL	CK_SPECIAL_RIGHT	;ANYTHING TO THE RIGHT
	BIT	#ST_WALL_TEST	;IS THIS SOME TYPE OF WALL ?
	BNE	@DO_IDLE	;YES A WALL
;
	CMP	#ST_DOOR0	;HIT DOOR ?
	BLT	@WALK_CONT	;NO
;
	CMP	#ST_DOOR2+1	;HIT DOOR ?
	BGE	@WALK_CONT
;
	SEC		;USING THE SPECIAL TILE #
	SBC	#ST_DOOR0	;MAKE IT A POINTER
	ASL	A
	TAX
	LDA	DOOR1_FLAG,X	;DOOR OPEN ?
	CMP	#4
	BNE	@DO_IDLE	;NO DOOR CLOSED
;-----
@WALK_CONT	LDA	OBJ_ANIM	;SET ANIM
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2
;-----
	LDA	JOY1_DATA	;GET JOYSTICK INPUT
	BIT	#JOY_UP	;GOING UP ?
	BNE	@CK_UP	;YES
;
	BIT	#JOY_DOWN	;GOING DOWN ?
	BEQ	@CK_NORMAL	;NO
;
	JSL	WALK_DOWN_CON	;LOOK BELOW FOR A CONTOUR
	JMP	@CK_DONE
;-----
@DO_IDLE
	LDA	#IDLE	;SET IDLE ANIMATION
	JMP	RUN_GOL_ANIM
;
@CK_UP	JSL	WALK_UP_CON	;LOOK ABOVE FOR CONTOUR
	JMP	@CK_DONE
;
@CK_NORMAL	JSL	WALK_CONTOUR	;LOOK FOR A CONTOUR
;
@CK_DONE	CMP	#0	;NO CONTOUR ?
	BEQ	@GO_FALL	;THEN GO FALL
;
	PHA
	SEC
	SBC	OBJ_CON	;SUB LAST CONTOUR
	CLC
	ADC	OBJ_Y
	STA	OBJ_Y
	PLA
	BIT	#$8000	;NEG CONTOUR ?
	BEQ	@CON_OK
	CLC
	ADC	#16	;MAKE POS VALUE
	JMP	@CON_OK2
@CON_OK	CMP	#16+1
	BLT	@CON_OK2
	SEC
	SBC	#16
@CON_OK2	STA	OBJ_CON
;-----
	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
	LDA	OBJ_CON	;LOOK AT CONTOUR UNDER FEET
	CMP	#0	;IS IT = 0 ?
	BNE	@WALK_DONE
@GO_FALL	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@WALK_DONE	PLP
	RTL
;-----
@NOT_RIGHT	BIT	#JOY_LEFT	;MOVE LEFT ?
;	BEQ	@WALK_DONE
	BEQ	@DO_IDLE
	SET_A8
	LDA	#$40	;HORZ FLIP
	STA	OBJ_FLIP
	SET_A16
;-----
;SEE IF IT HIT ANYTHING
	JSL	CK_SPECIAL_LEFT	;ANYTHING TO THE LEFT
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
	BNE	@DO_IDLE	;YES A WALL
;
	CMP	#ST_DOOR0	;HIT DOOR ?
	BLT	@WALK_CONT2	;NO
;
	CMP	#ST_DOOR2+1	;HIT DOOR ?
	BGE	@WALK_CONT2
;
	SEC
	SBC	#ST_DOOR0	;MAKE IT A POINTER
	ASL	A
	TAX
	LDA	DOOR1_FLAG,X	;DOOR OPEN ?
	CMP	#4
	BNE	@DO_IDLE
@WALK_CONT2	JMP	@WALK_CONT

        	MODEND

;------------------
;GOL_CTRL_FLY -- GOLIATH FLYING

GOL_CTRL_FLY	MODULE

	PHP
	SET_AXY16
;-----
	LDA	NMI_CTR	;GET CURRENT TIME
	CLC
	ADC	#4
	STA	OBJ_TIME	;BE SURE TO RETURN IF NO MOVEMENT
;-----
;WHAT DIR TO FLY ?
	LDA	JOY1_DATA	;GET JOYSTICK DATA
	BIT	#JOY_RIGHT	;MOVE RIGHT ?
	BNE	@FLY_LEFT
	JMP	@NOT_RIGHT
;-----
@FLY_LEFT	SET_A8
	LDA	#0
	STA	OBJ_FLIP	;NO FLIP
	SET_A16
;-----
@FLY_CONT	LDA	OBJ_ANIM
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2
	STA	THIS_OBJ_ANIM+2
;-----
	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
@FLY_DONE	PLP
	RTL
;
@NOT_RIGHT	BIT	#JOY_LEFT	;MOVE LEFT ?
	BEQ	@FLY_UP_DOWN
	SET_A8
	LDA	#$40	;HORZ FLIP
	STA	OBJ_FLIP
	SET_A16
	JMP	@FLY_CONT
;-----
@FLY_UP_DOWN	BIT	#JOY_UP	;FLY UP
	BEQ	@FLY_DOWN
	LDA	OBJ_Y
	SEC
	SBC	#6
	STA	OBJ_Y
	JMP	@FLY_CONT
;-----
@FLY_DOWN	BIT	#JOY_DOWN	;FLY DOWN
	BEQ	@FLY_DONE
	LDA	OBJ_Y
	CLC
	ADC	#6
	STA	OBJ_Y
	JMP	@FLY_CONT

        	MODEND

;------------------
;
;	FFFFF   AAA   L      L
;	F      A   A  L      L
;	FFFF   AAAAA  L      L
;	F      A   A  L      L
;	F      A   A  LLLLL  LLLLL
;
;GOL_CTRL_FALL -- GOLIATH FALLING NOW
;>>>FALL

GOL_CTRL_FALL	MODULE
	
	PHP
	SET_AXY16
;-----
	JSL	EXPLORE
;-----
	JSL	FALL_CONTOUR	;GET CONTOUR #
	CMP	#0	;NO COUNTOUR ?
	BNE	@IS_BOTTOM	;HIT A CONTOUR!
	JMP	@NOT_BOTTOM	;KEEP FALLING
;-----
@IS_BOTTOM	STA	OBJ_CON	;SAVE CONTOUR DATA
	DEC	A
	STA	TEMP_X
	LDA	CON_Y	;GET CONTOUR Y
	AND	#$FFF0	;MASK OFF LOWER 16
	ORA	TEMP_X
	SEC
	SBC	#GOL_HEIGTH	;GOL HEIGTH
	STA	OBJ_Y
	JMP	IDLE_OR_WALK	;SET THE WALK ANIM
;-----
;CHANGE DIR WHILE DROPPING
@NOT_BOTTOM	LDA	DB1_DATA	;SEE IF JUMP PRESSED
	BIT	#JOY_BBUTT
	BEQ	@NO_FLAP
	LDA	FLAP_FLAG	;ALREADY DID ONE.
	BNE	@NO_FLAP
	STZ	DB1_DATA	;CLEAR JUMP BUTTON
	LDA	#JFLP       	;SET THE JUMP FLAP
	JMP	SET_GOL_ANIM
;-----
@NO_FLAP	LDA	JOY1_DATA	;GET JOYSTICK DATA
	BIT	#JOY_RIGHT	;MOVE RIGHT ?
	BEQ	@NOT_RIGHT	;NOT RIGHT
;
	SET_A8
	LDA	#0
	STA	OBJ_FLIP	;NO FLIP
	SET_A16
	JSL	CK_SPECIAL_RIGHT	;LOOK FOR SPECIAL TILES
	CMP	#ST_CLIMB0	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	CMP	#ST_CLIMB2	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
	BNE	@JUST_FALL	;YES, CAN'T FALL TO RIGHT
;
	LDA	OBJ_X	;MOVE GOLIATH TO THE RIGHT
	CLC		    
	ADC	#4
	STA	OBJ_X
	JMP	@JUST_FALL	;KEEP FALLING
;
	LONGA ON
@NOT_RIGHT	BIT	#JOY_LEFT	;MOVE LEFT ?
	BEQ	@JUST_FALL	;NOT LEFT
;
	SET_A8
     	LDA	#$40	;HORZ FLIP
	STA	OBJ_FLIP
	SET_A16
	JSL	CK_SPECIAL_LEFT	;ANY SPECIAL TILES
	CMP	#ST_CLIMB0	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	CMP	#ST_CLIMB1	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
	BNE	@JUST_FALL	;YES, CAN'T FALL TO THE LEFT
;
	LDA	OBJ_X	;MOVE GOLAITH TO THE LEFT
	SEC
	SBC	#4
	STA	OBJ_X
;
@JUST_FALL	LDA	OBJ_ANIM	;SET THE ANIMATION
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2

	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;
	PLP
	RTL
;-----
@CK_CLIMB	LDA	JOY1_DATA	;SEE IF PUSHING JUMP BUTTON
	BIT	#JOY_BBUTT
	BEQ	@JUST_FALL	;NO. JUST FALL
	LDA	#CLIMB
	JMP	SET_GOL_ANIM

        	MODEND

;------------------
;
;	    J  U   U  M   M  PPPP
;	    J  U   U  MM MM  P	P
;	    J  U   U  M M M  PPPP
;	J   J  U   U  M   M  P
;	 JJJ	UUU   M   M  P
;
;GOL_CTRL_JUMP -- DO GOLIATHS JUMP CONTROLS
;...JUMP

JMP_YTABLE	DW	-12
	DW	-7,-6,-5,-4,-4,-4
	DW	-3,-3,-3,-3,-3,-3
	DW	-2,-2,-2,-2
	DW	-1,-1
	DW	0,0,0,0
	DW	1,1
	DW	2,2,2,2
	DW	3,3,3,3,3,3
	DW	4,4,4,4,4,4,5,6,7

JMP_ANIM	DL	GOL_JMP_LP  		;0 = SQUASH LAUNCH

	DL	GOL_JMP_LP+(1*9)		;1 = STRETCH
	DL	GOL_JMP_LP+(1*9)		;2
	DL	GOL_JMP_LP+(1*9)		;3

	DL	GOL_JMP_LP+(2*9)		;4 = LEAP
	DL	GOL_JMP_LP+(2*9)		;5
	DL	GOL_JMP_LP+(2*9)		;6

	DL	GOL_JMP_LP+(3*9)		;7 = HOLD
	DL	GOL_JMP_LP+(3*9)		;8
	DL	GOL_JMP_LP+(3*9)		;9
	DL	GOL_JMP_LP+(3*9)		;10
	DL	GOL_JMP_LP+(3*9)		;11
	DL	GOL_JMP_LP+(3*9)		;12
	DL	GOL_JMP_LP+(3*9)		;13
	DL	GOL_JMP_LP+(3*9)		;14

	DL	GOL_JMP_LP+(4*9)		;15 = TRANSITION
	DL	GOL_JMP_LP+(4*9)		;16

	DL	GOL_JMP_LP+(5*9)		;17
	DL	GOL_JMP_LP+(5*9)		;18

	DL	GOL_JMP_LP+(6*9)		;19
	DL	GOL_JMP_LP+(6*9)		;20
	DL	GOL_JMP_LP+(6*9)		;21
	DL	GOL_JMP_LP+(6*9)		;22

	DL	GOL_JMP_LP+(7*9)		;23
	DL	GOL_JMP_LP+(7*9)		;24

	DL	GOL_JMP_LP+(8*9)		;25
	DL	GOL_JMP_LP+(8*9)		;26

	DL	GOL_JMP_LP+(8*9)		;27 = HOLD DURING DROP
	DL	GOL_JMP_LP+(8*9)		;28
	DL	GOL_JMP_LP+(8*9)		;29
	DL	GOL_JMP_LP+(8*9)		;30
	DL	GOL_JMP_LP+(8*9)		;31
	DL	GOL_JMP_LP+(8*9)		;32
	DL	GOL_JMP_LP+(8*9)		;33
	DL	GOL_JMP_LP+(8*9)		;34

	DL	GOL_JMP_LP+(9*9)		;35 = TOUCH
	DL	GOL_JMP_LP+(9*9)		;36
	DL	GOL_JMP_LP+(9*9)		;37

	DL	GOL_JMP_LP+(10*9)	;38 = LAND
	DL	GOL_JMP_LP+(10*9)	;39
	DL	GOL_JMP_LP+(10*9)	;40

	DL	GOL_JMP_LP+(11*9)	;41 = SQUASH
	DL	GOL_JMP_LP+(11*9)	;42
	DL	GOL_JMP_LP+(11*9)	;43

;-----

GOL_CTRL_JUMP	MODULE

	PHP
	SET_AXY16
;-----
;IF JOYPAD IS UP AND THERE IS A CEILING TILE THEN DO CEILING IDLE ANIM
	LDA	JOY1_DATA
	BIT	#JOY_UP
	BEQ	@NOT_CLIMB
	JSL	CK_SPECIAL_HEAD	;SEE IF HEAD HIT CEILING
	CMP	#ST_CEILING	;CEILING TILE HERE ?
	BNE	@NO_CEILING	;NO NOT CEILING
	SET_A16
	LDA	#CEILING_CRAWL
	JMP	RUN_GOL_ANIM
;-----
@NO_CEILING	JSL	CK_SPECIAL	;GET SPECIAL TILE
	CMP	#ST_CLIMB0	;CLIMABLE FROM BOTH SIDES ?
	BEQ	@DO_CLIMB
	CMP	#ST_CLIMB1	;CLIMABLE WHILE FACING LEFT ?
	BEQ	@LEFT_CLIMB
	CMP	#ST_CLIMB2	;CLIMABLE WHILE FACING RIGHT ?
	BNE	@NOT_CLIMB	;NOT A CLIMBING TILE
	SET_A8
	LDA	OBJ_FLIP	;GET DIRECTION OF GOLIATH
	CMP	#0	;FACING RIGHT ?
	SET_A16
	BEQ	@DO_CLIMB	;GO CLIMB WALL
	JMP	@NOT_CLIMB
;-----
@LEFT_CLIMB	SET_A8
	LDA	OBJ_FLIP	;GET DIRECTION OF GOLIATH
	CMP	#$40	;FACING LEFT ?
	SET_A16
	BNE	@NOT_CLIMB
;-----
@DO_CLIMB	LDA	#CLIMB_IDLE	;START CLIMB ANIM
	JMP	RUN_GOL_ANIM
@NOT_CLIMB
;-----
;SEE IF GOLAITH IS GOING UP OR DOWN IN HIS JUMP
	LDA	OBJ_FLAG	;BUT NOT ON THE FIRST FRAME 
	CMP	#1
	BLT	@JUMP_CK
;-----
;SEE IF GOLIATH SHOULD DO A JUMP FLAP
;	CMP	#8
;	BLT	@JUMP_CK	;NOT YET
;	CMP	#19	;AT TOP OF JUMP ?
;	BGE	@NO_JUMP

	LDA	DB1_DATA
	BIT	#JOY_BBUTT	;JUMP BUTTON PRESSED ?
	BEQ	@NO_JUMP	;NO NOT A JUMP
	STZ	DB1_DATA	;CLEAR JUMP BUTTON
	LDA	#JFLP	;DO JUMP FLAP NOW
	JMP	SET_GOL_ANIM
;-----
;SEE IF AT TOP OF JUMP YET
@NO_JUMP	LDA	OBJ_FLAG
	CMP	#19	;TOP OF JUMP ?
	BLT	@JUMP_CK	;YES STILL JUMPING
;-----
;GOLIATH IS IN THE DOWNWARD PART OF HIS JUMP
	JSL	FALL_CONTOUR	;GET CONTOUR HERE
	STA	OBJ_CON	;SAVE FOR LATER
	CMP	#0	;NO CONTOUR
	BEQ	@JUMP_CK	;NO, DID NOT LAND
	JMP	@IS_DONE	;LANDED ON A CONTOUR
;-----
;SEE IF HITTING A WALL
@JUMP_CK	JSL	CK_SPECIAL	;GET A SPECIAL TILE
	CMP	#ST_NULL	;NULL TILE ?
	BEQ	@NOT_CAT	;YES NOTHING THERE
;
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
	BNE	@JMP_CONT2	;YES
;
	JSL	CK_SPECIAL_FEET	;WHAT UNDER FOOT ?
	PHA
	CMP	#ST_DOOR0	;A DOOR WAY ?
	BLT	@NOT_DOOR
	CMP	#ST_DOOR2+1	;A DOOR WAY ?
	BGE	@NOT_DOOR
	SEC
	SBC	#ST_DOOR0
	ASL	A
	TAX
	LDA	DOOR1_FLAG,X	;IS IT OPEN
	CMP	#4
	BEQ	@NOT_DOOR
	PLA
@JMP_CONT2	JMP	@JMP_CONT	
;
@NOT_DOOR	PLA
	CMP	#ST_CATAPULT0	;WAS IT A CATAPULT ?
	BLT	@NOT_CAT	;NO
	CMP	#ST_CATAPULT3+1	;WAS IT A CATAPULT ?
	BGE	@NOT_CAT	;NO
;-----
;SET A TRAJECTORY
	SEC
	SBC	#ST_CATAPULT0	;MAKE CATAPULT #	

	TAY
	SET_A8
	LDA	OBJ_FLIP
	AND	#$40	;JUST X FLIP BIT
	CMP	LVL1A_CAT_DIR,Y	;IS THIS THE RIGHT DIRECTION ?
	BNE	@NOT_CAT	
	SET_A16	
	TYA
	JMP	JMP_GOL_TRAJ
;-----
@NOT_CAT	SET_A16
;	LDA	JOY1_DATA	;SEE IF WE NEED TO MOVE RIGHT OR LEFT
;	BIT	#JOY_RIGHT	;JUMP RIGHT ?

;-----
;WHAT DIRECTION IS THE JUMP
;	STZ	OBJ_XSPEED	;RESET X SPEED TO ZERO
	LDA	OBJ_FLIP	;WHAT DIRECTION IS JUMP
	BIT	#$40
	BNE	@TRY_LEFT	;JUMPING LEFT
;-----
;JUMPING TO THE RIGHT
;SEE IF JOYPAD IS PRESSED TO THE RIGHT ALSO
	LDA	JOY1_DATA	;GET JOYPAD
	BIT	#JOY_RIGHT
	BNE	@IS_RIGHT
	BIT	#JOY_LEFT
	BEQ	@JMP_CONT3	;NO JUST JUMP
	LDA	#1
	STA	OBJ_XSPEED
	JMP	@LEFT_JUMP
;-----
@IS_RIGHT	LDA	OBJ_XSPEED	;SET OBJECT SPEED
	BNE	@RIGHT_JUMP
	LDA	#3	;DEFAULT SPEED
	STA	OBJ_XSPEED
;-----
;SEE IF HITTING SOMETHING
@RIGHT_JUMP	JSL	CK_SPECIAL_RIGHT
	BIT	#ST_WALL_TEST
	BNE	@JMP_CONT3
;
	CMP	#ST_DOOR0	;A DOOR WAY ?
	BLT	@NOT_DOOR2
	CMP	#ST_DOOR2+1	;A DOOR WAY ?
	BGE	@NOT_DOOR2
	SEC
	SBC	#ST_DOOR0
	ASL	A
	TAX
	LDA	DOOR1_FLAG,X	;IS IT OPEN
	CMP	#4
	BEQ	@NOT_DOOR2
@JMP_CONT3	JMP	@JMP_CONT	
;-----
@NOT_DOOR2	LDA	OBJ_X
	CLC
	ADC	OBJ_XSPEED	;ADD SPEED OF GOLIATH
	STA	OBJ_X
	JMP	@JMP_CONT
;-----
@TRY_LEFT	LDA	JOY1_DATA	;GET JOYPAD DATA
	BIT	#JOY_LEFT
	BNE	@IS_LEFT
        	BIT	#JOY_RIGHT	;JUMP LEFT PRESS RIGHT ?
	BEQ	@JMP_CONT	;NOT PRESSED SO JUST JUMP
	LDA	#1
	STA	OBJ_XSPEED	;SET NEGATIVE SPEED
	JMP	@RIGHT_JUMP
;-----
@IS_LEFT	LDA	OBJ_XSPEED
	BNE	@LEFT_JUMP
	LDA	#3	;SET OBJECT SPEED
	STA	OBJ_XSPEED
;-----
;SEE IF HITTING SOMETHING
@LEFT_JUMP	JSL	CK_SPECIAL_LEFT
	BIT	#ST_WALL_TEST
	BNE	@JMP_CONT
;
	CMP	#ST_DOOR0	;A DOOR WAY ?
	BLT	@NOT_DOOR3
	CMP	#ST_DOOR2+1	;A DOOR WAY ?
	BGE	@NOT_DOOR3
	SEC
	SBC	#ST_DOOR0
	ASL	A
	TAX
	LDA	DOOR1_FLAG,X	;IS IT OPEN
	CMP	#4
	BEQ	@NOT_DOOR3
	JMP	@JMP_CONT	
;-----
@NOT_DOOR3	LDA	OBJ_X
	SEC
	SBC	OBJ_XSPEED
	STA	OBJ_X
;-----
@JMP_CONT	JSL	CK_SPECIAL_HEAD	;SEE IF HEAD HIT CEILING
	BIT	#$80	;ANY TYPE OF WALL TILE ?
	BEQ	@HEAD_CLEAR
	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@HEAD_CLEAR	LDA	OBJ_FLAG	;WHERE IN THE JUMP IS IT ?
	CMP	#7
	BLT	@MOVE_Y
	CMP	#14
	BGE	@CK_DOWN
	LDA	JOY1_DATA
	BIT	#JOY_BBUTT	;STILL PRESSING JUMP ?
	BNE	@MOVE_Y	;YES
	LDA	#15
	SEC
	SBC	OBJ_FLAG	;# OF TICKS TO SKIP
	STA	JMP_SKIP	;SAVE IT
	LDA	#15
	STA	OBJ_FLAG
	JMP	@MOVE_Y
;-----
@CK_DOWN	CMP	#27	;START HOLD FOR DROP ?
	BNE	@MOVE_Y
	CLC
	ADC	JMP_SKIP
	STA	OBJ_FLAG	;SET NEW FRAME #
;-----
@MOVE_Y	SET_A8
	LDA	#^JMP_YTABLE
	PHA
	PLB
	SET_A16
	LDA	OBJ_FLAG
	ASL	A
	TAX
	LDA	OBJ_Y
	CLC
	ADC	JMP_YTABLE,X
	STA	OBJ_Y
;-----
	TXA
	ASL	A	;OBJ_FLAG * 4
	TAX
	LDA	JMP_ANIM,X
	STA	THIS_OBJ_ANIM
	LDA	JMP_ANIM+2,X
	STA	THIS_OBJ_ANIM+2
;-----
	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
	INC	OBJ_FLAG
;-----
;SEE IF GOLIATH HAS LANDED OBJ_FLAG = -1
	LDA	OBJ_FLAG
	CMP	#44	;END OF ANIM
	BNE	@IS_OK
	STZ	JMP_SKIP	;RESET # OF FRAMES TO SKIP FOR NEXT TIME
	JSL	FALL_CONTOUR	;SEE IF CONTOUR UNDER LANDING AREA
	STA	OBJ_CON
	CMP	#0
	BNE	@IS_DONE	;WAS CONTOUR UNDER GOLIATH SO WALK
	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@IS_OK	PLP
	RTL
;-----
;SET THE WALK ROUTINE
;
@IS_DONE	DEC	A
	STA	TEMP_X
	LDA	CON_Y	;GET CONTOUR Y
	AND	#$FFF0	;MASK OFF LOWER 16
	ORA	TEMP_X
	SEC
	SBC	#GOL_HEIGTH	;GOL HEIGTH
	STA	OBJ_Y
	JMP	IDLE_OR_WALK	;SET THE WALK ANIMATION

	MODEND

;------------------
;
;	PPPP   U   U  N   N   CCCC  H   H
;	P   P  U   U  NN  N  C	   H   H
;	PPPP   U   U  N N N  C	   HHHHH
;	P      U   U  N  NN  C	   H   H
;	P       UUU   N   N   CCCC  H   H
;
;GOL_CTRL_PUNCH -- DO GOLIATHS PUNCH

GOL_CTRL_PUNCH	MODULE

	PHP
	SET_AXY16
;-----
;SEEIF GOLIATH HAS FALLEN ONTO A CONTOUR OBJ_FLAG = 1
;OR IF GOLIATH IS STILL PUNCHING OBJ_FLAG = 0
;
	LDA	OBJ_FLAG
	CMP	#-1
	BEQ	@PUNCH_DONE
	JMP	@NOT_DONE	;STILL PUNCHING
;-----
;SEE WHAT WAS PUNCHED
@PUNCH_DONE			;JSL	KILL_OPP	;TEST

	JSL	CK_SPECIAL
;-----
;WAS IT A BREAKABLE DOORWAY ?
	CMP	#ST_DOOR0	;HIT DOOR ?
	BLT	@PUNCH_MISS	;NO
	CMP	#ST_DOOR2+1	;HIT DOOR ?
	BGE	@PUNCH_MISS
	SEC
	SBC	#ST_DOOR0	;MAKE IT A POINTER
	ASL	A
	TAX
	LDA	DOOR1_FLAG,X	;DOOR ALREADY OPEN ?
	CMP	#4
	BEQ	@PUNCH_MISS	;ALREADY OPEN
	INC	A
	STA	DOOR1_FLAG,X	;NEW HIT LEVEL
;-----
;DRAW THE NEXT LEVEL OF DAMAGE TO THE DOOR
ERR_HERE	ASL	A
	ASL	A
	ASL	A	;FRAME # * 8
	PHA
;-----
	TXA
	ASL	A
	TAY
	LDA	[DOOR_ADDR],Y	;GET OFFSET OF TABLE
	STA	FRAME_ADDR
	INY
	INY
	LDA	[DOOR_ADDR],Y	;GET BANK
	STA	FRAME_ADDR+2
	PLY
;-----
	LDX	DMA_HEAP_PTR
	SET_A8
	LDA	#^DMA_HEAP
	PHA
	PLB		;SET BANK OF DMA_HEAP
;-----
	LDA	#2	;SINGLE DMA
	STA	DMA_HEAP,X	;DMA COMMAND
	INX
;-----
	LDA	#$80	;VMAINC VALUE
	STA	DMA_HEAP,X
	INX
;-----
	SET_A16
	LDA	[FRAME_ADDR],Y	;GET OFFSET OF SOURCE
	INY
	INY
	STA	DMA_HEAP,X
	INX
	INX
;-----
	LDA	[FRAME_ADDR],Y	;GET BANK
	INY
	INY
	SET_A8
	STA	DMA_HEAP,X
	INX
;-----
	SET_A16
	LDA	[FRAME_ADDR],Y	;GET VRAM DEST
	LSR	A	;ADDRESS IS 2x OF VRAM VALUE
	INY
	INY
	CLC
	ADC	BG1_CHAR	;ADD BASE ADDRESS OF CHAR DATA
	STA	DMA_HEAP,X
	INX
	INX
;-----
	LDA	[FRAME_ADDR],Y	;GET # OF BYTES
	INY
	INY
	STA	DMA_HEAP,X
	INX
	INX
	STZ	DMA_HEAP,X	;END OF DMA_HEAP
	STX	DMA_HEAP_PTR	;SAVE PTR
;-----
	LDA	#1
	STA	VRAM_DMA_FLAG	;FORCE DMA
;-----
@PUNCH_MISS	JMP	IDLE_OR_WALK
;-----
;GO AHEAD AND CONTINUE PUNCH ANIMATION
;
@NOT_DONE	LDA	OBJ_ANIM
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2
	STA	THIS_OBJ_ANIM+2
;-----
	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
	PLP
	RTL

	MODEND

;------------------
;GOL_CTRL_TRAJ -- GOLIATH ON A TRAJECTORY

GOL_CTRL_TRAJ	MODULE

	PHP
	SET_AXY16
;-----
	LDA	NMI_CTR	;GET CURRENT TIME
;	BIT	#$01
;	BEQ	@NOT_END
;-----
;GET NEXT DELTA X,Y IN TRAJECTORY
	LDY	OBJ_FLAG	;GET OFFSET INTO TRAJ TABLE
	INY
	INY
	LDA	[GOL_TRAJ],Y	;GET DELTA Y
	BIT	#$8000	;A NEGATIVE # ?
	BNE	@GOING_UP
;-----
	JSL	FALL_CONTOUR	;GET CONTOUR #
	CMP	#0
	BNE	@LANDED
	JMP	@GOING_UP
;-----
@LANDED	STA	OBJ_CON	;SAVE CONTOUR DATA
	DEC	A
	STA	TEMP_X
	LDA	CON_Y	;GET CONTOUR Y
	AND	#$FFF0	;MASK OFF LOWER 16
	ORA	TEMP_X
	SEC
	SBC	#GOL_HEIGTH	;GOL HEIGTH
	STA	OBJ_Y
	JMP	IDLE_OR_WALK	;SET THE WALK ANIM
;-----
@GOING_UP	LDY	OBJ_FLAG
	LDA	[GOL_TRAJ],Y	;ADD DELTA X
	STA	TEMP_X
 	INY
	INY
	LDA	[GOL_TRAJ],Y	;ADD DELTA Y
	STA	TEMP_Y
	INY
	INY
	STY	OBJ_FLAG	;SAVE POINTER INTO TRAJ TABLE
;
         	JSL	CK_SPECIAL	;SEE IF HIT WALL
	BIT	#ST_WALL_TEST	;HIT WALL
	BEQ	@NO_WALL	
        	STZ	TEMP_X	;NO X MOTION
;
@NO_WALL	LDA	TEMP_X
;	STA	OBJ_DX
	CLC
	ADC	OBJ_X
	STA	OBJ_X
	LDA	TEMP_Y
;	STA	OBJ_DY
	CLC
	ADC	OBJ_Y
	STA	OBJ_Y
;
	LDA	TEMP_X	;END OF TRAJ ?
	ORA	TEMP_Y
	BNE	@NOT_END
	LDA	#FALL
	JMP	SET_GOL_ANIM

; 	JMP	JMP_GOL_FALL
;
@NOT_END	PLP
	RTL

        	MODEND

;------------------
;
;	 CCCC  L      III  M   M	 BBBB       III  DDDD   L      EEEEE
;	C      L       I   MM MM	 B   B       I   D   D	L      E
;	C      L       I   M M M	 BBBB        I   D   D	L      EEEE
;	C      L       I   M   M	 B   B       I   D   D	L      E
;	 CCCC  LLLLL  III  M   M  BBBB       III  DDDD	LLLLL  EEEEE
;
;GOL_CTRL_CLIDL -- GOLIATH CLIMB IDLE

GOL_CTRL_CLIDL	MODULE

	PHP
	SET_AXY16
;-----
	STZ	FLAP_FLAG	;ALLOW A FLAP
	JSL	EXPLORE
;-----
	LDA	DB1_DATA
	BIT	#JOY_BBUTT	;PRESS JUMP TO RELEASE FROM WALL
	BEQ	@NOT_JUMP
	SET_A8
	LDA	OBJ_FLIP
	EOR	#$40	;FLIP DIRECTION
	STA	OBJ_FLIP
	SET_A16
	LDA	#JUMP	;SET THE JUMP ANIMATION
	JMP	SET_GOL_ANIM
;-----
@NOT_JUMP	BIT	#JOY_ABUTT	;PRESS 'A' TO RELEASE FROM WALL
	BEQ	@NOT_FALL
	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@NOT_FALL	LDA	JOY1_DATA	;MOVE UP?
	BIT	#JOY_UP
	BEQ	@TRY_RIGHT
@DO_CLIMB	LDA	#CLIMB
	JMP	SET_GOL_ANIM
;-----
@TRY_RIGHT
;	BIT	#JOY_DOWN	;MOVE RIGHT ?
;	BNE	@DO_CLIMB
;-----
@DO_CIDLE	LDA	OBJ_ANIM	;SET THE ANIMATION
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2

	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
	PLP
	RTL

	MODEND

;------------------
;
;	 CCCC  L      III  M   M	 BBBB 
;	C      L       I   MM MM	 B   B
;	C      L       I   M M M	 BBBB 
;	C      L       I   M   M	 B   B
;	 CCCC  LLLLL  III  M   M  BBBB 
;
;GOL_CTRL_CLIMB -- CONTROL GOLIATH CLIMBING

GOL_CTRL_CLIMB	MODULE

          	PHP
	SET_AXY16
;-----
;PRESSING JUMP BUTTON ?
	LDA	DB1_DATA
	BIT	#JOY_BBUTT	;PRESSING JUMP BUTTON
	BEQ	@NOT_JUMP
;-----
;JUMP IN SAME DIRECTION ?
	LDA	JOY1_DATA	;GET JOY1 DATA
	BIT	#JOY_LEFT
	BNE	@JUMP_LEFT
;-----
;JOYPAD TO RIGHT
;IS GOLIATH TO RIGHT ?
	SET_A8
	LDA	OBJ_FLIP	;GET FLIP
	SET_A16
	BEQ	@NOT_JUMP	;SAME DIRECTION NO JUMP
	JMP	@DO_JUMP
;-----
;JOYPAD TO LEFT
;IS GOLIATH TO LEFT ?
@JUMP_LEFT	SET_A8
	LDA	OBJ_FLIP	;GET FLIP
	SET_A16
	BNE	@NOT_JUMP	;SAME DIRECTION NO JUMP
;-----
@DO_JUMP	SET_A8
	EOR	#$40	;FLIP DIRECTION
	STA	OBJ_FLIP
	SET_A16
	LDA	#JUMP	;SET THE JUMP ANIMATION
	JMP	SET_GOL_ANIM
;-----
@NOT_JUMP	LDA	JOY1_DATA	;SEE IF STILL GOING UP OR DOWN
	BIT	#JOY_UP
	BNE	@NOT_IDLE
	JMP	@SET_IDLE
@NOT_IDLE	JSL	CK_ST_CLIMB	;CLIMB WALL STILL THERE ?
	CMP	#ST_CLIMB0	;DOUBLE SIDED CLIMABLE WALL ?
	BEQ	@NOT_COLUMN	;CAN CLIMB BUT SEE IF A CEILING THERE
;	BEQ	@DO_CLIMB	;YES GO CLIMB IT
	CMP	#ST_TRANS0	;TOP OF A COLUMN ?
	BNE	@NOT_COLUMN
	SET_A8
	LDA	OBJ_FLIP
	SET_A16
	BNE	@LEFT_EDGE	;FACING LEFT
	JMP	@RIGHT_EDGE	;FACING RIGHT
;-----
@NOT_COLUMN	SET_A8
	LDA	OBJ_FLIP
	SET_A16
	BEQ	@CLIMB_RIGHT
	LDA	SPECIAL_00	;LOOK TO SEE IF CLIMBABLE WALL
	CMP	#ST_CLIMB1	;CLIMBING TO LEFT ?
	BEQ	@DO_CLIMB

	CMP	#ST_TRANS4	;EDGE OF FLOOR ?
	BEQ	@LEFT_EDGE	;GOING OVER A LEFT FACING LEDGE

	LDA	SPECIAL_01	;IS TILE TO THE RIGHT A CRAWL TILE ?
	CMP	#ST_CEILING	;CEILING HERE ?
	BEQ	@DO_CRAWL

	LDA	SPECIAL_00	;LOOK TO SEE IF CLIMBABLE WALL
	CMP	#ST_CLIMB0	;DOUBLE SIDED CLIMABLE WALL ?
	BEQ	@DO_CLIMB

	JMP	@MAKE_FALL    	;NO WALL GONE!
;-----
@CLIMB_RIGHT	LDA	SPECIAL_00
	CMP	#ST_CLIMB2	;CLIMBING TO RIGHT ?
	BEQ	@DO_CLIMB

	CMP	#ST_TRANS5	;EDGE OF FLOOR ?
	BEQ	@RIGHT_EDGE

	LDA	SPECIAL_01
	CMP	#ST_CEILING	;CEILING TILE ?
	BEQ	@DO_CRAWL

	LDA	SPECIAL_00	;LOOK TO SEE IF CLIMBABLE WALL
	CMP	#ST_CLIMB0	;DOUBLE SIDED CLIMABLE WALL ?
	BEQ	@DO_CLIMB

	JMP	@MAKE_FALL
;-----
@DO_CLIMB	LDA	OBJ_ANIM	;SET THE OBJECT ANIMATION 
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2

	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;
	PLP
	RTL
;-----
@MAKE_FALL	STZ	JOY1_DATA	;TEST
	LDA	#FALL	;SET THE FALLING ANIMATION
	JMP	SET_GOL_ANIM
;-----
@DO_CRAWL	SET_A8
	LDA	OBJ_FLIP
	EOR	#$40
	STA	OBJ_FLIP
	SET_A16
	LDA	#CEILING_IDLE	;SET THE CEILING CRAWL ANIMATION
	JMP	SET_GOL_ANIM
;-----
@SET_IDLE	LDA	#CLIMB_IDLE	;SET CLIMB IDLE ANIMATION
	JMP	SET_GOL_ANIM
;-----
@LEFT_EDGE	LDA	OBJ_X
	SEC
	SBC	#40
	STA	OBJ_X
	LDA	OBJ_Y
	SEC
	SBC	#75
	STA	OBJ_Y
	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@RIGHT_EDGE	LDA	OBJ_X
	CLC
	ADC	#$20
	STA	OBJ_X
	LDA	OBJ_Y
	SEC
	SBC	#75
	STA	OBJ_Y
	LDA	#FALL
	JMP	SET_GOL_ANIM



	MODEND

;------------------
;GOL_CTRL_R2S -- GOLIATH ACCELERATING UP TO FAST RUN

GOL_CTRL_R2S	MODULE

	PHP
	SET_AXY16
;-----
;SEE IF TIME TO TO SPRINT
	LDA	OBJ_FLAG
	CMP	#1	;DID ONE ANIM CYCLE ?
	BEQ	@SET_SPRINT
	JMP	R2S_CONT	;CONTINUE TO ACCELERATE
;-----
@SET_SPRINT	LDA	#SPRINT
	JMP	SET_GOL_ANIM	;START THE SPRINT ANIM

	MODEND

;------------------
;
;	 SSSS  PPPP   RRRR   III	 N   N  TTTTT
;	S      P   P  R   R   I	 NN  N	  T
;	SSSS   PPPP   RRRR    I	 N N N	  T
;	    S  P      R  R    I	 N  NN	  T
;	SSSS   P      R   R  III  N   N	  T
;
;GOL_CTRL_SPRT -- GOLIATH'S FAST RUN

GOL_CTRL_SPRT	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#5
	STA	OBJ_XSPEED	;SET GOLIATH'S SPEED
;-----
	JMP	R2S_CONT	;CONTINUE TO SPRINT

	RTL

	MODEND

;------------------
;
;	    J  U   U  M   M  PPPP	FFFFF  L       AAA   PPPP
;	    J  U   U  MM MM  P	P       F      L      A	  A  P	 P
;	    J  U   U  M M M  PPPP	FFFF   L      AAAAA  PPPP
;	J   J  U   U  M   M  P		F      L      A	  A  P
;	 JJJ	UUU   M   M  P		F      LLLLL  A	  A  P
;
;------------------
;GOL_CTRL_JFLP -- CONTROL GOLIATH'S JUMP FLAP

GOL_CTRL_JFLP	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#1
	STA	FLAP_FLAG	;DON'T ALLOW ANOTHER FOR NOW
;-----
	LDA	OBJ_FLAG	;IS IT DONE ?
	CMP	#-1
	BNE	@NOT_DONE
	LDA	OBJ_XSPEED	;IS IT STOPPED ?
	BNE	@NOT_STOPPED
	LDA	#1
	STA	OBJ_XSPEED	;SET SPEED
@NOT_STOPPED	LDA	#GLIDE       	;DO GLIDE ANIM NOW
	JMP	SET_GOL_ANIM
;-----
;IF JOYPAD IS UP AND THERE IS A CEILING TILE THEN DO CEILING IDLE ANIM
@NOT_DONE	LDA	JOY1_DATA
	BIT	#JOY_UP
	BEQ	@NO_CEILING
	JSL	CK_SPECIAL_HEAD	;SEE IF HEAD HIT CEILING
	CMP	#ST_CEILING	;CEILING TILE HERE ?
	BNE	@NO_CEILING	;NO NOT CEILING
	SET_A16
	LDA	#CEILING_CRAWL
	JMP	RUN_GOL_ANIM
;-----
@NO_CEILING	LDA	OBJ_FLAG
	CMP	#1	;ALREADY DECELERATED ?
	BEQ	@FLAP_MOVE
;
	LDA	OBJ_XSPEED	;GET CURRENT SPEED
	CMP	#0	;ALREADY STOPPED ?
	BEQ	@STOPPED
	LDA	OBJ_XSPEED
	SEC
	SBC	#1
	STA	OBJ_XSPEED
	LDA	#1
	STA	OBJ_FLAG
;-----
@FLAP_MOVE	LDA	DB1_DATA	;GET DEBOUNCE DATA
	BIT	#JOY_BBUTT	;JUMP BUTTON ?
	BNE	@JUST_FALL
	LDA	OBJ_FLIP
	BIT	#$40	;MOVE RIGHT ?
	BNE	@NOT_RIGHT	;NOT RIGHT
;
	JSL	CK_SPECIAL_RIGHT	;LOOK FOR SPECIAL TILES
	CMP	#ST_CLIMB0	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	CMP	#ST_CLIMB2	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
	BNE	@JUST_FALL	;YES, CAN'T FALL TO RIGHT
;
	LDA	OBJ_X	;MOVE GOLIATH TO THE RIGHT
	CLC		    
	ADC	OBJ_XSPEED
	STA	OBJ_X
	JMP	@STOPPED	;KEEP FALLING
;
@NOT_RIGHT	JSL	CK_SPECIAL_LEFT	;ANY SPECIAL TILES
	CMP	#ST_CLIMB0	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	CMP	#ST_CLIMB1	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
	BNE	@JUST_FALL	;YES, CAN'T FALL TO THE LEFT
;
	LDA	OBJ_X	;MOVE GOLAITH TO THE LEFT
	SEC
	SBC	OBJ_XSPEED
	STA	OBJ_X
;-----		     
@STOPPED	LDA	OBJ_ANIM	;SET THE ANIMATION
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2

	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
	PLP
	RTL
;-----
@JUST_FALL	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@CK_CLIMB	LDA	JOY1_DATA	;SEE IF PUSHING JUMP BUTTON
	BIT	#JOY_BBUTT
	BEQ	@JUST_FALL	;NO. JUST FALL
	LDA	#CLIMB
	JMP	SET_GOL_ANIM

	MODEND

;------------------
;
;	GGGGG  L      III  DDDD   EEEEE
;	G      L       I   D   D	 E
;	G  GG  L       I   D   D	 EEE
;	G   G  L       I   D   D	 E
;	GGGGG  LLLLL  III  DDDD	 EEEEE
;
;GOL_CTRL_GLIDE -- CONTROL GOLIATH DURING HIS GLIDE

GOL_CTRL_GLIDE	MODULE

	PHP
	SET_AXY16
;-----
	JSL	EXPLORE
;-----
	LDA	#2
	STA	OBJ_YSPEED
	INC	OBJ_FLAG
;-----
	JSL	FALL_CONTOUR	;GET CONTOUR #
	CMP	#0	;NO COUNTOUR ?
	BNE	@IS_BOTTOM	;HIT A CONTOUR!
	JMP	@NOT_BOTTOM	;KEEP FALLING
;-----
@IS_BOTTOM	STA	OBJ_CON	;SAVE CONTOUR DATA
	DEC	A
	STA	TEMP_X
	LDA	CON_Y	;GET CONTOUR Y
	AND	#$FFF0	;MASK OFF LOWER 16
	ORA	TEMP_X
	SEC
	SBC	#GOL_HEIGTH	;GOL HEIGTH
	STA	OBJ_Y
	JMP	IDLE_OR_WALK	;SET THE WALK ANIM
;-----
;CHANGE DIR WHILE DROPPING
@NOT_BOTTOM	SET_A8
	LDA	OBJ_FLIP
	CMP	#$40	;GOING LEFT ?
	SET_A16
	BEQ	@GLIDE_LEFT
;-----
	LDA	JOY1_DATA
	BIT	#JOY_RIGHT	;SPEED UP ?
	BNE	@SPEED_UP
	BIT	#JOY_LEFT	;SLOW DOWN ?
	BNE	@SLOW_DOWN
	JMP	@NO_CONTROL
;-----
@GLIDE_LEFT	LDA	JOY1_DATA
	BIT	#JOY_LEFT	;SPEED UP ?
	BNE	@SPEED_UP
	BIT	#JOY_RIGHT	;SLOW DOWN ?
	BNE	@SLOW_DOWN
	JMP	@NO_CONTROL
;-----
@SPEED_UP	LDA	OBJ_FLAG
	BIT	#7
	BNE	@NO_CONTROL
	LDA	OBJ_XSPEED
	CMP	#5	;AT MAX ?
	BEQ	@NO_CONTROL
	INC	A
	STA	OBJ_XSPEED
	JMP	@NO_CONTROL
;-----
@SLOW_DOWN	LDA	OBJ_FLAG
	BIT	#7
	BNE	@NO_CONTROL
	LDA	OBJ_XSPEED
	CMP	#1	;AT MIN ?
	BNE	@NOT_MIN
	SET_A8
	LDA	OBJ_FLIP
	EOR	#$40
	STA	OBJ_FLIP	;TURN AROUND
	SET_A16
	JMP	@NO_CONTROL
@NOT_MIN	DEC	A
	STA	OBJ_XSPEED
;-----
@NO_CONTROL	LDA	DB1_DATA	;GET DEBOUNCE DATA
	BIT	#JOY_BBUTT	;JUMP BUTTON ?
	BNE	@FLAP_OR_FALL
	LDA	OBJ_FLIP
	BIT	#$40	;MOVE RIGHT ?
	BNE	@NOT_RIGHT	;NOT RIGHT
;
	JSL	CK_SPECIAL_RIGHT	;LOOK FOR SPECIAL TILES
	CMP	#ST_CLIMB0	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	CMP	#ST_CLIMB2	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
	BNE	@JUST_FALL	;YES, CAN'T FALL TO RIGHT
;
	LDA	OBJ_X	;MOVE GOLIATH TO THE RIGHT
	CLC		    
	ADC	OBJ_XSPEED
	STA	OBJ_X
	JMP	@GLIDE_Y	;KEEP FALLING
;
@NOT_RIGHT	JSL	CK_SPECIAL_LEFT	;ANY SPECIAL TILES
	CMP	#ST_CLIMB0	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	CMP	#ST_CLIMB1	;A CLIMBABLE WALL
	BEQ	@CK_CLIMB	;SEE IF GOLIATH SHOULD CLIMB
	BIT	#ST_WALL_TEST	;ANY TYPE OF WALL ?
;	BNE	@JUST_FALL	;YES, CAN'T FALL TO THE LEFT
;
	LDA	OBJ_X	;MOVE GOLAITH TO THE LEFT
	SEC
	SBC	OBJ_XSPEED
	STA	OBJ_X
;
@GLIDE_Y	LDA	OBJ_Y
	CLC
	ADC	OBJ_YSPEED	;GLIDE RATIO
	STA	OBJ_Y
;-----
	LDA	OBJ_ANIM	;SET THE ANIMATION
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2

	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;
	PLP
	RTL
;-----
@JUST_FALL	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@CK_CLIMB	LDA	JOY1_DATA	;SEE IF PUSHING JUMP BUTTON
	BIT	#JOY_BBUTT
	BEQ	@JUST_FALL	;NO. JUST FALL
	LDA	#CLIMB
	JMP	SET_GOL_ANIM
;-----
@FLAP_OR_FALL	LDA	FLAP_FLAG
	BNE	@JUST_FALL
	STZ	DB1_DATA	;CLEAR JUMP BUTTON
	LDA	#JFLP	;SET THE JUMP FLAP ANIM
	JMP	SET_GOL_ANIM

	MODEND

;------------------
;
;	CCCCC  EEEEE  III  L	 III  N	  N  GGGGG     CCCCC  RRRR    AAA   W	W  L	     III  DDDD   L   	EEEEE
;	C      E       I   L	  I   NN  N  G	       C      R	  R  A	 A  W	W  L	      I	  D   D	 L	E
;	C      EEEE    I   L	  I   N	N N  G	GG     C      RRRR   AAAAA  W	W  L	      I	  D   D	 L	EEEE
;	C      E       I   L	  I   N	 NN  G	 G     C      R  R   A	 A  W W W  L	      I	  D   D	 L	E
;	CCCCC  EEEEE  III  LLLLL  III  N	  N  GGGGG     CCCCC  R	  R  A	 A  WW WW  LLLLL     III  DDDD	 LLLLL  EEEEE
;				  
;GOL_CTRL_CIDLE -- GOLIATH IDLE ON CEILING

GOL_CTRL_CIDLE	MODULE

	PHP
	SET_AXY16
;-----
	STZ	FLAP_FLAG	;ALLOW A FLAP
	JSL	EXPLORE
;-----
	LDA	DB1_DATA
	BIT	#JOY_BBUTT
	BEQ	@NOT_FALL
	STZ	DB1_DATA	;CLEAR JUMP BUTTON
	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
@NOT_FALL	JSL	CK_SPECIAL_CRAWL	;GET CRAWLING SPECIAL TILES	
	CMP	#ST_TRANS3
	BNE	@NOT_LEFT
	JSL	CK_ROLL_LEFT
	BCC	@TRY_LEFT
	JMP	@WRAP_2RIGHT
;
@NOT_LEFT	CMP	#ST_TRANS2
	BNE	@TRY_LEFT
	JSL	CK_ROLL_RIGHT
	BCC	@DO_CIDLE
	JMP	@WRAP_2LEFT
;-----
@TRY_LEFT	LDA	JOY1_DATA
	BIT	#JOY_LEFT	;MOVE LEFT ?
	BEQ	@TRY_RIGHT
	SET_A8
	LDA	OBJ_FLIP
	BNE	@DO_CRAWL
	LDA	#$40
	STA	OBJ_FLIP	;SET NORMAL DIRECTION
	SET_A16
	JMP	@DO_CRAWL
;-----
@DO_CRAWL	SET_A16
	LDA	#CEILING_CRAWL
	JMP	SET_GOL_ANIM
;-----
@TRY_RIGHT	BIT	#JOY_RIGHT	;MOVE RIGHT ?
	BEQ	@DO_CIDLE
	SET_A8
	LDA	OBJ_FLIP
	BEQ	@DO_CRAWL
	LDA	#0
	STA	OBJ_FLIP
	SET_A16
	JMP	@DO_CRAWL
;-----
@DO_CIDLE	LDA	OBJ_ANIM	;SET THE ANIMATION
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2

	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
	PLP
	RTL
;-----
@WRAP_2RIGHT	SET_A8
	STZ	OBJ_FLIP
	SET_A16
;-----
	LDA	OBJ_Y
	SEC
	SBC	#70
	STA	OBJ_Y
	LDA	#IDLE
	JMP	SET_GOL_ANIM
;-----
@WRAP_2LEFT	SET_A8
	LDA	#$40
	STA	OBJ_FLIP
	SET_A16
;-----
	LDA	OBJ_X
	SEC
	SBC	#$0
	STA	OBJ_X
	LDA	OBJ_Y
	SEC
	SBC	#70
	STA	OBJ_Y
	STX	JOY1_DATA
	LDA	#IDLE
	JMP	SET_GOL_ANIM

	MODEND

;------------------
;
;	CCCCC  EEEEE  III  L	 III  N	  N  GGGGG     CCCCC  RRRR    AAA   W	W  L
;	C      E       I   L	  I   NN  N  G	       C      R	  R  A	 A  W	W  L
;	C      EEEE    I   L	  I   N	N N  G	GG     C      RRRR   AAAAA  W	W  L
;	C      E       I   L	  I   N	 NN  G	 G     C      R  R   A	 A  W W W  L
;	CCCCC  EEEEE  III  LLLLL  III  N	  N  GGGGG     CCCCC  R	  R  A	 A  WW WW  LLLLL
;				  
;GOL_CTRL_CRAWL -- GOLIATH CRAWLING ACROSS A CEILING

GOL_CTRL_CRAWL	MODULE

	PHP
	SET_AXY16
;-----
	LDA	DB1_DATA
	BIT	#JOY_BBUTT
	BEQ	@NOT_FALL
;-----
@DO_FALL	LDA	#FALL
	JMP	SET_GOL_ANIM
;-----
;CRAWLING TO THE LEFT HERE
@NOT_FALL	LDA	JOY1_DATA	;MOVE LEFT?
	BIT	#JOY_LEFT
	BEQ	@TRY_RIGHT
	SET_A8
	LDA	OBJ_FLIP
	SET_A16
	BEQ	@STOP_CRAWL	;GO TO IDLE
	JSL	CK_SPECIAL_CRAWL	;GET CRAWLING SPECIAL TILES
	CMP	#ST_CEILING	;A CRAWLABLE CEILING ?
	BEQ	@CRAWL_LEFT	;YES 
	CMP	#ST_CLIMB2	;CLIMB WALL TO RIGHT?
	BEQ	@CLIMB_RIGHT	;YES
	CMP	#ST_TRANS3	;A WRAP AROUND EDGE ?
	BEQ	@WRAP_2RIGHT_LK
	JMP	@DO_FALL
@WRAP_2RIGHT_LK
	JMP	@WRAP_2RIGHT
;-----
@CRAWL_LEFT	LDA	OBJ_X
	SEC
	SBC	#3
	STA	OBJ_X
	JMP	@DO_CRAWL
;-----
;CRAWLING TO THE RIGHT
@TRY_RIGHT	BIT	#JOY_RIGHT	;MOVE RIGHT ?
	BEQ	@STOP_CRAWL
	SET_A8
	LDA	OBJ_FLIP	;ALREADY POINTING RIGHT ?
	SET_A16
	BNE	@STOP_CRAWL
	JSL	CK_SPECIAL_CRAWL	;GET CRAWLING SPECIAL TILES
	CMP	#ST_CEILING	;A CRAWLABLE CEILING ?
	BEQ	@CRAWL_RIGHT	;YES 
	CMP	#ST_CLIMB1	;CLIMB WALL TO LEFT?
	BEQ	@CLIMB_LEFT	;YES
	CMP	#ST_TRANS2	;A WRAP AROUND EDGE ?
	BEQ	@WRAP_2LEFT
	JMP	@DO_FALL
;-----
@CRAWL_RIGHT	LDA	OBJ_X
	CLC
	ADC	#3
	STA	OBJ_X
;-----
@DO_CRAWL	LDA	OBJ_ANIM	;SET THE ANIMATION
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2

	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
@NO_CRAWL	PLP
	RTL
;-----
@STOP_CRAWL	LDA	#CEILING_IDLE
	JMP	SET_GOL_ANIM
;-----
@CLIMB_RIGHT	SET_A8
	STZ	OBJ_FLIP
	SET_A16
	LDA	OBJ_X
	SEC
	SBC	#64-20
	STA	OBJ_X
	LDA	#CLIMB_IDLE
	JMP	SET_GOL_ANIM
;-----
@CLIMB_LEFT	SET_A8
	LDA	#$40
	STA	OBJ_FLIP
	SET_A16
;-----
	LDA	OBJ_X
	CLC
	ADC	#64-20	;MOVE GOLIATH OVER TO SIMULATE SWING
	STA	OBJ_X
	LDA	OBJ_Y
	SEC
	SBC	#8
	STA	OBJ_Y
;-----
	LDA	#CLIMB_IDLE
	JMP	SET_GOL_ANIM
;-----
@WRAP_2RIGHT	STZ	JOY1_DATA
	JMP	@STOP_CRAWL

	SET_A8
	STZ	OBJ_FLIP
	SET_A16
;-----
	LDA	OBJ_Y
	SEC
	SBC	#70
	STA	OBJ_Y
	LDA	#IDLE
	JMP	SET_GOL_ANIM
;-----
@WRAP_2LEFT	STZ	JOY1_DATA
	JMP	@STOP_CRAWL

	SET_A8
	LDA	#$40
	STA	OBJ_FLIP
	SET_A16
;-----
	LDA	OBJ_X
	SEC
	SBC	#$0
	STA	OBJ_X
	LDA	OBJ_Y
	SEC
	SBC	#70
	STA	OBJ_Y
	STX	JOY1_DATA
	LDA	#IDLE
	JMP	SET_GOL_ANIM

	MODEND
;------------------
;
;	EEEEE  X   X  PPPP   L	    OOO	  RRRR	 EEEEE
;	E	X X   P   P  L	   O   O  R   R	 E
;	EEEE	 X    PPPP   L	   O   O  RRRR	 EEEE
;	E	X X   P      L	   O   O  R  R	 E
;	EEEEE  X   X  P      LLLLL   OOO	  R   R  EEEEE
;
;
;EXPLORE -- SEE IF EXPLORE MODE ON
;...EXPLORE

EXPLORE	MODULE

	LDA	JOY1_DATA
	BIT	#JOY_RBUTT
	BEQ	@NO_EXPLORE
	BIT	#JOY_RIGHT
	BEQ	@EXP_LEFT
	LDA	OBJ_X
	CLC
	ADC	#3
	STA	OBJ_X
	JMP	@EXP_UP
@EXP_LEFT	BIT	#JOY_LEFT
	BEQ	@EXP_UP
	LDA	OBJ_X
	SEC
	SBC	#3
	STA	OBJ_X
@EXP_UP	LDA	JOY1_DATA
	BIT	#JOY_UP
	BEQ	@EXP_DOWN
	LDA	OBJ_Y
	SEC
	SBC	#3
	STA	OBJ_Y
	JMP	@EXP_DONE
@EXP_DOWN	BIT	#JOY_DOWN
	BEQ	@EXP_DONE
	LDA	OBJ_Y
	CLC
	ADC	#3
	STA	OBJ_Y
;-----
@EXP_DONE	SET_A16
	LDA	OBJ_ANIM	;SET ANIM 
	STA	THIS_OBJ_ANIM
	LDA	OBJ_ANIM+2	;ADDRESS
	STA	THIS_OBJ_ANIM+2
;-----
	JSL	DO_ANIM_CMDS	;EXECUTE THE ANIM LIST NOW
;-----
;PULL RETURN ADDRESS
	PLA
	SET_A8
	PLA
;-----
	PLP
@NO_EXPLORE	RTL

	MODEND

;------------------
;CK_ROLL_RIGHT -- SEE IF A ROLL RIGHT PATTERN WAS INPUT

ROLL_RIGHT	DW	20*2	;# OF 1/60 SECOND DATA POINTS TO GO BACK
	DW	JOY_RIGHT	;INPUT TO LOOK FOR
	DW	15	;MAX # OF REPEATS
	DW	JOY_RIGHT!JOY_UP	;INPUT TO LOOK FOR
	DW	15	;MAX # OF REPEATS
	DW	JOY_UP	;INPUT TO LOOK FOR
	DW	15	;MAX # OF REPEATS
	DW	-1	;END OF PATTERN

	DW	JOY_UP!JOY_LEFT	;INPUT TO LOOK FOR
	DW	10	;MAX # OF REPEATS
	DW	JOY_LEFT	;INPUT TO LOOK FOR
	DW	10	;MAX # OF REPEATS

CK_ROLL_RIGHT	MODULE

	SET_A16
	LDA	#<ROLL_RIGHT
	STA	PATTERN
	LDA	#^ROLL_RIGHT
	STA	PATTERN+2
	JMP	PATTERN_CK

	MODEND

;------------------
;CK_ROLL_LEFT -- SEE IF A ROLL LEFT PATTERN WAS INPUT

ROLL_LEFT	DW	20*2	;# OF 1/60 SECOND DATA POINTS TO GO BACK
	DW	JOY_LEFT	;INPUT TO LOOK FOR
	DW	15	;MAX # OF REPEATS
	DW	JOY_LEFT!JOY_UP	;INPUT TO LOOK FOR
	DW	15	;MAX # OF REPEATS
	DW	JOY_UP	;INPUT TO LOOK FOR
	DW	15	;MAX # OF REPEATS
	DW	-1	;END OF PATTERN

CK_ROLL_LEFT	MODULE

	SET_A16
	LDA	#<ROLL_LEFT
	STA	PATTERN
	LDA	#^ROLL_LEFT
	STA	PATTERN+2
	JMP	PATTERN_CK

	MODEND

;------------------
;------------------
;------------------
;------------------

