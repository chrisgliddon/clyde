;------------------
;
;GG_TEXT.658
;
;  TEXT HANDLING ROUTINES
;
;
;------------------

;	SECTION	GG_CODE,?

;------------------
;EQUATES


MSG1	DB	'THIS IS A TEST MESSAGE',13
	DB	'!"#$%&''()*+,-./',13
	DB	'0123456789:;<=>?@',13
	DB	'ABCDEFGHIJKLMNOPQRSTUV',13
	DB	'WXYZ[\]^_`abcdefghijkl',13
	DB	'mnopqrstuvwxyz{|}~',0

MAIN_MENU	DB	'PICK A LEVEL',0

;-----

LEVEL_TEXT	DL	LVL1
	DL	LVL2
	DL	LVL3
	DL	LVL4
	DL	LVL5
	DL	LVL6

	DL	MODE7
	DL	LVL7
	DL	DISNEY
	DL	BONUS


;-----

SUB_LVLS	EQU	10

CLR_TEXT	DB	'                                 ',0

LVL1	DB	'LEVEL 1 - ANCIENT CASTLE',0

LVL2	DB	'LEVEL 2 - ROOKERY',0

LVL3	DB	'LEVEL 3 - ATRIUM',0

LVL4	DB	'DESCENT TEST',0

LVL5	DB	'LEVEL 5 - MUSEUM',0

LVL6	DB	'ASCENT TEST',0

MODE7	DB	'MODE 7 TEST',0

LVL7	DB	'LEVEL 7 - HYDRO PLANT',0

DISNEY	DB	'DISNEY LOGO',0

BONUS	DB	'CLOCK TOWER',0

;------------------
;DO_MAIN_MENU -- 

DO_MAIN_MENU	MODULE

	PHP
;-----
	SET_A16
	STZ	XSCROLL_RATE
	STZ	YSCROLL_RATE
;
	LDA	#$0400
	STA	TEXT_PAL
	LDA	#0
	STA	CHAR_BASE
;
	LDA	#<MAIN_MENU
	STA	TEXT_ADDR
	SET_A8
	LDA	#^MAIN_MENU
	STA	TEXT_ADDR+2
	SET_A16
	LDX	#10
	LDY	#20
	JSL	PRINT_TEXT
;
	LDA	#<CLR_TEXT
	STA	TEXT_ADDR
	SET_A8
	LDA	#^CLR_TEXT
	STA	TEXT_ADDR+2
	SET_A16
	LDX	#0
	LDY	#22
	JSL	PRINT_TEXT
;
	LDA	GAME_LEVEL
	ASL	A
	ASL	A
	TAX
;
	LDA	LEVEL_TEXT,X	;GET OFFSET
	STA	TEXT_ADDR
	SET_A8
	LDA	LEVEL_TEXT+2,X	;GET BANK
	STA	TEXT_ADDR+2
	SET_A16
	JSL	CENTER_TEXT	;CENTER THE TEXT VALUE IN X
	LDY	#22
	JSL	PRINT_TEXT
;-----
;NOW SETUP THE DMA DATA ON THE DMA_HEAP
	SET_XY16
	LDX	DMA_HEAP_PTR	;GET PTR
	SET_A8
	LDA	#2	;SINGLE DMA
	STA	DMA_HEAP,X
	INX
;
	LDA	#$80	;VMAINC
	STA	DMA_HEAP,X
	INX
;
	SET_A16
	LDA	#<SCRN_BUF	;OFFSET
	STA	DMA_HEAP,X
	INX
	INX
;
	SET_A8
	LDA	#^SCRN_BUF	;BANK
	STA	DMA_HEAP,X
	INX
;
	SET_A16
	LDA	BG1_SC	;DEST OFFSET
	STA	DMA_HEAP,X
	INX
	INX
;
	LDA	#2048	;# OF BYTES
	STA	DMA_HEAP,X
	INX
	INX
	STZ	DMA_HEAP,X	;END OF DMA_HEAP
	STX	DMA_HEAP_PTR	;SAVE PTR
;
	LDA	#1
	STA	VRAM_DMA_FLAG	;FORCE DMA
;-----
	PLP
	RTL

	MODEND

;------------------
;NEXT_LVL -- SELECT THE NEXT LEVEL

NEXT_LVL	MODULE

	PHP
	SET_AXY16
;-----
	LDA	GAME_LEVEL
	CLC
	ADC	#1
	CMP	#SUB_LVLS	;TOTAL FOR NOW ?
	BNE	@LVL_OK
	LDA	#0
@LVL_OK	STA	GAME_LEVEL	;SET NEW LEVEL
	JSL	DO_MAIN_MENU
;-----
	PLP
	RTL


	MODEND

;------------------
;PREV_LVL -- SELECT THE PREVIOUS LEVEL

PREV_LVL	MODULE

	PHP
	SET_AXY16
;-----
	LDA	GAME_LEVEL
	SEC
	SBC	#1
	BCS	@LVL_OK
	LDA	#SUB_LVLS-1
@LVL_OK	STA	GAME_LEVEL	;SET NEW LEVEL
	JSL	DO_MAIN_MENU
;-----
	PLP
	RTL


	MODEND

;------------------
;CENTER_TEXT -- CALC THE OFFSET FOR CENTERING [TEXT_ADDR]

CENTER_TEXT	MODULE

	PHP
	SET_A8
	SET_XY16
;-----
	LDY	#0
@CENTER_LP	LDA	[TEXT_ADDR],Y
	CMP	#0
	BEQ	@TEXT_END
	INY
	JMP	@CENTER_LP
;
@TEXT_END	SET_A16
	STY	TEMP_X	;SAVE WIDTH
	LDA	#32
	SEC
	SBC	TEMP_X
	LSR	A
	TAX		;SAVE RESULT
;-----
	PLP
	RTL

	MODEND

;------------------
;------------------
;PRINT_TEXT -- PRINT OUT A TEXT STRING TO SCRN_BUF
;
;	X - SCRN X
;	Y - SCRN Y 
;
;	ADDRESS OF TEXT STRING IS IN TEXT_ADDR.
;	NEED TO SET TEXT_PAL.
;	NEED TO SET CHAR_BASE BEFORE USING FOR FIRST TIME.

PRINT_TEXT	MODULE

	PHP
	PHB
	SET_A8
	LDA	#^SCRN_BUF	;GET PAGE OF SCRN_BUF
	PHA
	PLB		;SET DATA BANK TO IT
	SET_AXY16
;-----
	TYA		;GET SCRN Y
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A
	ASL	A	;Y x 32 x 2
	STA	TEXT_PTR
	CLC
	TXA
	ASL	A
	ADC	TEXT_PTR
	STA	TEXT_PTR
	TAX		;SET DEST POINTER
;-----
	LDY	#0
	SET_A8
@LOOP1	LDA	[TEXT_ADDR],Y	;GET NEXT CHARACTER
	INY
	CMP	#0	;END OF TEXT ?
	BEQ	@TEXT_END
	CMP	#13	;CARRIAGE RETURN ?
	BEQ	@DO_RETURN
	JSR	CHAR_8X8	;PRINT THE CHARACTER
	JMP	@LOOP1
;-----
@DO_RETURN	SET_A16
	LDA	TEXT_PTR	;GET START OF THIS ROW
	CLC
	ADC	#32*2	;NEXT ROW
	STA	TEXT_PTR
	TAX
	SET_A8
	JMP	@LOOP1
;-----
@TEXT_END:
	PLB
	PLP
	RTL

	MODEND

;------------------
;CHAR_8X8 -- DRAW AN 8X8 CHAR
;
;	ACC 	- CHAR #
;	X	- DEST OFFSET

CHAR_8X8	MODULE

	PHP
	SEC
	SBC	#32	;ADJUST TO 0
	SET_A16
	AND	#$00FF	;CLEAR HIGH BYTE
	CLC
	ADC	CHAR_BASE	;ADD FIRST CHAR # FOR TEXT
	ORA	TEXT_PAL	;ADD IN PAL FLIP AND PRIORITY
	STA	SCRN_BUF,X	;PRINT THE CHAR
	INX
	INX		;NEXT CHAR
	PLP
	RTS

	MODEND

;------------------
;------------------
;------------------

;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
