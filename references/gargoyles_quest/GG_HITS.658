;------------------
;
;GG_HITS.658
;
;  COLLISION DETECTION CODE
;
;------------------
; 
;  THERE ARE TWO MAIN TYPES OF HIT BOXES THAT ARE FOUND
;IN ANIMS:
;
;1) CODE = $00 -- THIS IS A BOX THAT DESCRIBES THE AREA OF THE ANIM
;THAT CAN SUSTAIN DAMAGE FROM ANOTHER OBJECTS TYPE 2 HIT
;
;2) CODE = $01 -- THIS BOX IS ONLY FOND ON FRAMES OF AN ANIM WHERE
;THAT OBJECT CAN INFLICT DAMAGE ON ANOTHER
;
;AFTER SETING UP THE ANIMS FOR THE NEXT DMA THE COLLISION ROUTINES
;WILL DO THE FOLLOWING:
;
;1) SEE IF THE MAIN OBJECT HAS A TYPE $01 HIT BOX
;	- IF IT DOES THEN COMPARE IT TO ALL OTHER OBJECTS
;	TYPE $00 HIT BOXES. IF A COLLISION HAS OCCURED THEN
;	CALL THE HIT ROUTINE OF THE DAMAGED OBJECT.
;
;2) SEE IF ANY OF THE ENEMY OBJECTS HAVE TYPE $01 HIT BOXES
;	- IF SO THEN COMPARE THEM TO THE TYPE $00 HIT BOX OF THE
;	MAIN OBJECT. IF THERE IS A HIT THEN CALL THE HIT ROUTINE
;	OF THE MAIN OBJECT. 
;
;------------------
;EQUATES



;------------------
;CK_HITS -- 	1) SEE IF GOLIATH IS HITTING SOMETHING
;	2) SEE IF OTHER OBJECTS ARE HITTING GOLIATH

CK_HITS	MODULE

	PHP
	PHB
	SET_AXY16
;-----
;SEE IF GOLIATH HAS ANY HIT TABLE TO LOOK AT
	LDA	OBJ_HIT_TBL
	ORA	OBJ_HIT_TBL+2
	BNE	@HAS_TABLE
@CK_OTHERS_LK	JMP	@CK_OTHERS
;-----
;IF SO THEN SEE IF GOLIATH HAS ANY HIT BOXES IN THIS ANIM
@HAS_TABLE	LDA	OBJ_HIT_TBL
	STA	HIT_ADDR
	LDA	OBJ_HIT_TBL+2
	STA	HIT_ADDR+2
;
	LDY	#0
	LDA	[HIT_ADDR],Y
	INY
	INY
	CMP	#0        	;ANY HIT BOXES ?
	BEQ	@CK_OTHERS_LK	
	STA	HIT_BOXES	;SAVE # OF BOXES
;-----
;IF SO THEN SEE IF ANY OF THE HIT BOXES ARE TYPE $01
@LK_AGAIN	LDA	[HIT_ADDR],Y	;GET HIT BOX TYPE
	CMP	#1
	BEQ	@TYPE_01	;IS A TYPE $01
	TYA
	CLC
	ADC	#10	;TO NEXT HIT BOX
	TAY
	DEC	HIT_BOXES
	BNE	@LK_AGAIN
	JMP	@CK_OTHERS	;NONE LEFT TO CHECK
;-----
;SAVE THE HIT BOX DATA TO ZERO PAGE
@TYPE_01	INY
	INY
	LDA	[HIT_ADDR],Y	;X1
	CLC
	ADC	OBJ_X	;MAKE FINAL X1
	STA	HIT_X1
	INY
	INY
	LDA	[HIT_ADDR],Y	;X2
	CLC
	ADC	OBJ_X	;MAKE FINAL X2
	STA	HIT_X2
	INY
	INY
	LDA	[HIT_ADDR],Y	;Y1
	CLC
	ADC	OBJ_Y	;MAKE FINAL Y1
	STA	HIT_Y1
	INY
	INY
	LDA	[HIT_ADDR],Y	;Y2
	CLC
	ADC	OBJ_Y	;MAKE FINAL Y2
	STA	HIT_Y2
;-----
;NOW COMPARE THIS AREA TO ALL OTHER ACTIVE OBJECTS THAT HAVE A TYPE $00
	LDX	#4	;NOW LOOK AT OTHER OBJ
@LK_NEXT	LDA	OBJ_CONTROL,X	;IS THERE AN OBJ HERE ?
	CMP	#0
	BNE	@CK_THIS
@GOTO_NEXT	INX
	INX
	INX
	INX
	CPX	#OBJS*4	;LOOKED AT ALL OBJECTS ?
	BNE	@LK_NEXT
	JMP	@CK_OTHERS
;-----
;SEE IF THIS OBJECT HAS A HIT TABLE
@CK_THIS	STX	HIT_PTR	;SAVE POINTER FOR TEST
	LDA	OBJ_HIT_TBL,X
	ORA	OBJ_HIT_TBL+2,X	;IS THERE A HIT TABLE ?
	BEQ	@GOTO_NEXT	;NO SKIP THIS
	LDA	OBJ_HIT_TBL,X
	STA	HIT_ADDR
	LDA	OBJ_HIT_TBL+2,X
	STA	HIT_ADDR+2
	LDY	#0
	LDA	[HIT_ADDR],Y	;HOW MANY HIT BOXES ?
	BEQ	@GOTO_NEXT
	INY
	INY
	STA	HIT_BOXES	;SAVE # OF HIT BOXES
;-----
;SEE IF IT HAS A TYPE $00 HIT BOX	
@LK_AGAIN2	LDA	[HIT_ADDR],Y	;GET HIT BOX TYPE
	BEQ	@TYPE_00	;IS A TYPE $00
	TYA
	CLC
	ADC	#10	;TO NEXT HIT BOX
	TAY
	DEC	HIT_BOXES
	BNE	@LK_AGAIN2
	JMP	@GOTO_NEXT	;NONE LEFT TO CHECK
;-----
;SEE IF OVERLAP IN X
@TYPE_00	INY
	INY
	TXA
	LSR	A	;MAKE WORD PTR
	TAX
	LDA	[HIT_ADDR],Y	;GET X1B
	CLC
	ADC	OBJ_X,X	;MAKE FINAL X1B
	CMP	HIT_X2	;X1B > X2A ?
	BGE	@MISS
	INY
	INY
	LDA	[HIT_ADDR],Y	;GET X2B
	CLC
	ADC	OBJ_X,X	;MAKE FINAL X2B
	CMP	HIT_X1	;X2B < X1A ?
	BLT	@MISS
	INY
	INY
;-----
;SEE IF OVERLAP IN Y
	LDA	[HIT_ADDR],Y	;GET Y1B
	CLC
	ADC	OBJ_Y,X	;MAKE FINAL Y1B
	CMP	HIT_Y2	;Y1B > Y2A ?
	BGE	@MISS
	INY
	INY
	LDA	[HIT_ADDR],Y	;HET Y2B
	CLC
	ADC	OBJ_Y,X
	CMP	HIT_Y1	;Y2B < Y1A ?
	BLT	@MISS
;-----
;GOLIATH HAS HIT THIS OBJECT
	JSL	KILL_OPP	;TEST THIS
	JMP	@CK_OTHERS
;-----
;MISSED THIS ONE SO LOOK AT NEXT ONE
@MISS	LDX	HIT_PTR
	JMP	@GOTO_NEXT

;-----
;NOW CHECK THE OTHER OBJECTS TO SEE IF THEY NEED TO HIT SOMETHING
@CK_OTHERS

;-----
;



;-----
@EXIT	PLB
	PLP
	RTL

	MODEND

;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
