;------------------
;
;GG_PAL.658
;
;  PALETTE CONTROL ROUTINES
;
;
;------------------
;COLOR_CYCLES -- DO ANY COLOR CYCLES

COLOR_CYCLES	MODULE

	PHP
	SET_AXY16
;-----
	LDX	#0
@CC_LP1	LDA	CC_TYPE,X	;GET TYPE
	BNE	@CC_ACTIVE
@CC_CONTINUE	INX
	INX
	CPX	#CCS*2	;LOOK AT ALL AVAILABLE COLOR CYCLES
	BNE	@CC_LP1
;-----
	PLP
	RTL
;-----
@CC_ACTIVE	DEC	CC_COUNT,X	;DEC COUNTER
	BNE	@CC_CONTINUE	;NOT YET
;-----
;RESET THE COUNTER
	LDA	CC_TIME,X	;GET COUNTER RESET VALUE
	STA	CC_COUNT,X	;RESET IT
;-----
;IF GLOBAL THEN GO DO THE CYCLE
;IF REGIONAL THEN SEE IF GOLIATH IN REGION
	LDA	CC_TYPE,X	;GET TYPE
	CMP	#1
	BEQ	@DO_CYCLE
	LDA	OBJ_X
	CMP	CC_X1,X	;GOLIATH < X1 ?
	BLT	@CC_CONTINUE
	CMP	CC_X2,X	;GOLIATH > X2 ?
	BGE	@CC_CONTINUE
	LDA	OBJ_Y,X
	CMP	CC_Y1	;GOLIATH < Y1 ?
	BLT	@CC_CONTINUE
	CMP	CC_Y2	;GOLIATH > Y2 ?
	BGE	@CC_CONTINUE
;-----
@DO_CYCLE	PHX		;SAVE CYCLE POINTER
	LDA	CC_DIR,X	;WHICH DIRECTION TO MOVE COLORS ?
	BEQ	@CYCLE_UP
	LDA	CC_START,X	;GET START POINTER
	TAY
	LDA	CG_DATA,Y	;GET BOTTOM COLOR
	PHA		;SAVE IT FOR END OF CYCLE
	LDA	CC_START,X	;GET START POINTER
	CLC
	ADC	#<CG_DATA	;ADD OFFSETO OF OAM_DATA AREA
	TAY		;= DEST POINTER
	LDA	CC_LENGTH	;GET LENGTH
	PHA		;SAVE IT
	TYA		;GET DEST
	CLC	
	ADC	#2	;ADD 2 FOR SOURCE
	TAX		;= SOURCE POINTER
	PLA		;RESTORE COUNT
	MVN	$0,$0	;MOVE OAM DATA
	PLA
	STA	$0,Y	;SET LAST WORD
	PLX
	SET_A8
	LDA	#1
	STA	CG_DMA_FLAG
	SET_A16
	JMP	@CC_CONTINUE
;-----
@CYCLE_UP	LDA	CC_START,X	;GET START POINTER
	CLC
	ADC	CC_LENGTH,X	;ADD LENGTH FOR LAST POSITION
	ADC	#<CG_DATA
	TAY
	LDA	$0,Y	;GET LAST WORD OF AREA
	PHA		;SAVE IT
	LDA	CC_LENGTH,X	;GET LENGTH
	PHA
	INY
	TYA
	SEC
	SBC	#2
	TAX
	PLA		;GET LENGTH
	MVP	$0,$0	;MOVE CG DATA
	PLA		;GET WORD AT TOP
	STA	$0,Y	;PUT AT BOTTOM
	PLX
	SET_A8
	LDA	#1
	STA	CG_DMA_FLAG
	SET_A16
	JMP	@CC_CONTINUE

	MODEND

;------------------
;CC_TEST -- TEST COLOR CYCLES

CC_TEST	MODULE

	PHP
	SET_AXY16
;-----
	LDA	#1
	STA	CC_TYPE
	LDA	#$10*2
	STA	CC_START
	LDA	#15*2
	STA	CC_LENGTH
	LDA	#30
	STA	CC_COUNT
	STA	CC_TIME
	LDA	#1
	STA	CC_DIR
	LDA	#100
	STA	CC_X1
	STA	CC_Y1
	LDA	#200
	STA	CC_X2
	STA	CC_Y2
;-----
	PLP
	RTL

	MODEND

;------------------
;READ_CG -- READ THE CG DATA

READ_CG	MODULE

	PHP
	SET_A8
	SET_XY16
;-----
	LDA	#0
	PHA
	PLB
	STA	CGADD
;
	LDX	#0
@CG_LP	LDA	RCGDATA
	STA	CG_DATA,X
	INX
	LDA	RCGDATA
	AND	#$7F
	STA	CG_DATA,X
	INX
	CPX	#512
	BNE	@CG_LP
	PLP
	RTL

	MODEND

;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
;------------------
