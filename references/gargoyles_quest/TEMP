;DRAW_TITLE-- DRAW TITLE SCREEN MAP

DRAW_TITLE	MODULE

	SET_AXY16
	LDA	#$6800
	STA	VMADDL
	LDX	#96
	LDA	#0
@CLR_LP	SET_A8
	STA	WVMDATAH
	STA	WVMDATAL
	DEX
	BNE	@CLR_LP
;-----
	SET_A16
	LDA	#$6800+96
	STA	VMADDL
	LDX	#0	;MAP_X

	LDA	#16
	STA	SCRN_HEIGTH
@DS_LP0	PHX
	PHX
	LDA	#16	;BLOCK WIDTH OF SCREEN
	STA	SCRN_WIDTH
;
@DS_LP1	LDA	TTL_MAP,X	;GET MAP DATA
	TAY
	AND	#$3C00
	STA	MAP_PC	;SAVE PRIORITY AND COLOR
	TYA
	AND	#$C000
	STA	MAP_FB	;SAVE FLIP BITS
	TYA
	AND	#$03FF	;JUST INDEX DATA
	ASL
	ASL
	ASL
	TAY
	LDA	MAP_FB	;TEST FOR X FLIP
	BIT	#$4000
	BNE	@XFLIPTOP
;
	LDA	TTL_BLOCK,Y	;GET TOP LEFT CHAR#
	ORA	MAP_PC	;ADD INFO BITS
	EOR	MAP_FB
	SET_A8
	XBA
	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
;
	LDA	TTL_BLOCK+2,Y	;GET TOP RIGHT
	ORA	MAP_PC
	EOR	MAP_FB
	SET_A8
	XBA
            	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
	JMP	@DS100
;
@XFLIPTOP	LDA	TTL_BLOCK+2,Y	;GET TOP RIGHT
	ORA	MAP_PC
	EOR	MAP_FB
	SET_A8
	XBA
            	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
;
	LDA	TTL_BLOCK,Y	;GET TOP LEFT CHAR#
	ORA	MAP_PC	;ADD INFO BITS
	EOR	MAP_FB
	SET_A8
	XBA
	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
;
@DS100	INX
	INX
	DEC	SCRN_WIDTH
	BEQ	@DS_110
	JMP	@DS_LP1
;
@DS_110	PLX
;
	LDA	#16	;BLOCK WIDTH OF SCREEN
	STA	SCRN_WIDTH
@DS_LP2	LDA	TTL_MAP,X	;GET MAP DATA
	TAY
	AND	#$3C00
	STA	MAP_PC	;SAVE PRIORITY AND COLOR
	TYA
	AND	#$C000
	STA	MAP_FB	;SAVE FLIP BITS
	TYA
	AND	#$03FF	;JUST INDEX DATA
	ASL
	ASL
	ASL
	TAY
	LDA	MAP_FB	;TEST FOR X FLIP
	BIT	#$4000
	BNE	@XFLIPBOT
;
	LDA	TTL_BLOCK+4,Y	;GET BOTTOM LEFT CHAR#
	ORA	MAP_PC	;ADD INFO BITS
	EOR	MAP_FB
	SET_A8
	XBA
	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
;
	LDA	TTL_BLOCK+6,Y	;GET BOTTOM RIGHT
	ORA	MAP_PC
	EOR	MAP_FB
	SET_A8
	XBA
            	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
	JMP	@DS200
;
@XFLIPBOT	LDA	TTL_BLOCK+6,Y	;GET BOTTOM RIGHT
	ORA	MAP_PC
	EOR	MAP_FB
	SET_A8
	XBA
            	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
;
	LDA	TTL_BLOCK+4,Y	;GET BOTTOM LEFT CHAR#
	ORA	MAP_PC	;ADD INFO BITS
	EOR	MAP_FB
	SET_A8
	XBA
	STA	WVMDATAH	;STORE IT
	XBA
	STA	WVMDATAL
	SET_A16
;
@DS200	INX
	INX
	DEC	SCRN_WIDTH
	BNE	@DS_LP2
;
	PLA
	CLC
	ADC	#32	;NEXT ROW
	TAX
	DEC	SCRN_HEIGTH
	BEQ	@DS_DONE
	JMP	@DS_LP0
@DS_DONE	RTS

	MODEND


