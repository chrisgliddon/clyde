;------------------
;
;GG_OBJ.658
;
;  OBJECT HANDLING ROUTINES
;
;
;------------------
;EQUATES






;------------------
;ENEMY TABLES FOR EACH LEVEL

ENEMIES	DL	L1_ENEMIES
	DL	L2_ENEMIES
;	DL	L3_ENEMIES
;	DL	L4_ENEMIES
;	DL	L5_ENEMIES
;	DL	L6_ENEMIES
;	DL	L7_ENEMIES

L1_ENEMIES	DL	INIT_VI	;INIT ROUTINE FOR ID # 1
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 2
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 3
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 4
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 5
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 6
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 7
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 8
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 9
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 10
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 11
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 12
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 13
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 14
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y
	DL	INIT_NULL	;INIT ROUTINE FOR ID # 15
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y


L2_ENEMIES	DL	INIT_VI	;INIT ROUTINE FOR ID # 1
	DW	-16,-32	;X,Y OFFSET FROM ENEMY X,Y



;------------------
;OBJ_HANDLER -- EXECUTE OBJECT CONTROL ROUTINES

OBJ_HANDLER	MODULE

	PHP
	SET_AXY16
;-----
	LDX	#0	;START WITH MASTER
	LDY	#0	;WORD PTR
OH_LP	LDA	OBJ_CONTROL,X	;GET OFFSET
	CMP	#0
	BNE	OH_FOUND
;-----
;CALL THE SCROLL HANDLER IF ANY
OH_NEXT	CPX	#0	;1ST OBJECT ?
	BNE	@NOT_1ST	;NOPE!
;-----
;MAKE THE DELAT X AND DELTA Y
	LDA	OBJ_X	;GET CURRENT X
	SEC
	SBC	OBJ_DX	;SUBTRACT LAST X
	BCS	@DXOK
     	EOR	#$FFFF	;MAKE IT POSITIVE
	INC	A
@DXOK	STA	OBJ_DX	;MAKE DELTA X
;-----
	LDA	OBJ_Y	;GET CURRENT Y
	SEC
	SBC	OBJ_DY	;SUBTRACT LAST Y
	STA	OBJ_DY	;MAKE DELTA Y
	BCS	@DYOK
	EOR	#$FFFF	;MAKE IT POSITIVE
	INC	A
@DYOK	STA	ABS_DY	;ABSOLUTE VALUE OF DELTA Y
;-----
;IF THIS IS OBJECT #0 THEN DO THE SCROLL ROUTINE IF ANY
;THIS WILL SET THE MAP X,Y TO A NEW LOCATION AND ALIGN
;ANY OTHER OBJECTS TO THEIR CORRECT SCROLL LOCATION ON 
;THE SCREEN
	PHX
	PHY
	LDA	SCRL_HANDLER	;SEE IF THERE IS ONE
	BEQ	@RETURN2
          	PHK		;PUSH PROGRAM BANK
	PER	@RETURN2-1	;SAVE RETURN ADDRESS
	SET_A8
	LDA	SCRL_HANDLER+2	;GET BANK
	PHA		;PUSH PROGRAM BANK ADDRESS
	SET_A16
	LDA	SCRL_HANDLER	;GET ADDRESS OF ROUTINE
	SEC
	SBC	#1
	PHA		;PUSH ADDRESS
	RTL		;GO THERE NOW
;-----
;RETURN FROM OBJ_CONTROL ROUTINE HERE
@RETURN2	PLY
	PLX
;	
@NOT_1ST	INX
	INX
	INX
	INX
	INY
	INY
	CPX	#OBJS*4	;LOOK AT ALL OF THEM ?
	BNE	OH_LP
	PLP
	RTL
;-----
OH_FOUND	CPX	#0	;IS THIS GOLIATH ?
	BNE	OH_DO
	LDA	OBJ_X
	STA	OBJ_DX	;SAVE CURRENT X FOR SCROLL ROUTINE
	LDA	OBJ_Y
	STA	OBJ_DY	;SAVE CURRENT Y FOR SCROLL ROUTINE
;-----
;LOAD THE SPECIAL TILE ARRAY FOR GOLIATH
;	PHX
;	PHY
;	JSL	READ_SPECIAL
;	PLY
;	PLX
;-----
OH_DO	STX	OBJ_DWORD	;SAVE OBJ POINTERS
	STY	OBJ_WORD
;-----
          	PHK		;PUSH PROGRAM BANK
	PER	@RETURN-1	;SAVE RETURN ADDRESS
	SET_A8
	LDA	OBJ_CONTROL+2,X	;GET BANK
	PHA		;PUSH PROGRAM BANK ADDRESS
	SET_A16
	LDA	OBJ_CONTROL,X	;GET ADDRESS OF ROUTINE
	SEC
	SBC	#1
	PHA		;PUSH ADDRESS
	RTL		;GO THERE NOW
;-----
;RETURN FROM OBJ_CONTROL ROUTINE HERE
@RETURN	SET_AXY16
;-----
	LDX	OBJ_DWORD	;RESTORE OBJ POINTERS
	LDY	OBJ_WORD
;
	JMP	OH_NEXT

	MODEND

;------------------
;MASTER_OBJ -- INITIALIZE THE MASTER OBJECT THAT THE SCROLL FOLLOWS

MASTER_OBJ	MODULE

	PHP
	SET_AXY16
	LDX	#0	;MASTER OBJ
	JMP	IO_FOUND	;INIT INTO SLOT 0

	MODEND

;------------------
;INIT_OBJ -- INITIALIZE A NEW OBJECT
;
;LOOK FOR AN EMPTY SLOT ( OBJ_CONTROL = $000000 )
;SET OBJ_FLAG TO ZERO
;CALL OBJECT HANDLER

INIT_OBJ	MODULE

	PHP
;-----
	SET_AXY16
	LDX	#4	;SKIP MASTER OBJECT
@IO_LP	LDA	OBJ_CONTROL,X	;GET OFFSET
	CMP	#0
	BEQ	IO_FOUND
	INX
	INX
	INX
	INX
	CPX	#OBJS*4	;LOOK AT ALL OF THEM ?
	BNE	@IO_LP
	JMP	IO_NO_ROOM
;-----
IO_FOUND	LDA	NEW_OBJ_CTRL	;CONTROL ROUTINE
	STA	OBJ_CONTROL,X
	LDA	NEW_OBJ_CTRL+2
	STA	OBJ_CONTROL+2,X
;
	LDA	NEW_OBJ_HIT	;HIT CONTROL ROUTINE
	STA	OBJ_HIT,X
	LDA	NEW_OBJ_HIT+2
	STA	OBJ_HIT+2,X
;
	LDA	NEW_OBJ_ANIM	;ANIM ADDRESS
	STA	OBJ_ANIM,X
	LDA	NEW_OBJ_ANIM+2
	STA	OBJ_ANIM+2,X
;
	TXA
	LSR	A	;WORD PTR
	TAY
;
	LDA	NEW_OBJ_X	;SAVE X
	STA	OBJ_X,Y
	LDA	NEW_OBJ_Y	;SAVE Y
	STA	OBJ_Y,Y
;
	STZ	OBJ_FLAG,X	;CLEAR FLAG
	STX	OBJ_DWORD	;SAVE POINTERS
	STY	OBJ_WORD
;-----
;CALL THE CONTROL ROUTINE
	PHK		;PUSH PROGRAM BANK
	PER	@RETURN-1	;SAVE RETURN ADDRESS
	SET_A8
	LDA	OBJ_CONTROL+2,X	;GET BANK
	PHA		;PUSH PROGRAM BANK ADDRESS
	SET_A16
	LDA	OBJ_CONTROL,X	;GET ADDRESS OF ROUTINE
	SEC
	SBC	#1
	PHA		;PUSH ADDRESS
	RTL		;GO THERE NOW
;-----
@RETURN	SET_AXY16
;-----
IO_NO_ROOM	PLP
	RTL


	MODEND

;------------------
;CLR_OBJ_SPRITES -- CLEAR ALL THE SUBSPRITES OF AN OBJECT OFF THE SCREEN
;
;	ENTER WITH OBJ # IN X

CLR_OBJ_SPRITES	MODULE

	PHP
	SET_AXY8
;-----
	LDA	OBJ_MAX_SPR,X	;GET # OF SPRITE USED
	INC	A	;1 MORE THAN NEEDED
	TAY
	LDA	OBJ_SPRITE,X	;GET STARING SPRITE #
	STA	THIS_SPRITE
	JMP	@200
;-----
@100	PHY
	LDA	THIS_SPRITE
	SET_AXY16
	AND	#$00FF
	ASL	A
	ASL	A
	TAX
	SET_A8
	LDA	#$20
	STA	OAM_DATA,X
	SET_XY8
	LDY	THIS_SPRITE_MSB
	LDX	THIS_SPRITE_BYTE
	LDA	OBJ_MASK,X	;GET MASK FOR THIS SPRITE
	ORA	OAM_DATA+512,Y	;SET THE MSB
	STA	OAM_DATA+512,Y
	PLY
	INC	THIS_SPRITE	;NEXT SPRITE
	LDA	THIS_SPRITE
@200	LSR	A
	LSR	A
	STA	THIS_SPRITE_MSB	;SAVE MSB POINTER
	LDA	THIS_SPRITE
	AND	#3
	STA	THIS_SPRITE_BYTE	;SET BYTE POSITION
	DEY
	BNE	@100
;-----
	PLP
	RTL

	MODEND

;------------------
;ENEMY_DETECT -- SEE IF ANY NEW OBJECT TILES MOVED ONTO THE SCREEN

ENEMY_DETECT	MODULE

	PHP
	SET_AXY16
;-----
	LDA	OBJ_DX		;MOVED IN X ?
	BNE	@MOVED_IN_X
@NO_X_OBJ	JMP	@CHECK_Y
;-----
@MOVED_IN_X	BIT	#$8000		;MOVING LEFT ?
	BNE	@LOOK_LEFT
	LDA	MAP1_X
	CLC
	ADC	#21*16		;ADD 21 TILES TO RIGHT
	CMP	RIGHT_EDGE1		;OFF RIGHT EDGE OF MAP ?
	BGE	@NO_X_OBJ
	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_X / 16
	STA	ENEMY_X
	TAY
;
	LDA	MAP1_Y
	SEC
	SBC	#6*16		;TILES ABOVE THE SCREEN TO START
	BCS	@RIGHT_TOP_OK
	LDA	#0		;MAX THE TOP
@RIGHT_TOP_OK	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_Y / 16
;
@TEST_X	STA	ENEMY_Y		;SAVE STARTING TILE Y
	LDX	MAP1_WIDTH
	JSL	CALC_OFF		;MAP OFFSET
	LDX	#28		;# OF TILES TO CHECK
@CK_OBJ_X	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	CLC
	ADC	#3	;SKIP COUNTOUR & SPECIAL
	TAY
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	BNE	@FOUND_INX
	SET_A16
@CONT_IN_X	LDA	MAP_OFF
	CLC
	ADC	MAP1_WIDTH
	ADC	MAP1_WIDTH	;NEXT MAP ROW
	INC	ENEMY_Y	;NEXT TILE Y
	STA	MAP_OFF
	DEX
	BNE	@CK_OBJ_X
	JMP	@CHECK_Y
;-----
;FOUND AN OBJECT TILE
	LONGA	OFF
@FOUND_INX	STA	ENEMY_ID	;SAVE OBJ ID#
	SET_A16
	JSR	INIT_ENEMY	;TURN THIS ENEMY ON
	JMP	@CONT_IN_X
;-----
@LOOK_LEFT	LDA	MAP1_X
	SEC
	SBC	#6*16		;6 TILES TO THE LEFT
	BCC	@CHECK_Y
	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_X / 16
	STA	ENEMY_X
	TAY
;
	LDA	MAP1_Y
	SEC
	SBC	#6*16		;TILES ABOVE THE SCREEN TO START
	BCS	@LEFT_TOP_OK
	LDA	#0		;MAX THE TOP
@LEFT_TOP_OK	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_Y / 16
	JMP	@TEST_X		;GO TEST IT NOW
;-----
@CHECK_Y	LDA	OBJ_DY		;DID IT MOVE UP OR DOWN ?
	BNE	@MOVED_IN_Y
	PLP
	RTL
;-----
@MOVED_IN_Y	BIT	#$8000		;MOVING UP ?
	BNE	@LOOK_UP
	LDA	MAP1_X
	SEC
	SBC	#6*16		;6 TILES TO LEFT
	BCS	@BOT_OK
	LDA	#0		;START AT ZERO
@BOT_OK	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_X / 16
	STA	ENEMY_X
	TAY
;
	LDA	MAP1_Y
	CLC
	ADC	#21*16		;21 TILE BELOW
	CMP	MAP1_HEIGTH		;OFF THE MAP ?
	BGE	@NO_Y_OBJ
	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_Y / 16
;
@TEST_Y	STA	ENEMY_Y
	LDX	MAP1_WIDTH
	JSL	CALC_OFF		;MAP OFFSET
	LDX	#28		;# OF TILES TO CHECK
@CK_OBJ_Y	LDY	MAP_OFF
	LDA	[MAP1_IN_RAM],Y	;GET MAP DATA
	AND	#$03FF	;JUST INDEX DATA
	ASL	A
	ASL	A
	CLC
	ADC	#3	;SKIP COUNTOUR & SPECIAL
	TAY
	LDA	#0	;CLEAR WORD
	SET_A8
	LDA	[FLR_IN_RAM],Y	;GET SPECIAL DATA
	BNE	@FOUND_INY
	SET_A16
@CONT_IN_Y	LDA	MAP_OFF
	CLC
	ADC	#2
	STA	MAP_OFF	;OVER 1 TILE IN MAP
	INC	ENEMY_X	;NEXT ENEMY X
	DEX
	BNE	@CK_OBJ_Y
@NO_Y_OBJ	PLP
	RTL
;-----
	LONGA 	OFF
@FOUND_INY	STA	ENEMY_ID	;SAVE OBJ TILE #
	SET_A16
	JSR	INIT_ENEMY
	JMP	@CONT_IN_Y
;-----
@LOOK_UP	LDA	MAP1_X
	SEC
	SBC	#6*16		;6 TILES TO THE LEFT
	BCS	@TOP_OK
	LDA	#0
@TOP_OK	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_X / 16
	STA	ENEMY_X
	TAY
;
	LDA	MAP1_Y
	SEC
	SBC	#6*16		;TILES ABOVE THE SCREEN TO START
	BCC	@NO_Y_OBJ
	LSR	A
	LSR	A
	LSR	A
	LSR	A		;TILE # = MAP1_Y / 16
	JMP	@TEST_Y		;GO TEST IT NOW

	MODEND

;------------------
;INIT_ENEMY -- INITIALIZE THIS ENEMY IF NOT ALREADY ACTIVE
;
;1) SEE IF THIS ENEMY ID IS ALREADY ON
;2) IF NOT THEN LOOK FOR AN OPEN SPOT TO STORE THIS ENEMY
;3) GET AND SAVE THE OFFSET X,Y
;4) CALL THE INIT ROUTINE FOR THIS ID# WITH OBJECT # IN X

INIT_ENEMY	MODULE

	PHX
	PHY
;-----
	SET_AXY8
	LDA	#^ENEMY_STATE		;SET BANK FOR ENEMY DATA
	PHA
	PLB
;-----
	LDX	ENEMY_ID		;GET ID #
	LDA	ENEMY_STATE,Y		;GET STATE DATA
	CMP	#0		;NOT ACTIVE ?
	BEQ	@CAN_INIT
         	JMP	@IE_ABORT
;-----
	SET_A16
@CAN_INIT	LDX	#4		;SKIP GOLIATH
@IE_LOOP1	LDA	OBJ_CONTROL,X		;GET OBJECTS ID #
	BEQ	@FOUND_EMPTY
	INX
	INX
	INX
	INX
	CPX	#OBJS*4		;LOOKED AT ALL THE OBJECTS ?
	BNE	@IE_LOOP1
	JMP	@IE_ABORT
;-----
@FOUND_EMPTY	SET_A8
	LDY	ENEMY_ID		;SET THE ENEMY STATE TO ACTIVE
	TXA
	LSR	A
	LSR	A		; / 4 FOR OBJ #
	STA	ENEMY_STATE,Y		;SAVE THE OBJECT # THAT THIS ENEMY IS USING
;-----
;GET ADDRESS OF THIS LEVELS ENEMIES TABLE
	LDA	#^ENEMIES
	PHA
	PLB
	SET_AXY16
	LDA	GAME_LEVEL		;GET LEVEL #
	ASL	A
	ASL	A		;# * 4
	TAX
	LDA	ENEMIES,X		;GET LSW
	STA	FRAME_ADDR
	LDA	ENEMIES+2,X		;GET MSW
	STA	FRAME_ADDR+2
;-----
;ADD OFFSET TO THE ENEMY ID #
	LDA	#0
	SET_A8
	LDA	ENEMY_ID	
	DEC	A
	SET_A16
	ASL	A
	ASL	A
	ASL	A		;8 BYTES PER ENEMY
	CLC
	ADC	FRAME_ADDR
	STA	FRAME_ADDR		;ADD TO LSW
	LDA	FRAME_ADDR+2
	ADC	#0
	STA	FRAME_ADDR+2		;ADD TO MSW
;-----
;NOW GET THE X,Y OFFSET AND INIT ADDRESS
	LDY	#4		;SKIP ADDRESS FOR NOW
	LDA	[FRAME_ADDR],Y		;GET X OFFSET
	STA	ENEMY_XOFF
	LDY	#6
	LDA	[FRAME_ADDR],Y		;GET Y OFFSET
	STA	ENEMY_YOFF
	LDY	#2
;-----
;CALL THE CONTROL ROUTINE
	PHK		;PUSH PROGRAM BANK
	PER	@RETURN-1	;SAVE RETURN ADDRESS
	LDA	[FRAME_ADDR],Y	;GET BANK
	SET_A8
	PHA		;PUSH PROGRAM BANK ADDRESS
	SET_A16	
	LDY	#0
	LDA	[FRAME_ADDR],Y	;GET ADDRESS OF ROUTINE
	SEC
	SBC	#1
	PHA		;PUSH ADDRESS
	RTL		;GO THERE NOW
;-----
@RETURN
;-----
@IE_ABORT	SET_AXY16
	PLY
	PLX
	RTS

	MODEND

;------------------
;------------------
;------------------
